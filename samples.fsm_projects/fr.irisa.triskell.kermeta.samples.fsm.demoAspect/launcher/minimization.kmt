/* $Id: minimization.kmt,v 1.6 2008-08-25 13:04:00 vmahe Exp $
 * Project    : fr.irisa.triskell.kermeta.samples.fsm.demoAspect
 * File       : minimization.kmt
 * License    : EPL
 * Copyright  : IRISA / INRIA / Universite de Rennes 1
 * -------------------------------------------------------------------
 * Creation date : 27 juil. 2005
 * Modified By : 
 *        Waqas Ahmed Saeed <wahmedsa@irisa.fr>
 *        Cyril Faucher <cfaucher@irisa.fr>
 * Description : 
 *       Launching Kermeta File for Finite State Machine minimization
 *		 Use main method as entering point for choosing the file to minimize and then launching the minimization
 */
@mainClass "fsm::Minimization"
@mainOperation "main"

package fsm;
require "platform:/resource/fr.irisa.triskell.kermeta.samples.fsm.demoAspect/metamodels/fsm_Helper.kmt"
require kermeta

using fsm
using kermeta::standard
//using kermeta::persistence

class Minimization
{
	reference equivalent_pairs : set Pair[0..*]  // The initial pairSet of the equivalence relation { F \ Q }� ^ F�
	reference all_input_pairs : set Pair[0..*]       // The QxQ set of possible pairs, where Q is the stateSet
	reference helper : AutomatonHelper
	
    operation main() : Void is do 
    
    	// Input automaton (non-determinist)
    	var input : FSM
		var output_path : String
		var output : FSM init FSM.new
		
		// Initialize attributes
	   	helper := AutomatonHelper.new 
		//input := Sampler.new.createSampleM1() // "../models/sampleM1min.automaton"
		
		input := helper.ask_emfmodel("Enter the EMF model of the automaton to minimize: ")
		output_path := stdio.read("Enter the output file where to store the minimal automaton:\nplatform:/resource/fr.irisa.triskell.kermeta.samples.fsm.demoAspect/models/") 
		output_path := "platform:/resource/fr.irisa.triskell.kermeta.samples.fsm.demoAspect/models/"+output_path
		
		stdio.writeln("input:"+ input.isVoid.toString + "tostring : "+ input.toString)

	   	// Initialize equivalent_pairs and possible all_input_pairs
	   	input.ownedState.each { p | input.ownedState.each { b | 
	   		// Check : (p,q) is in Eo, i.e either both are final states or both are NOT final states
	   		var isFinalLeft : Boolean init input.finalState.detect { e | p.name == e.name }!=void
	   		var isFinalB    : Boolean init input.finalState.detect { e | b.name == e.name }!=void
	   		// Also fill the all input pairs
	   		if find_one(all_input_pairs, p, b) == void then all_input_pairs.add(helper.createPair(p, b)) end
	   		if  ((isFinalLeft and isFinalB) or (not isFinalLeft and not isFinalB)) 
		      and
			      find_one(equivalent_pairs, p, b) == void
		    then 
		    	stdio.writeln("{"+p.name + ","+b.name+"} ,")
		   		equivalent_pairs.add(helper.createPair(p, b))
	   	    end
	   	    } } 
		stdio.writeln("Eo = Square{F-Q}^Square{F} is initialized")	   	     
	   	// Minimalize 
    	minimalize(input, output)
    	
    	//output.prettyprint
    	// We do not want to store combination
    	output.ownedState.each { s | s.combination.clear } 
		helper.dotprint(output)
		helper.saveEMFAutomaton(kermeta::persistence::EMFRepository.new, output, output_path, "http://www.kermeta.org/fsm")
    end


    /**
     * MINIMIZATION ALGORITHM
     * params : 
     *    - input : the non-minimalized automaton
     *    - output : the minimal automaton equivalent to input
     */
    operation minimalize(input : FSM, output : FSM) is do
    	// Remove not accessible states
    	remove_unavailable_states(input)
    	// final_equivalent_pairs : the smallest equivalent pair set
		var final_equivalent_pairs : Set<Pair> init Set<Pair>.new
		final_equivalent_pairs.addAll(equivalent_pairs)
		var old_equivalent_pairs : Set<Pair> init all_input_pairs
    	from var it : Iterator<Pair> init old_equivalent_pairs.iterator
    	until old_equivalent_pairs == final_equivalent_pairs
    	loop
    		old_equivalent_pairs := final_equivalent_pairs
    		// For each pair
    		old_equivalent_pairs.each { eqPair |
    			// For all letters of input, if eqPair is not valid, remove it
	    		if (isNotOwnedTransition(input, eqPair, old_equivalent_pairs) == true) then 
	    			// remove this pair from final eq. pairs (H)
	    			final_equivalent_pairs := old_equivalent_pairs
	    			var fp : Pair init find_one(final_equivalent_pairs, eqPair.left, eqPair.right)
	    			if (fp!=void) then
	    				if (fp.left.name!=fp.right.name) then // the twin pairs are always valid!
		    				final_equivalent_pairs.remove(fp)
		    			end 
	    			end
	    		end
    		}
    	end
    	stdio.writeln("Minimalize succeeded")
    	// Create the equivalent classes, which become the new states
    	createEquivalenceClasses(output, input.ownedState, old_equivalent_pairs)
    	stdio.writeln("Equivalence classes creation succeeded : " + output.ownedState.size.toString)
		output.ownedState.each { s | s.name := String.clone(helper.join(s.combination.collect{ a | a.name }))  }
		// Create the transition between the new states -- inputStates contains the links to their eq.class
    	createEquivalentTransitions(output.ownedState, input.ownedState, input.ownedTransition)
    	stdio.writeln("Transition set creation succeeded")
    	// Set the initial and the final states
    	output.initialState := input.initialState.combination.one
    	output.finalState.addAll(input.finalState.one.combination) 
    end     
               
    /** Find the pair in pairSet which equals (left, right) or (right, left) (xRy => yRx) */
    operation find_one(pairSet : Set<Pair>, left : State, right : State) : Pair is do
    	result := pairSet.detect { p | 
   		(p.left.name == left.name and p.right.name == right.name) or
   		(p.right.name == left.name and p.left.name == right.name) }
    end
    
    /** Remove the states to which no transition leads */
    operation remove_unavailable_states(a : FSM) is do
    
    var tmpState : State init State.new
    
		var as_seq : Sequence<State> init a.ownedState.reject { s | not a.ownedTransition.collect { t | t.target}.contains(s) and s != a.initialState}
		var at_seq : Sequence<Transition> init a.ownedTransition.reject { t | not as_seq.contains(t.target) }
		a.ownedTransition.clear
		a.ownedTransition.addAll(at_seq)
		a.ownedState.clear
		a.ownedState.addAll(as_seq)
    end
         
    /** 
     *  If the result state-pair ( T(pair.left), T(pair.right) ) does not belong to equivalent pairs then
     *  return true otherwise false
     *  params : 
     *    - automaton : the automaton in which we test the validity of the result state-pair
     *    - pair : the pair on which we apply each transition of automaton
     *    - equivalent_pairs : the equivalent_pairs in which we search the result state-pair
     */
    operation isNotOwnedTransition(automaton : FSM, pair : Pair, equivalent_pairs : Set<Pair>) : Boolean is do 
    	//  if there exists a letter a in the automaton such as T(pair.left, a), T(pair.right, a) belongs to distinct_pairs
    	//  "void" pair is allowed!
    	result := false
    	from var it : Iterator<String> init automaton.alphabet.iterator
    	until it.isOff or result == true
    	loop
    		var letter : String init it.next
    		var tleft : Transition init automaton.ownedTransition.detect { t | t.source.name==pair.left.name and t.input == letter }
    		var tright : Transition init automaton.ownedTransition.detect { t | t.source.name==pair.right.name and t.input == letter }    		
    		if (tleft!=void and tright!=void) then // empty word belongs to accepted words
    			if find_one(equivalent_pairs, tleft.target, tright.target) == void then
	    			result := true	    		
	    		end
	    	else if (tleft==void and tright!=void) or (tleft!=void and tright==void) then result:=true end
	    	end
    	end
    end
    
    /**
     * Principle of the creation of the equivalent classes :
     *     for each state of the initial automaton do :
     *           select all the pairs which first element is this state
     *                for each of these pairs, if right element is not "classed" yet, do :
     *                    * if a class does not exist for the left element, create one
     *                    * add the right element to the class of the left one
     *                end
     *     end
     */
    operation createEquivalenceClasses(output : FSM, stateSet : Set<State>, equivalent_pairs : Set<Pair>) is do
    	var eqClass : Set<State> init Set<State>.new
    	from var it : Iterator<State> init stateSet.iterator
    	until it.isOff
    	loop
    		var state : State init it.next
    		var news : State init State.new// eqClass as new state of output
	    	equivalent_pairs.select
	    	{ pair | pair.left.name == state.name }.each
	    	{ pair |
	    		// combination becomes an improvised "marker"
	    		// if it is void, it means that it does not belong to a eqclass yet
    			if (state.combination == void) then 
    				// create the eq. class and the state
	    			news.combination := Set<State>.new
	    			news.combination.add(pair.left)
	    			output.ownedState.add(news)
	    			// Mark state that is already added // we use combination to ease the transition computation
	    			state.combination := Set<State>.new 
	    			state.combination.add(news)
	    		end
	    		// Process the right element of the pair : add it to the eq.class of the left element!
	    		var sright : State init stateSet.detect { s | pair.right.name == s.name and s.combination == void }
    			if (sright != void) then
    				// Complete the eq. class
    				var found_eq : State init output.ownedState.detect{ c | c.combination.detect{ s| s.name==state.name }!=void }
    				found_eq.combination.add(pair.right) 
    				if (sright.combination == void) then
	    				sright.combination := Set<State>.new 
	    				sright.combination.add(found_eq)
	    			end
    			end
    		}
    	end
    end
    
    /** 
     * Create the transitions of the minimal automaton
     * params :
     *     - eqClassStateSet : the minimal automaton set of states
     *     - ownedState : the input automaton set of states
     *     - ownedTransition : the intput automaton set of transitions
     */
    operation createEquivalentTransitions(
    	eqClassStateSet : OrderedSet<State>, ownedState : OrderedSet<State>, ownedTransition : OrderedSet<Transition>) : Void is do
    	var aResult : OrderedSet<Transition> init OrderedSet<Transition>.new
    	
    	// for each eq-class
    	ownedState.each { nextInputState |
    		
    		// Get the eq.class to which the current state belongs
    		var nextEqClassState : State init nextInputState.combination.one 
    		
			// For each letter, Get the transition for which the current state is a source
			var nextTransitionSet : Sequence<Transition> init ownedTransition.select { t | t.source == nextInputState }
			
			// The target combination is the eq. class target of the new transition!
			nextTransitionSet.each { t |
				// Add this transition
				if aResult.detect {  rt | rt.source == nextEqClassState and rt.input == t.input } == void then
					var nextEqClassStateTarget : State init eqClassStateSet.detect { s | s.combination.detect{ cs | cs.name==t.target.name }!=void }
					var newTransition : Transition init helper.createTransition(nextEqClassState, nextEqClassStateTarget, t.input)
					aResult.add(newTransition)
				end
			}
    	}
    end
    
}

