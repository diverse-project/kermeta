package ex::stateMachine;
using kermeta::emfpersistence
using kermeta::standard
using kermeta::utils
using kermeta::io::StdIO => stdio
class StateMachineSlicer {
	reference sliceRoots : Bag<Object>
	reference requiredStates : State[0..*]

	operation initialise(requiredStates : bag State[0..*]) : StateMachineSlicer is do
		result := self
		self.sliceRoots := Bag<Object>.new
		self.requiredStates.addAll(requiredStates)
	end

	operation launch() is do
		self.requiredStates.each{theState | theState.visitToAddClasses(self) }
		self.requiredStates.each{theState | theState.visitToAddRelations(self) }
		onEnd()
	end

	operation onEnd() is do
		var repository : ResourceSet init ResourceSet.new
		var resource : Resource 
 		resource ?= repository.createResource("./slice.xmi", "./StateMachine.ecore")
 		
 		resource.getContents.addAll(sliceRoots)
 		resource.save(void)
	end
	operation addToRoot(obj : Object) is do
		var cont : Object init obj.container
		if(cont.isVoid.orElse{b | (not cont.isInstanceOf(SlicerVisitor)).orElse{b2 | 
			(not cont.asType(SlicerVisitor).sliced.isVoid).andThen{b3 | not cont.asType(SlicerVisitor).sliced}}}) then
			sliceRoots.add(obj)
		end
	end
}
