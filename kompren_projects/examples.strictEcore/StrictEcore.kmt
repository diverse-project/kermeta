package ecore;
using kermeta::emfpersistence
using kermeta::standard
using kermeta::utils
using kermeta::io::StdIO => stdio
class StrictEcore {
	reference sliceRoots : Bag<Object>
	reference requiredEClasss : EClass[0..*]

	operation initialise(requiredEClasss : bag EClass[0..*]) : StrictEcore is do
		result := self
		self.sliceRoots := Bag<Object>.new
		self.requiredEClasss.addAll(requiredEClasss)
	end

	operation launch() is do
		self.requiredEClasss.each{theEClass | theEClass.visitToAddClasses(self) }
		self.requiredEClasss.each{theEClass | theEClass.visitToAddRelations(self) }
		onEnd()
	end

	operation onEnd() is do
		var repository : ResourceSet init ResourceSet.new
		var resource : Resource 
 		resource ?= repository.createResource("./slice.xmi", "http://www.eclipse.org/emf/2002/Ecore")
 		
 		resource.getContents.addAll(sliceRoots)
 		resource.save(void)
	end
	operation addToRoot(obj : Object) is do
		var cont : Object init obj.container
		if(cont.isVoid.orElse{b | (not cont.isInstanceOf(SlicerVisitor)).orElse{b2 | 
			(not cont.asType(SlicerVisitor).sliced.isVoid).andThen{b3 | not cont.asType(SlicerVisitor).sliced}}}) then
			sliceRoots.add(obj)
		end
	end
}
