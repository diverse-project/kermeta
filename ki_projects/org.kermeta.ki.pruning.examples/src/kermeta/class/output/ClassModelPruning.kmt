package ex::classModel;
require kermeta
require "platform:/resource/org.kermeta.ki.pruning.examples/src/kermeta/class/ClassModel.ecore"
require "ClassModelPruningAspect.kmt"

using kermeta::standard

abstract class ClassModelPruning {
	attribute radius : Integer

	reference requiredClazzs : Clazz[0..*]

	reference addedClazzs : Bag<Clazz>

	operation initialise(requiredClazzs : bag Clazz[0..*], radius : Integer) : ClassModelPruning is do
		result := self
		self.addedClazzs := Bag<Clazz>.new
		self.radius := radius
		self.requiredClazzs.addAll(requiredClazzs)
	end

	operation launch() : Void is do
		var theClazzs  : Bag<Clazz> init Bag<Clazz>.new
		var theClazzs2 : Bag<Clazz> init Bag<Clazz>.new
		var currentRadius : Integer init 0

		requiredClazzs.each{theClazz | theClazzs.add(theClazz) }

		from true
		until (theClazzs.empty()) or currentRadius>radius
		loop
			theClazzs.each{theClazz |
				if(theClazz.visited.isVoid) then
					theClazz.visit(self, theClazzs2)
				end
			}

			theClazzs.clear()
			theClazzs2.each{theClazz | theClazzs.add(theClazz) }
			theClazzs2.clear()
			currentRadius := currentRadius + 1
		end

		addedClazzs.each{theClazz | theClazz.addClazzs(self) }
	end

	operation onClazzAdded(theClazz : Clazz) : Void is abstract
	operation onsuperClassesAdded(theClazzSrc : Clazz, theClazzTar : Clazz) : Void is abstract
}
