/* $Id:$ 
 * Creation : November 17, 2010
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            ablouin
 */
package ex::stateMachine;

require kermeta
require "StateMachine.kmt"
require "StateMachinePruning.kmt"

using kermeta::standard

aspect class State {
	attribute visited : Boolean
	
	operation visit(theStateMachinePruning : StateMachinePruning, theStates : Bag<State>) : Void is do
		visited := true
		theStateMachinePruning.onStateAdded(self)
		theStateMachinePruning.addedStates.add(self)
	end
	
	operation addRelations(theStateMachinePruning : StateMachinePruning) : Void is do
	end
}


aspect class OutputState {
	method visit(theStateMachinePruning : StateMachinePruning, theStates : Bag<State>) : Void is do
		super(theStateMachinePruning, theStates)
		outgoingTransitions.each{theTransition | theTransition.addStates(theStateMachinePruning, theStates) }
	end
	
	
	method addRelations(theStateMachinePruning : StateMachinePruning) : Void is do
		outgoingTransitions.each{theTransition |
			if(not theTransition.visited and theTransition.source.visited and theTransition.target.visited) then
				theTransition.visit(theStateMachinePruning)
			end
		}
	end
}



aspect class Transition {
	attribute visited : Boolean
	
	operation visit(theStateMachinePruning : StateMachinePruning) : Void is do
		self.visited := true
		theStateMachinePruning.onTransitionAdded(self)
		theStateMachinePruning.addedTransitions.add(self)
	end
	
	
	operation addStates(theStateMachinePruning : StateMachinePruning, theStates : Bag<State>) : Void is do
		if(not visited) then
			if(not source.visited) then theStates.add(source) end
			if(not target.visited) then theStates.add(target) end
		end
	end
}

