package ex::stateMachine;
require kermeta
require "platform:/resource/org.kermeta.ki.pruning.examples/src/kermeta/SM/StateMachine.ecore"
require "StateMachinePruning.kmt"
using kermeta::standard

aspect class OutputState {
	method visit(theStateMachinePruning : StateMachinePruning, theStates : Bag<State>) : Void is do
		super(theStateMachinePruning, theStates)
		self.outgoingTransitions.each{theTransition | theTransition.visit(theStateMachinePruning, theStates) }
	end

	method addTransitions(theStateMachinePruning : StateMachinePruning) : Void is do
		self.outgoingTransitions.each{theTransition |
			if(not theTransition.visited and (theTransition.target.isVoid or theTransition.target.visited)) then
				theTransition.visited := true
				theStateMachinePruning.onTransitionAdded(theTransition)
				theStateMachinePruning.addedTransitions.add(theTransition)
			end
		}
	end
}
aspect class Transition {
	attribute visited : Boolean

	operation visit(theStateMachinePruning : StateMachinePruning, theStates : Bag<State>) : Void is do
		if(not self.target.visited) then theStates.add(self.target) end
	end
}
aspect class State {
	attribute visited : Boolean

	operation visit(theStateMachinePruning : StateMachinePruning, theStates : Bag<State>) : Void is do
		self.visited := true
		theStateMachinePruning.onStateAdded(self)
		theStateMachinePruning.addedStates.add(self)
	end

	operation addTransitions(theStateMachinePruning : StateMachinePruning) : Void is do
	end
}
