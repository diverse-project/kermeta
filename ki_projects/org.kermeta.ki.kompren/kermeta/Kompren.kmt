/*
 * Creation : March 11, 2010
 * Licence  : EPL 
 * Copyright: INRIA Rennes, Triskell
 * Authors  : Arnaud Blouin
 */
@mainClass "kermeta::ki::kompren::Kompren"
@mainOperation "main"

package kermeta::ki::kompren;

require "platform:/resource/org.kermeta.ki.kompren/kermeta/ModelExtension.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/Pruner.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/Hand.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/Hierarcher.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/VisitableKermeta.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/InstrumentsSelector.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/InstrumentsCustomiser.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/ElementsDisplayer.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/Searcher.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/action/Prune.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/ZoomerMetamodel.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/instrument/UndoRedoManager.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/instrument/Scroller.kmt"

using kermeta
using kermeta::ki::malai::undo
using kermeta::ki::malai::widget
using kermeta::ki::malai::action
using kermeta::standard
using kermeta::ki::malai::interaction::event
using kermeta::ki::malai::instrument
using kermeta::persistence
using kermeta::language::structure
using kermeta::language::behavior
using kermeta::ki::malai::picking


class Kompren
{
	operation main() : Void is do
		var aui : MetamodelCanvas init MetamodelCanvas.new
		
		aui.initialise()
		extern org::kermeta::ki::kompren::KomprenFrameExtern.organise(aui)
		aui.setVisible(true)
		aui.refreshView
	end
}




class MetamodelCanvas inherits ActionHandler, UndoHandler {
	attribute metamodel : ModelingUnit
	
	attribute eltDisplayer : ElementsDisplayer
	
	attribute pruner : Pruner
	
	attribute hand : Hand
	
	attribute hierarcher : Hierarcher
	
	attribute flattener : Flattener
	
	attribute undoManager : UndoRedoManager
	
	attribute undoCollector : UndoCollector
	
	attribute selector : InstrumentsSelector
	
	attribute customiser : InstrumentsCustomiser
	
	attribute searcher : Searcher
	
	attribute zoomer : ZoomerMetamodel
	
	attribute scroller : Scroller
	
	attribute actionRegistry : ActionRegistry
	
	attribute toolbar : Panel
	
	attribute viewPanel : Panel
	


	
	operation initialise() : Void is do
//		loadMetamodel("/examples/example2.km")
//		loadMetamodel("/examples/example1.km")
		loadMetamodel("file:/home/ablouin/Bureau/gen/examples/Ecore.km")
//		loadMetamodel("/examples/framework.km")
//		loadMetamodel("/examples/UML.km")

		undoCollector     := UndoCollector.new 
		actionRegistry    := ActionRegistry.new
		
		undoCollector.initialise
		undoCollector.handlers.add(self)
		actionRegistry.initialise(undoCollector)
		actionRegistry.handlers.add(self)

		eltDisplayer	 := ElementsDisplayer.new
		hand			 := Hand.new
		hierarcher		 := Hierarcher.new
		zoomer			 := ZoomerMetamodel.new
		pruner      	 := Pruner.new
		flattener      	 := Flattener.new
		undoManager 	 := UndoRedoManager.new
		selector         := InstrumentsSelector.new
		customiser		 := InstrumentsCustomiser.new
		scroller         := Scroller.new
		searcher		 := Searcher.new
		
		eltDisplayer.initialise(actionRegistry)
		hand.initialise(actionRegistry)
		hierarcher.initialise(actionRegistry)
		zoomer.initialise(actionRegistry)
		flattener.initialise(actionRegistry)
		scroller.initialise(actionRegistry)
		customiser.pruner := pruner
		customiser.initialise(actionRegistry)
		pruner.initialise(actionRegistry)
		undoManager.initialise(actionRegistry)
		searcher.initialise(actionRegistry)
		
		pruner.metamodel := metamodel
		pruner.initialiseLinks()
		flattener.metamodel := metamodel
		flattener.initialiseLinks()
		hand.initialiseLinks()
		hierarcher.metamodel := metamodel
		hierarcher.initialiseLinks()
		searcher.initialiseWidgets()
		searcher.initialiseLinks()
		searcher.metamodel := metamodel
		
		eltDisplayer.metamodel := metamodel
		eltDisplayer.initialise(actionRegistry)
		selector.initialise(actionRegistry)
		selector.initialiseInstruments(pruner, flattener, hierarcher, hand)

		var eventManager : EventManager init EventManager.new
		eventManager.initialise()
		
		initialiseUI
		extern org::kermeta::ki::kompren::KomprenFrameExtern.initialise(self, metamodel, toolbar, viewPanel, selector.handButton)

		metamodel.setEventManager(eventManager)
		var model2view : Metamodel2ViewVisitor init Metamodel2ViewVisitor.new
		model2view.modelingUnit := metamodel
		metamodel.accept(model2view)
		metamodel.update()
		
		eltDisplayer.initialiseLinks()
		eltDisplayer.interimFeedback
		eltDisplayer.setIsActivated(true)
		zoomer.zoomable  := metamodel
		zoomer.metamodel := metamodel
		zoomer.initialiseLinks()
		zoomer.setIsActivated(true)
		scroller.panel  := viewPanel
		scroller.initialiseLinks()
		scroller.setIsActivated(true)
		customiser.picker := toolbar
		customiser.initialiseLinks()
		customiser.setIsActivated(true)	
		selector.initialiseLinks()
		selector.setIsActivated(true)
		pruner.metamodel := metamodel
		undoManager.undoCollector := undoCollector		
		undoCollector.handlers.add(undoManager)
		undoManager.initialiseLinks()
		undoManager.setIsActivated(true)
		searcher.setIsActivated(true)
		
		pruner.addEventable(metamodel)
		flattener.addEventable(metamodel)
		hand.addEventable(metamodel)
		hierarcher.addEventable(metamodel)
		eltDisplayer.addEventable(toolbar)
		selector.addEventable(toolbar)
		undoManager.addEventable(toolbar)
		customiser.addEventable(toolbar)
		zoomer.addEventable(metamodel)
		zoomer.addEventable(toolbar)
		scroller.addEventable(metamodel)
		scroller.addEventable(toolbar)
		searcher.addEventable(toolbar)
	end
	
	
	
	method updateUndo() : Void is do
		refreshView
	end
	
	
	
	operation initialiseUI() : Void is do
		toolbar    := Panel.new
		viewPanel  := Panel.new
		
		toolbar.initialiseEventManager(true)
		viewPanel.initialiseEventManager(false)

		extern org::kermeta::ki::kompren::KomprenFrameExtern.initialiseToolbar(toolbar, undoManager.undoButton, 
					undoManager.redoButton, selector.prunerButton, selector.flattenerButton, selector.hierarcherButton,
					selector.handButton, pruner.grayedButton, pruner.hideButton, pruner.radiusSpinner, pruner.cardCheckBox,
					eltDisplayer.opButton, eltDisplayer.attrButton, searcher.textField)
					
		toolbar.addComponent(undoManager.undoButton, false)
		toolbar.addComponent(undoManager.redoButton, false)
		toolbar.addComponent(pruner.hideButton, false)
		toolbar.addComponent(pruner.grayedButton, false)
		toolbar.addComponent(pruner.radiusSpinner, false)
		toolbar.addComponent(pruner.cardCheckBox, false)
		toolbar.addComponent(selector.prunerButton, false)
		toolbar.addComponent(selector.hierarcherButton, false)
		toolbar.addComponent(selector.flattenerButton, false)
		toolbar.addComponent(selector.handButton, false)
		toolbar.addComponent(eltDisplayer.attrButton, false)
		toolbar.addComponent(eltDisplayer.opButton, false)
		toolbar.addComponent(searcher.textField, false)
	end



	operation loadMetamodel(muURI: String) : Void is do
		var rep : EMFRepository init EMFRepository.new
		var res : EMFResource
		res ?= rep.getResource(muURI)
		res.load()
		metamodel ?= res.one
	end

	
	
	operation setVisible(visible : Boolean) : Void is do
		extern org::kermeta::ki::kompren::KomprenFrameExtern.setVisible(self, visible)
	end
	
	
	operation refreshView() : Void is do
		extern org::kermeta::ki::kompren::KomprenFrameExtern.refreshView(self)
	end
	
	
	
	method onActionAdded(action : Action) : Void is do
		refreshView
	end
	
	method onActionAborted(action : Action) : Void is do
		refreshView
	end 
	
	method onActionCancelled(action : Action) : Void is do
		refreshView
	end 
	
	method onActionExecuted(action : Action) : Void is do
		refreshView
	end 
}



class Metamodel2ViewVisitor inherits KermetaVisitor {
	attribute firstPass : Boolean
	
	reference modelingUnit : ModelingUnit

	reference classes : ClassDefinition[0..*]
	
	reference links : Property[0..*]
	

	method visitModelingUnit(mu: ModelingUnit): Void is do
		firstPass := true
		mu.packages.each{p | p.accept(self) }
		firstPass := false
		mu.packages.each{p | p.accept(self) }
	end
	
	
	method visitPackage(p: Package) : Void is do
		p.nestedPackage.each{p | p.accept(self)}
		p.ownedTypeDefinition.each{td | td.accept(self)}
	end
	
	
	method visitOperation(o: Operation) : Void is do
		var typeName : String init if(o.type.isInstanceOf(NamedElement)) then o.type.asType(NamedElement).name else "" end
		var paramTypeName 	: String

		extern org::kermeta::ki::kompren::view::ClassViewExtern.onOperationAdded(o.owningClass, o, o.name, typeName, o.isAbstract)
		
		o.ownedParameter.each{param |
			paramTypeName := if(param.type.isInstanceOf(NamedElement)) then param.type.asType(NamedElement).name else void end
			
			if(not paramTypeName.isVoid) then
				extern org::kermeta::ki::kompren::view::OperationViewExtern.onParameterAdded(o, param.name, paramTypeName)
			end
		}
	end


	
	method visitProperty(p: Property): Void is do
		if(p.type.isInstanceOf(Class)) then
			var cd : ClassDefinition init p.type.asType(Class).classDefinition

			if(cd.isKermetaPrimitiveType) then
				extern org::kermeta::ki::kompren::view::ClassViewExtern.onAttributeAdded(p.owningClass, p, p.name, cd.name)
			else	
				if(classes.contains(cd)) then
					var oppositeName : String init ""
					var oppositeCompo : Boolean init p.isComposite
					var oppositeCardString : String init ""
					
					if(not p.opposite.isVoid) then 
						oppositeName  := p.opposite.name 
						oppositeCompo := p.opposite.isComposite
						oppositeCardString := p.opposite.getCardinalityString
					end
					extern org::kermeta::ki::kompren::view::ClassDiagramViewExtern.onRelationAdded(modelingUnit, p, p.isComposite or oppositeCompo, p.owningClass, cd, 
												p.name, p.getCardinalityString, oppositeName, oppositeCardString, -1, oppositeCompo)
				end
			end
			
			links.add(p)
		else
			if(p.type.isInstanceOf(PrimitiveType)) then
				var pt : PrimitiveType init p.type.asType(PrimitiveType)
				
				extern org::kermeta::ki::kompren::view::ClassViewExtern.onAttributeAdded(p.owningClass, p, p.name, pt.name)
			else
				if(p.type.isInstanceOf(Enumeration)) then
					var enum : Enumeration init p.type.asType(Enumeration)
					extern org::kermeta::ki::kompren::view::ClassViewExtern.onAttributeAdded(p.owningClass, p, p.name, enum.name)
				end
			end
		end
	end
	
	
	
	method visitClassDefinition(c: ClassDefinition) : Void is do
		if(firstPass) then
			classes.add(c)
			extern org::kermeta::ki::kompren::view::ClassDiagramViewExtern.onEntityAdded(modelingUnit, c, c.isAspect, -1, c.name)
			c.entity2ViewMapping
		else
			c.ownedAttribute.each{p | p.accept(self)}
			c.ownedOperation.each{o | o.accept(self)}
			
			c.superType.each{t |
				if(t.isInstanceOf(Class)) then
					var cd : ClassDefinition init t.asType(Class).classDefinition
					
					if(classes.contains(cd)) then
						extern org::kermeta::ki::kompren::view::ClassDiagramViewExtern.onInheritanceAdded(modelingUnit, c, cd, -1)
					end
				end
			}
		end
	end
}
