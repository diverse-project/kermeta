/*
 * Creation : April 23, 2010
 * Licence  : EPL 
 * Copyright: INRIA Rennes
 * Authors  : Arnaud Blouin
 */
package kermeta::ki::kompren;


require "platform:/resource/org.kermeta.ki.kompren/kermeta/ModelExtension.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/action/PruneHierarchy.kmt"
require "platform:/resource/org.kermeta.ki.kompren/kermeta/instrument/ZoomerMetamodel.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/library/Press.kmt"


using kermeta::ki::malai::instrument
using kermeta::ki::malai::interaction
using kermeta::language::structure



class Hierarcher inherits Instrument
{
	reference metamodel : ModelingUnit
	
	reference zoomer : ZoomerMetamodel
	
	
	method initialiseLinks() : Void is do
		addLink(Press2GetHierarchy.new, false)
	end
}




class Press2GetHierarchy inherits PressLink
{
	method getActionClass() : Class is do
		result := PruneHierarchy
	end
	
	
	operation getObject() : Object is do
		var press : Press init interaction.asType(Press)
		var zoomer : ZoomerMetamodel init instrument.asType(Hierarcher).zoomer
		result := press.getPickableAt(zoomer.getZoomedX(press.px), zoomer.getZoomedY(press.py), press.source)
	end
	
	
	method updateAction() : Void is do
		if(not action.isVoid) then
			action.asType(PruneHierarchy).selection.add(getObject().asType(ClassDefinition))
		end
	end
	
	
	
	method isConditionRespected() : Boolean is do
		var obj : Object init getObject()
		result := not obj.isVoid and obj.isInstanceOf(ClassDefinition)
	end
	
	
	method createAction() : Void is do
		var prune      : PruneHierarchy init PruneHierarchy.new
		var hierarcher : Hierarcher init instrument.asType(Hierarcher)
		
		prune.initialise(instrument.actionRegistry)
		prune.metamodel    := hierarcher.metamodel
		prune.radiusEffect := 0
		prune.card0		   := false
		prune.viewPolicy   := PrunerViewPolicy.hide
		action             := prune
	end
}
