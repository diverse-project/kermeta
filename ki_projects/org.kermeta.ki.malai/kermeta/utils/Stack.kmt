/* $Id:$ 
 * Creation : February 4, 2010
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            ablouin
 */
@mainClass "kermeta::ki::malai::utils::Stack"


package kermeta::ki::malai::utils;


require kermeta

using kermeta::standard


/** This class implements a stack based on the Sequence collection. */
class Stack<T>
{
	/** The sequence that contains the elements of the stack. */
	attribute elements : Sequence<T>
	
	
	/** Pushes the given object into the stack.
		'object': the object to push into the stack */
	operation push(object : T) : Void is do
		elements.add(object)
	end
	
	/** Remove the object at the top of the stack and returns it.
		Returns the removed object or void if the stack is empty. */
	operation pop() : T is do
		if not isEmpty then
			result := elements.elementAt(elements.size-1)
			elements.removeAt(elements.size-1)
		end
	end
	
	
	/** Returns the object at the top of the stack or void if the stack is empty */
	operation peek() : T is do
		result := if not isEmpty  then elements.elementAt(elements.size-1) end
	end
	
	
	/** Removes all the objects of the stack. */
	operation clear() : Void is do
		var size : Integer init elements.size
		
		size.times { i | elements.removeAt(0) }
	end
	
	
	/** the size of the stack */
	operation size() : Integer is do
		result := elements.size
	end
	
	
	
	/** Checks if the stack is empty. */
	operation isEmpty() : Boolean is do
		result := elements.size==0
	end
}
