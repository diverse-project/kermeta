/*
 * Creation : February 22, 2010
 * Licence  : EPL 
 * Copyright:
 * Authors  : Arnaud Blouin
 */
package kermeta::ki::malai::interaction;


require kermeta
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/State.kmt"

using kermeta::standard
using kermeta::ki::malai::interaction::event
using kermeta::ki::malai::widget


abstract class StateMachine inherits EventHandler
{
	attribute activated : Boolean

	attribute states : State[0..*]
	
	reference initState : InitState
	
	reference currentState : State
	
	/** The general events manager that dispatches produced events. */
	reference eventManager : EventManager
	
	
	operation initialise(eventManager : EventManager) : Void is do
		self.eventManager := eventManager
		activated := true
		initState := InitState.new
		initState.initialise("init")
		addState(initState)
		reinit
		initStateMachine	
	end
	
	
	/**
	* Indicates if the state machine is running.
	*/
	operation isRunning() : Boolean is do
		result := activated and currentState!=initState
	end
	
	
	
	operation setActivated(activated : Boolean) : Void is do
		self.activated := activated
	end
	
	
	
	operation initStateMachine() : Void is abstract
	
	
	operation reinit() : Void is do
		currentState := initState
	end
	
	
	operation addState(state : State) : Void is do
		if(state!=void) then
			states.add(state)
			state.stateMachine := self
		end
	end

	
	
	operation executeTransition(t : Transition) : Void is do
		if(activated) then
    		do
    			t.action()
    			currentState.onOutgoing()
    			currentState := t.outputState
    			currentState.onIngoing()
    		rescue(ex : kermeta::exceptions::Exception) reinit end
    	end
	end

	
	
	method onTextChanged(textCompName : String, TextValue : String) : Void is do
		if(activated) then
			var stop : Boolean init false
			
			currentState.transitions.exists{ t |
				if(t.isInstanceOf(TextChangedTransition)) then
					var tct : TextChangedTransition init t.asType(TextChangedTransition)
	
					tct.textCompName := textCompName
					tct.text := TextValue
					
					if(t.isGuardRespected()) then
						stop := true
						executeTransition(t)
					end
				end
				
				stop
			}
		end
	end
	
	

	method onButtonPressed(button : Button) : Void is do
		if(activated) then
			var stop : Boolean init false
			var t : Transition
			
			currentState.transitions.exists{t |
				if(t.isInstanceOf(ButtonPressedTransition)) then
					t.asType(ButtonPressedTransition).button := button
					
					if(t.isGuardRespected) then
						stop := true
						executeTransition(t)
					end
				end
				
				stop
			}
		end		
	end
	
	

	method onKeyPressure(key : Integer, idHID : Integer) : Void is do
		if(activated) then
			var stop : Boolean init false
			var t : Transition
			
			currentState.transitions.exists{t |
				if(t.isInstanceOf(KeyPressureTransition)) then
					t.asType(KeyPressureTransition).key := key
					
					if(t.isGuardRespected) then
						stop := true
						executeTransition(t)
					end
				end
				
				stop
			}
		end
	end



	method onKeyRelease(key : Integer, idHID : Integer) : Void is do
		if(activated) then
			var stop : Boolean init false
			var t : Transition
			
			currentState.transitions.exists{t |
				if(t.isInstanceOf(KeyReleaseTransition)) then
					t.asType(KeyReleaseTransition).key := key
					
					if(t.isGuardRespected) then
						stop := true
						executeTransition(t)
					end
				end
				
				stop
			}
		end
	end



	method onMove(button : Integer, px : Real, py : Real, pressed : Boolean, idHID : Integer) : Void is do
		if(activated) then
			var stop : Boolean init false
			var t : Transition
			
			currentState.transitions.exists{t |
				if(t.isInstanceOf(MoveTransition)) then
					var mt : MoveTransition init t.asType(MoveTransition)
					
					mt.px      := px
					mt.py      := py
					mt.button  := button
					mt.pressed := pressed
					
					if(t.isGuardRespected) then
						stop := true
						executeTransition(t)
					end
				end
				
				stop
			}
		end
	end
	
	


	method onPressure(button : Integer, px : Real, py : Real, idHID : Integer) : Void is do
		if(activated) then
			var stop : Boolean init false
			var t : Transition

			currentState.transitions.exists{t |
				if(t.isInstanceOf(PressureTransition)) then
					var pt : PressureTransition init t.asType(PressureTransition)
					
					pt.px     := px
					pt.py     := py
					pt.button := button
					
					if(t.isGuardRespected) then
						stop := true
						executeTransition(t)
					end
				end
				
				stop
			}
		end
	end



	method onRelease(button : Integer, px : Real, py : Real, idHID : Integer) : Void is do
		if(activated) then
			var stop : Boolean init false
			var t : Transition
			
			currentState.transitions.exists{t |
				if(t.isInstanceOf(ReleaseTransition)) then
					var rt : ReleaseTransition init t.asType(ReleaseTransition)
					
					rt.px     := px
					rt.py     := py
					rt.button := button
					
					if(t.isGuardRespected) then
						stop := true
						executeTransition(t)
					end
				end
				
				stop
			}
		end
	end
}