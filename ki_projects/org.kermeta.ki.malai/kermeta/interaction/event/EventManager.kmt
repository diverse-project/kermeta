/*
 * Creation : February 24, 2010
 * Licence  : EPL 
 * Copyright: INRIA Rennes, Triskell
 * Authors  : Arnaud Blouin
 */
package kermeta::ki::malai::interaction::event;

require kermeta
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/EventHandler.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/event/Event.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/event/MouseEvent.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/event/KeyEvent.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/event/ActionEvent.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/event/MouseWheelEvent.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/event/ItemEvent.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/event/ChangeEvent.kmt"

using kermeta::ki::malai::interaction
using kermeta::ki::malai::widget
using kermeta::standard


/**
	This class is Dispatchable: an EventManager can be managed by a disptacher.
	The goal of this class is to map a Java EventManagerWrapper, that gathers
	Java event, to Kermeta.
*/
class EventManager
{
	/** The objects that must be aware about new produced events. */
	reference handlers : EventHandler[0..*]
	

	/** Initialises the EventManager. */
	operation initialise() : Void is do
		// The EventManager is mapped to an corresponding Java EventManagerWrapper and therefore to a Java DisptacherWrapper.
		result ?= extern org::kermeta::ki::malai::interaction::eventWrapper::EventManagerWrapperExtern.initialise(self)
	end
	
	
	
	operation run() : Void is do
		var event : Event
		// We get the event to process.
		event ?= extern org::kermeta::ki::malai::interaction::eventWrapper::EventManagerWrapperExtern.getTopEvent(self)
		
		if(event.isVoid) then
			stdio.writeln("event is void")
		else
			var existStr : String
			// We get the string corresponding to an EXIT event.
			existStr ?= extern org::kermeta::ki::malai::interaction::eventWrapper::EventWrapperExtern.getExitEventString(self)
			
			if(event.getName.equals(existStr)) then
				// We launch an exception to stop the program.
				var ex : kermeta::exceptions::Exception init kermeta::exceptions::Exception.new
				ex.message := existStr
				raise ex
			else
				process(event)// The event is processed.
			end
		end
	end
	
	
	/**
	* Processes the event produces by the Java UI and transfered to Kermeta.
	*/
	operation process(event : Event) : Void is do
		var name : String   init event.getName
		var info : AWTEvent init event.getInfo

		if(info.isInstanceOf(MouseEvent)) then
			var me 		: MouseEvent init info.asType(MouseEvent)
			var button 	: Integer 	 init me.getButton
			var x 		: Real 		 init me.getX.toReal
			var y 		: Real 		 init me.getY.toReal
			var object  : Object     init info.getSourceObject
			
			if(name.equals("MOUSE_MOVED")) then
				handlers.each{h | h.onMove(button, x, y, false, 0, object) }
			else
			if(name.equals("MOUSE_DRAGGED")) then
				handlers.each{h | h.onMove(button, x, y, true, 0, object) }
			else
			if(name.equals("MOUSE_PRESSED")) then
				handlers.each{h | h.onPressure(button, x, y, 0, object) }
			else
			if(name.equals("MOUSE_RELEASED")) then
				handlers.each{h | h.onRelease(button, x, y, 0, object) }
			end 
			if(name.equals("MOUSE_WHEEL_MOVED")) then
				var mwe    : MouseWheelEvent init info.asType(MouseWheelEvent)
				var isUp   : Boolean init mwe.getWheelRotation<0
				var increment : Integer init mwe.getScrollAmount
				
				handlers.each{h | h.onWheel(x, y, isUp, increment, 0, object) }
			end end end end
		else
		if(info.isInstanceOf(KeyEvent)) then
			var keyCode : Integer init info.asType(KeyEvent).getKeyCode
			
			if(name.equals("KEY_PRESSED")) then
				var object  : Object init info.getSourceObject
				handlers.each{h | h.onKeyPressure(keyCode, 0) }
			else
			if(name.equals("KEY_RELEASED")) then
				var object  : Object init info.getSourceObject
				handlers.each{h | h.onKeyRelease(keyCode, 0) }
			end end
		else
		if(name.equals("ACTION_PERFORMED")) then
			var src : Object init info.asType(ActionEvent).getSourceObject

			if(src.isInstanceOf(TextField)) then
				var textField : TextField init src.asType(TextField)
				handlers.each{h | h.onTextChanged(textField) }
			else
				if(src.isInstanceOf(CheckBox)) then
					var checkBox : CheckBox init src.asType(CheckBox)
					handlers.each{h | h.onCheckBoxPressed(checkBox) }
				else
					if(src.isInstanceOf(Button)) then
						var button : Button init src.asType(Button)
						handlers.each{h | h.onButtonPressed(button) }
					end
				end
			end
		else
		if(name.equals("ITEM_STATE_CHANGED")) then
			var src : Object init info.asType(ItemEvent)
			//TODO
		else
		if(name.equals("STATE_CHANGED")) then
			var src : Object init info.asType(ChangeEvent).getSourceObject

			if(src.isInstanceOf(Spinner)) then
				var spinner : Spinner init src.asType(Spinner)
				handlers.each{h | h.onSpinnerValueModified(spinner) }
			end
		end end end end end
	end
}
