/* $Id:$ 
 * Creation : February 24, 2010
 * Licence  : EPL 
 * Copyright:
 * Authors  : Arnaud Blouin
 */

package kermeta::ki::malai::dispatcher;


require kermeta

using kermeta::standard


abstract class AbstractDispatcher
{
	attribute running : Boolean
	
	
	operation initialise() : Void is do
		extern org::kermeta::ki::malai::dispatcherWrapper::DispatcherWrapper.initialise(self)
	end
	

	
	operation waitForEvent() : Void is do
		extern org::kermeta::ki::malai::dispatcherWrapper::DispatcherWrapper.waitForEvent(self)
	end
	
	
	
	operation run() : Void is abstract
}




abstract class Dispatchable
{
	reference dispatcher : AbstractDispatcher

	operation isWaiting() : Boolean is abstract
	
	operation run() : Void is abstract
}



class SingleDisptacher inherits AbstractDispatcher 
{
	reference component : Dispatchable
	
	
	method run() : Void is do
		from running := true
		until not running
		loop
			do
				waitForEvent
				
				from true
				until not component.isWaiting
				loop component.run()
				end
			rescue(ex : kermeta::exceptions::Exception) running := false end
		end
	end
}



class ListDisptacher inherits AbstractDispatcher
{
	reference components : Dispatchable[0..*]
	
	
	method run() : Void is do
		from running := true
		until not running
		loop
			do
				waitForEvent
				
				from var stop : Boolean init false
				until stop
				loop
					stop := true
					components.each{c| 
						if(c.isWaiting) then 
							c.run()
							stop := false
						end
					}
				end
			rescue(ex : kermeta::exceptions::Exception) running := false end
		end
	end
}
