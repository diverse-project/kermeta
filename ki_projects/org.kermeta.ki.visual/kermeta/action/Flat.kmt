/*
 * Creation : April 9, 2010
 * Licence  : EPL 
 * Copyright: INRIA Rennes, Triskell
 * Authors  : Arnaud Blouin
 */
package kermeta::ki::visual;

require kermeta
require "platform:/resource/org.kermeta.ki.malai/kermeta/action/Action.kmt"
require "platform:/resource/org.kermeta.ki.visual/kermeta/operation/ClassFlattener.kmt"
require "platform:/resource/org.kermeta.ki.visual/kermeta/ModelExtension.kmt"

using kermeta::ki::malai::action
using kermeta::ki::malai::undo
using kermeta::language::structure



class Flat inherits Action, Undoable {
	attribute flatteningOperation : ClassFlattener
	
	reference metamodel : ModelingUnit
	
	reference classToFlat : ClassDefinition
	
	
	
	method doActionBody() : Void is do
		flatteningOperation 			:= ClassFlattener.new
		flatteningOperation.metamodel 	:= metamodel
		
		flatteningOperation.flat(classToFlat)
		metamodel.organise()
	end
	
	
	method canDo() : Boolean is do
		result := not metamodel.isVoid and not classToFlat.isVoid
	end
	
	
	method cancelledBy(action : Class) : Boolean is do
		result := false
	end
	
	
	method isRegisterable() : Boolean is do
		result := true
	end
	
	
	method undo() : Void is do
		flatteningOperation.removedClasses.each{clazz |
			var obj : Object init clazz.container()
			
			if(obj.isInstanceOf(Package)) then
				obj.asType(Package).ownedTypeDefinition.add(clazz)
				extern org::kermeta::ki::kompren::view::ClassDiagramViewExtern.onEntityAdded(metamodel, clazz, clazz.isAspect, -1, clazz.name)
			end
		}
	
		flatteningOperation.removedProperties.each{prop |
			prop.owningClass.ownedAttribute.add(prop.prop)
			prop.prop.owningClass := prop.owningClass
			var oppositeName : String init ""
			var oppositeCompo : Boolean init false
			var oppositeCardString : String init ""
			
			if(not prop.prop.opposite.isVoid) then 
				oppositeName  := prop.prop.opposite.name 
				oppositeCompo := prop.prop.opposite.isComposite
				oppositeCardString := prop.prop.opposite.getCardinalityString
			end
			
			extern org::kermeta::ki::kompren::view::ClassDiagramViewExtern.onRelationAdded(metamodel, prop.prop, prop.prop.isComposite, 
									prop.prop.owningClass, prop.owningClass, prop.prop.name, prop.prop.getCardinalityString, 
									oppositeName, oppositeCardString, -1, oppositeCompo)
		}
		
		flatteningOperation.addedOperations.each{op |
			classToFlat.moveOperationTo(op.op, op.owningClass)
		}
	
		flatteningOperation.addedProperties.each{prop |
			classToFlat.movePropertyTo(prop.prop, prop.owningClass, metamodel)
		}
		
		flatteningOperation.removedInheritances.each{in |
			if(in.sourceType.isVoid) then
				var clazz : Class init Class.new
				clazz.typeDefinition := in.target
				in.sourceType := clazz
			end
			
			in.source.superType.add(in.sourceType)
			extern org::kermeta::ki::kompren::view::ClassDiagramViewExtern.onInheritanceAdded(metamodel, in.source, in.target, -1)
		}
		
		flatteningOperation.addedInheritances.each{in |
			in.source.superType.each{st |
				if(st.isInstanceOf(ParameterizedType)) then
					if(in.target==st.asType(ParameterizedType).typeDefinition) then
						in.source.superType.remove(st)
					end
				end
			}
			extern org::kermeta::ki::kompren::view::ClassDiagramViewExtern.onInheritanceRemoved(metamodel, in.source, in.target)
		}
		
		metamodel.organise()
	end
	
	
	
	
	method redo() : Void is do
		doActionBody
	end
}
