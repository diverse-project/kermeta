/*
 * Creation : June 9, 2010
 * Licence  : EPL
 * Copyright: INRIA Rennes
 * Authors  : Arnaud Blouin 
 */
package kermeta::ki::visual;

require kermeta
require "platform:/resource/org.kermeta.ki.malai/kermeta/action/Action.kmt"
require "platform:/resource/org.kermeta.ki.visual/kermeta/ModelExtension.kmt"


using kermeta::ki::malai::action
using kermeta::standard
using kermeta::language::structure


class CentreOnClass inherits Action {
	attribute className : String
	
	reference metamodel : ModelingUnit
	
	
	
	operation getSearchedClassesInPackage(pkg : Package, name : String) : TypeDefinition is do
		result := pkg.ownedTypeDefinition.detect{t | t.name==name }
		
		if(result.isVoid) then
			pkg.nestedPackage.exists{p | 
				result := getSearchedClassesInPackage(p, name)
				not result.isVoid
			}
		end
	end
	
	
	operation getSearchedClasses() : TypeDefinition is do
		metamodel.packages.exists{p |
			result:= getSearchedClassesInPackage(p, className)
			not result.isVoid
		}
	end
	
	
	method doActionBody() : Void is do
		var clazz : TypeDefinition init getSearchedClasses()
		metamodel.focusOnTypeDefinition(clazz)
		done()
	end
	
	
	method canDo() : Boolean is do
		result := not className.isVoid and not metamodel.isVoid
		
		if(result) then
			result := not getSearchedClasses().isVoid
		end
	end
	
	
	method cancelledBy(action : Class) : Boolean is do
		result := false
	end
	
	
	method isRegisterable() : Boolean is do
		result := false
	end
}
