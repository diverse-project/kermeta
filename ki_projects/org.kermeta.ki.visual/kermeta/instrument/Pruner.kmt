/*
 * Creation : March 17, 2010
 * Licence  : EPL 
 * Copyright: INRIA Rennes, Triskell
 * Authors  : Arnaud Blouin 
 */
package kermeta::ki::visual;


require "platform:/resource/org.kermeta.ki.visual/kermeta/ModelExtension.kmt"
require "platform:/resource/org.kermeta.ki.visual/kermeta/action/Prune.kmt"
require "platform:/resource/org.kermeta.ki.visual/kermeta/interaction/CtrlMultiPress.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/interaction/library/Press.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/instrument/library/PressLink.kmt"

using kermeta::language::structure
using kermeta::standard
using kermeta::ki::malai::interaction::event
using kermeta::ki::malai::instrument
using kermeta::ki::malai::interaction
using kermeta::ki::malai::action
using kermeta::ki::malai::widget



class Pruner inherits Instrument
{
	reference metamodel : ModelingUnit
	
	attribute viewPolicy : PrunerViewPolicy
	
	attribute hideButton : ToggleButton
	
	attribute grayedButton : ToggleButton
	
	
	
	method initialise(actionRegistry : ActionRegistry) : Void is do
		super(actionRegistry)
		
		viewPolicy := PrunerViewPolicy.hide
	end
	
	
	
	operation initialiseWidgets(eventManager : EventManager) : Void is do
		hideButton   := ToggleButton.new
		grayedButton := ToggleButton.new
		
		hideButton.initialiseWithText("Hide", eventManager)
		grayedButton.initialiseWithText("Gray", eventManager)
		
		interimFeedback
	end
	
	
	
	method initialiseLinks(eventManager : EventManager) : Void is do
		addLink(MultiPress2Prune.new, metamodel, void, eventManager, false)
		addLink(Press2Prune.new, metamodel, void, eventManager, false)
		addLink(Press2Reinit.new, metamodel, void, eventManager, false)
	end
	
	
	method interimFeedback() : Void is do
		hideButton.setSelected(viewPolicy==PrunerViewPolicy.hide)
		grayedButton.setSelected(viewPolicy==PrunerViewPolicy.gray)
		hideButton.setVisible(activated)
		grayedButton.setVisible(activated)
	end
}




class MultiPress2Prune inherits Link
{
	method createInteraction() : Interaction is do
		result := CtrlMultiPress.new
	end
	
	
	method getActionClass() : Class is do
		result := ActionPrune
	end
	
	
	method isConditionRespected() : Boolean is do
		result := true
	end
	
	
	method updateAction() : Void is do
		if(not action.isVoid) then
			var press : CtrlMultiPress init interaction.asType(CtrlMultiPress)
			var as    : ActionPrune init action.asType(ActionPrune)
			
			press.objects.each{o |
				if((not o.isVoid).andThen{b | o.isInstanceOf(ClassDefinition)}) then
					as.selection.add(o.asType(ClassDefinition))
				end
			}

		end
	end
	
	
	method createAction() : Void is do
		var as : ActionPrune init ActionPrune.new
		var pruner : Pruner init instrument.asType(Pruner)
		
		as.initialise(instrument.actionRegistry)
		as.viewPolicy := pruner.viewPolicy
		as.metamodel  := pruner.metamodel
		action        := as
	end
}




class Press2Reinit inherits Link
{
	method createInteraction() : Interaction is do
		result := Press.new
	end
	
	method updateAction() : Void is do
	end
	
	
	method getActionClass() : Class is do
		result := ActionReinitView
	end
	
	
	method isConditionRespected() : Boolean is do
		var press : Press init interaction.asType(Press)
		
		result := press.target.isVoid
	end
	
	
	method createAction() : Void is do
		var as : ActionReinitView init ActionReinitView.new
		var pruner : Pruner init instrument.asType(Pruner)
		
		as.initialise(instrument.actionRegistry)
		as.metamodel := pruner.metamodel
		action       := as
	end
}






class Press2Prune inherits PressLink
{
	method getActionClass() : Class is do
		result := ActionPrune
	end
	
	
	method updateAction() : Void is do
		if(not action.isVoid) then
			var press : Press init interaction.asType(Press)
			var as    : ActionPrune init action.asType(ActionPrune)
			
			if(press.target.isInstanceOf(ClassDefinition)) then
				as.selection.clear
				as.selection.add(press.target.asType(ClassDefinition))
			end
		end
	end
	
	
	
	method isConditionRespected() : Boolean is do
		var press : Press init interaction.asType(Press)
		result := not press.target.isVoid and press.target.isInstanceOf(ClassDefinition)
	end
	
	
	method createAction() : Void is do
		var as : ActionPrune init ActionPrune.new
		var pruner : Pruner init instrument.asType(Pruner)
		
		as.initialise(instrument.actionRegistry)
		as.viewPolicy := pruner.viewPolicy
		as.metamodel  := pruner.metamodel
		action        := as
	end
}
