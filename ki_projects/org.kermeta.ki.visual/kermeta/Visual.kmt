/*
 * Creation : March 11, 2010
 * Licence  : EPL 
 * Copyright: INRIA Rennes, Triskell
 * Authors  : Arnaud Blouin
 */
@mainClass "kermeta::ki::visual::Visual"
@mainOperation "main"

package kermeta::ki::visual;

require kermeta
require "platform:/resource/org.kermeta.ki.visual/kermeta/ModelExtension.kmt"
require "platform:/resource/org.kermeta.ki.visual/kermeta/pruning/Pruner.kmt"
require "platform:/resource/org.kermeta.ki.visual/kermeta/VisitableKermeta.km"
require "platform:/resource/org.kermeta.ki.visual/kermeta/instrument/InstrumentsSelector.kmt"
require "platform:/resource/org.kermeta.ki.malai/kermeta/instrument/UndoRedoManager.kmt"

using kermeta
using kermeta::ki::malai::undo
using kermeta::ki::malai::widget
using kermeta::ki::malai::action
using kermeta::standard
using kermeta::ki::malai::interaction::event
using kermeta::ki::malai::instrument
using kermeta::persistence
using kermeta::language::structure
using kermeta::language::behavior
using kermeta::ki::malai::dispatcher
using kermeta::ki::malai::picking


class Visual
{
	operation main() : Void is do
		var aui			   : MetamodelCanvas init MetamodelCanvas.new
		var eventManager   : EventManager    init EventManager.new
		var dispatcher     : ListDispatcher  init ListDispatcher.new
		
		dispatcher.initialise
		eventManager.initialise(dispatcher)
		dispatcher.processes.add(eventManager)
		
		aui.initialise(eventManager)
		extern org::kermeta::ki::visual::MetamodelVizuFrame.organise(aui)
		aui.setVisible(true)
		aui.refreshView
		
		dispatcher.run
	end
}




class MetamodelCanvas inherits ActionHandler, UndoHandler {
	attribute metamodel : ModelingUnit
	
	attribute pruner : Pruner
	
	attribute undoManager : UndoRedoManager
	
	attribute undoCollector : UndoCollector
	
	attribute selector : InstrumentsSelector
	
	attribute actionRegistry : ActionRegistry
	
	attribute eventManager : EventManager
	
	attribute toolbar : Panel
	


	
	operation initialise(eventManager : EventManager) : Void is do
		loadMetamodel("/examples/example1.km")
//		loadMetamodel("/examples/Ecore.km")

		self.eventManager := eventManager
		undoCollector     := UndoCollector.new 
		actionRegistry    := ActionRegistry.new
		
		undoCollector.initialise
		undoCollector.handlers.add(self)
		actionRegistry.initialise(undoCollector)
		actionRegistry.handlers.add(self)

		pruner      	 := Pruner.new
		undoManager 	 := UndoRedoManager.new
		selector         := InstrumentsSelector.new
		
		pruner.initialise(actionRegistry)
		undoManager.initialise(actionRegistry)
		undoManager.initialiseWidgets(eventManager)
		selector.initialise(actionRegistry)
		selector.initialiseInstruments(pruner, eventManager)

		initialiseUI
		extern org::kermeta::ki::visual::MetamodelVizuFrame.initialise(self, metamodel, toolbar, eventManager)
		
		selector.picker := toolbar
		selector.initialiseLinks(eventManager)
		selector.setActivated(true)
		pruner.metamodel := metamodel
		pruner.initialiseLinks(eventManager)

		undoManager.undoCollector := undoCollector		
		undoManager.picker := toolbar
		undoCollector.handlers.add(undoManager)
		undoManager.initialiseLinks(eventManager)
		undoManager.setActivated(true)
		
		var model2view : Metamodel2ViewVisitor init Metamodel2ViewVisitor.new
		model2view.modelingUnit := metamodel
		metamodel.accept(model2view)
	end
	
	
	
	method updateUndo() : Void is do
		refreshView
	end
	
	
	
	operation initialiseUI() : Void is do
		toolbar := Panel.new
		toolbar.initialise(eventManager)
		extern org::kermeta::ki::visual::MetamodelVizuFrame.initialiseToolbar(toolbar, undoManager.undoButton, 
					undoManager.redoButton, selector.prunerButton)
		toolbar.components.add(undoManager.undoButton)
		toolbar.components.add(undoManager.redoButton)
		toolbar.components.add(selector.prunerButton)
	end



	operation loadMetamodel(muURI: String) : Void is do
		var rep : EMFRepository init EMFRepository.new
		var res : EMFResource
		res ?= rep.getResource(muURI)
		metamodel ?= res.one
	end

	
	
	operation setVisible(visible : Boolean) : Void is do
		extern org::kermeta::ki::visual::MetamodelVizuFrame.setVisible(self, visible)
	end
	
	
	operation refreshView() : Void is do
		extern org::kermeta::ki::visual::MetamodelVizuFrame.refreshView(self)
	end
	
	
	
	method onActionAdded(action : Action) : Void is do
		refreshView
	end
	
	method onActionAborted(action : Action) : Void is do
		refreshView
	end 
	
	method onActionCancelled(action : Action) : Void is do
		refreshView
	end 
	
	method onActionExecuted(action : Action) : Void is do
		refreshView
	end 
}



class Metamodel2ViewVisitor inherits KermetaVisitor {
	attribute firstPass : Boolean
	
	reference modelingUnit : ModelingUnit

	reference classes : ClassDefinition[0..*]
	
	reference links : Property[0..*]
	

	method visitModelingUnit(mu: ModelingUnit): Void is do
		firstPass := true
		mu.packages.each{p | p.accept(self) }
		firstPass := false
		mu.packages.each{p | p.accept(self) }
	end
	
	
	method visitPackage(p: Package) : Void is do
		p.nestedPackage.each{p | p.accept(self)}
		p.ownedTypeDefinition.each{td | td.accept(self)}
	end
	
	
	method visitOperation(o: Operation) : Void is do
	end	


	
	method visitProperty(p: Property): Void is do
		if(p.type.isInstanceOf(Class)) then
			var cd : ClassDefinition init p.type.asType(Class).classDefinition
			
			if(classes.contains(cd)) then
				extern org::kermeta::ki::visual::view::MetamodelView.onLinkAdded(modelingUnit, p, p.isComposite, p.owningClass, cd, -1)
			end
			links.add(p)			
		end
	end
	
	
	
	method visitClassDefinition(c: ClassDefinition) : Void is do
		if(firstPass) then
			classes.add(c)
			extern org::kermeta::ki::visual::view::MetamodelView.onEntityAdded(modelingUnit, c, c.isAspect, -1)
			c.entity2ViewMapping
		else
			c.ownedAttribute.each{p | p.accept(self)}
			c.ownedOperation.each{o | o.accept(self)}
			
			c.superType.each{t |
				if(t.isInstanceOf(Class)) then
					var cd : ClassDefinition init t.asType(Class).classDefinition
					
					if(classes.contains(cd)) then
						extern org::kermeta::ki::visual::view::MetamodelView.onInheritanceAdded(modelingUnit, c, cd, -1)
					end
				end
			}
		end
	end
}
