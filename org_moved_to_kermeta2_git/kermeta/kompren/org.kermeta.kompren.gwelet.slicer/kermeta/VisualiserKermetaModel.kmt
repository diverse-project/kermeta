package org::kermeta::language::structure;

using kermeta::standard
using kermeta::utils
using kermeta::io::StdIO => stdio
class VisualiserKermetaModel {
	attribute radius : Integer

	attribute considersuperType : Boolean

	attribute considerownedAttribute : Boolean

	attribute considertype : Boolean

	attribute idPass : Integer

	reference requiredClassDefinitions : ClassDefinition[0..*]

	reference requiredModelingUnits : ModelingUnit[0..*]

	reference addedPackages : Bag<Package>

	reference addedClassDefinitions : Bag<ClassDefinition>

	operation initialise(requiredClassDefinitions : bag ClassDefinition[0..*], requiredModelingUnits : bag ModelingUnit[0..*], radius : Integer, considersuperType : Boolean, considerownedAttribute : Boolean, considertype : Boolean) : VisualiserKermetaModel is do
		result := self
		self.addedPackages := Bag<Package>.new
		self.addedClassDefinitions := Bag<ClassDefinition>.new
		self.radius := radius
		self.requiredClassDefinitions.clear()
		self.requiredClassDefinitions.addAll(requiredClassDefinitions)
		self.requiredModelingUnits.clear()
		self.requiredModelingUnits.addAll(requiredModelingUnits)
		self.considersuperType := considersuperType
		self.considerownedAttribute := considerownedAttribute
		self.considertype := considertype

		if(idPass.isVoid) then idPass := 0
		else idPass := idPass + 1 end
	end

	operation launch() is do
		onStart()
		self.requiredClassDefinitions.each{theClassDefinition | theClassDefinition.visitToAddClasses(self, 0) }
		self.requiredModelingUnits.each{theModelingUnit | theModelingUnit.visitToAddClasses(self, 0) }
		self.requiredClassDefinitions.each{theClassDefinition | theClassDefinition.visitToAddRelations(self, 0) }
		self.requiredModelingUnits.each{theModelingUnit | theModelingUnit.visitToAddRelations(self, 0) }
		self.addedPackages.each{thePackage | if(thePackage.checkCanReallyBeAdded()) then onPackageAdded(thePackage) end }
		self.addedClassDefinitions.each{theClassDefinition | onClassDefinitionAdded(theClassDefinition) }
		onEnd()
	end

	operation onPackageAdded(pkg : Package) is do

		stdio.writeln("package: " + pkg.name)
	
	end
	operation onClassDefinitionAdded(cl : ClassDefinition) is do

		stdio.writeln("clazz: " + cl.name)
	
	end
	operation onEnd() is do
	end
	operation onStart() is do
	end

}
