/* $Id:$ 
 * Creation : October 29, 2010
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            dvojtise
 */


package org::kermeta::ecore::binarymerger;


require kermeta
require "BinaryMergerContext.kmt"

class Mergeable
{
	/** if this element is in the AspectModel, it may have an equivalent in the MergedModel */
	reference similarElementInMergedModel : Mergeable#similarElementInAspectModel
	/** if this element is in the MergedModel, it may have an equivalent in the AspectModel */
	reference similarElementInAspectModel : Mergeable#similarElementInMergedModel
	
	operation binaryMerger_MoveToMergedModel(context : BinaryMergerContext) : Mergeable is abstract
	operation fixReferences() is abstract
	
	/** returns the similarElement regardless where it is stored (similarElementInMergedModel or similarElementInAspectModel) */
	operation getSimilarElement() : Mergeable is do
		result := similarElementInMergedModel
		if( result.isVoid) then
			result := similarElementInAspectModel
		end
	end
	
	/** returns the Element in merged model (ie. similarElementInMergedModel or self) */
	operation getElementInMergedModel() : Mergeable is do
		result := similarElementInMergedModel
		if( result.isVoid) then
			result := self
		end
	end
	
	/**
	 * used by generic merge strategy
	 */
	operation selectFirstNonVoidProperty( propertyName: kermeta::standard::String, primaryValue : kermeta::standard::Object, aspectValue : kermeta::standard::Object) : kermeta::standard::Object is do				
		result := primaryValue
		// select non void value
		if (not aspectValue.isVoid) and (primaryValue.isVoid) then
			result := aspectValue
		end
	end
	
	/**
	 * indicates if this element has already been marked for referenceMerge
	 * void is considered as false
	 */
	attribute isAddedForReferenceMerge : kermeta::standard::Boolean
	
	/**
	 * indicates if this element has already been marked for referenceFix
	 * void is considered as false
	 */
	attribute isAddedForReferenceFix : kermeta::standard::Boolean
	
}