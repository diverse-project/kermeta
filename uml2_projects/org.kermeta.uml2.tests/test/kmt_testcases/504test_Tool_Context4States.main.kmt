/* $Id: 504test_Tool_Context4States.main.kmt,v 1.4 2007-12-14 12:29:22 vmahe Exp $
 * Creation date: Summer 2006
 * License:		Eclipse Public License
 * Copyright:	INRIA
 * Authors:		Vincent MAHE <vmahe@irisa.fr>
 */
@mainClass "tools_tests::ToolContext4StateTestCase"
@mainOperation "main"


package tools_tests;

require kermeta
require "platform:/plugin/org.kermeta.uml2/src/kermeta/transformations/BehaviorTools.kmt"

using uml
using uml::transformations

class ToolContext4StateTestCase inherits kermeta::kunit::TestCase
{
	operation main() : Void is do 
		var tr : kermeta::kunit::TestRunner init kermeta::kunit::TestRunner.new
		tr.run(ToolContext4StateTestCase)
		tr.printTestResult		
	end

    reference Aclass : BehavioredClassifier
    reference Sstate : State
    reference ct : Context4States_Tool
    reference Rregion : Region
    reference  SMmachine : StateMachine
	
	method setUp() is do
    	ct := Context4States_Tool.new
    	Aclass := Class.new
    	Aclass.name := "A"
    	Rregion := Region.new
    	SMmachine := StateMachine.new
    	Sstate := State.new
    	Sstate.name := "S"
//    	Aclass.classifierBehavior := sm		// classifierBehavior has no opposite in UML2 metamodel
    	Aclass.ownedBehavior.add(SMmachine)
    	SMmachine.region.add(Rregion)
    	Rregion.subvertex.add(Sstate)
	end
	
	method tearDown() is do
		// We don't need to tearDown anything in this test case.
	end
    
    operation testContext4StatesTool_Classifier() is do
    	assertTrueWithMsg(ct.contextOfSubstate(Sstate) == Aclass,
    		"The Context4States_Tool fails to give the classifier of the given state")
    end
    operation testContext4StatesTool_Submachine() is do
    	var r : Region init Region.new
    	var s : State init State.new
    	SMmachine.region.remove(Rregion)
    	SMmachine.region.add(r)
    	r.subvertex.add(s)
    	s.region.add(Rregion)
    	
    	assertTrueWithMsg(ct.contextOfSubstate(Sstate) == Aclass,
    		"The Context4States_Tool fails to give the classifier of the given state")
    end
}