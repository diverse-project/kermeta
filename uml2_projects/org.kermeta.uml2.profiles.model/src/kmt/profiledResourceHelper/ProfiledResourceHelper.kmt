/* $Id: ProfiledResourceHelper.kmt,v 1.10 2009-01-06 13:15:08 dvojtise Exp $
 * Creation date: June 19, 2007
 * License: EPL
 * Copyright: IRISA/INRIA
 * Authors:
 * 		Francois Tanguy
 *		Didier Vojtisek
 */


package profiledResourceHelper;


require kermeta
require "platform:/lookup/org.kermeta.uml2.profiles.model/model/ecore/Ecore.profile.ecore"
require "platform:/lookup/org.kermeta.uml2.profiles.model/model/ecore/Standard.profile.ecore"
require "../profileHelpers/StereotypeInstanceHelper.kmt"
//require "platform:/resource/org.kermeta.uml2.profiles.model/model/ecore/Ecore.profile.ecore"
//require "platform:/resource/org.kermeta.uml2.profiles.model/model/ecore/Standard.profile.ecore"


using kermeta::persistence
using kermeta::standard

using ecore
using uml
using uml::profileHelpers

/**
 * This class is an helper for resources containing an UML model with profiles
 * It provides several operations that will search for stereotypes in the resource 
 * this is needed because of the special implementation of stereotypes and profiles in UML2
 * Performance note : this is quite inefficient because it needs to read all the resource stereotypes to be able to
 *   do its job, its is better to use UMLStereotypeHelper and aspectize the profile to enable the opposite navigation
 *   which is much faster
 */
class ProfilesHelper {
	
	reference profilResource : Resource


	reference modelResource : Resource

	/**
	 * Stereotypes Instances coming from the uml model.
	 */
	reference stereotypeInstances : Set <kermeta::language::structure::Object>

	/**
	 * Stereotypes coming from the profile.
	 */
	reference stereotypes : Set <ecore::EClass>


	/**
	 * A call to this operation is mandatory for a correct use of this class
	 * It takes 2 resources, in case the profile resource is different of the uml model resource.
	 * However, in most situations you can use the same resource for both parameter
	 */
	operation initialize( modelResource : Resource, profileResource : Resource ) : ProfilesHelper is do
		self.profilResource := profilResource
		self.modelResource := modelResource
		stereotypeInstances := Set <Object>.new
		stereotypes := Set <ecore::EClass>.new
		getStereotypeInstances()
		result := self
	end
		
	/**
	 * Retreives all the stereotypes that decorate the given object 
	 */
	operation getStereotypesFor( object : Object ) : Set <Object> is do
	
		var stereotypes : Set <Object> init Set <Object>.new
		var stereotypeHelper : StereotypeInstanceHelper init StereotypeInstanceHelper.new
	 
		stereotypeInstances.each { instance |
						
			var base_Object : Object init stereotypeHelper.getBase(instance)
			if ( (base_Object != void) and (base_Object.equals(object) ) ) then
				stereotypes.add( instance )
		 	//else
		 	//	stdio.writeln("Stereotype doesn't apply to " + object.toString + " but to " + getBase(instance).toString)
			end
		}
		
		result := stereotypes
	
	end
		
	
	
			
	/**
	 * Fills the internal set 'stereotypes'
	 * (called by the initialize() operation, user is not supposed to call it directly)
	 */
	operation getStereotypes() is do
	
		var p : EPackage 
		p ?= profilResource.instances.one
		p.eClassifiers.each { classifier |
		
			if ( classifier.isInstanceOf(ecore::EClass) ) then
			
				var c : ecore::EClass
				c ?= classifier
				stereotypes.add( c )
			
			end
		
		}
	
	end
	
	
	operation getStereotypeInstances() is do
		if not modelResource.isVoid() then			
			var stereotypeHelper : StereotypeInstanceHelper init StereotypeInstanceHelper.new
			stereotypeInstances.addAll( 
				modelResource.instances.select{ instance | 										
					not instance.isInstanceOf(uml::Element) 
					and ( stereotypeHelper.hasBaseProperty(instance)
					
							/* getProperty( "base_Class", instance ) != void
						or getProperty( "base_Package", instance ) != void
						or getProperty( "base_Property", instance ) != void */
					)
				} 
			) 
		end
		//stdio.writeln("stereotypes to add : " + modelResource.instances.size.toString)
		/*stereotypeInstances.addAll(modelResource.contents.select{ instance | instance.isInstanceOf(uml::Stereotype) })
		stereotypeInstances.each{i| stdio.writeln(i.asType(uml::Stereotype).name)}
		stdio.writeln("stereotypes instances " + stereotypeInstances.size.toString)*/
	end
	
	/**
	 * The content root of a uml model is composed of a model element and possibly several stereotypes instances.
	 */
	operation getModel() : uml::Model is do
	
		result ?= modelResource.instances.detect{ instance | instance.isInstanceOf(uml::Model) }
	
	end

}

/**
 * Exception used to report errors in UML stereotypes
 */
class InvalidUMLStereotype inherits kermeta::exceptions::Exception {

}