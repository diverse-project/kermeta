@mainClass "tools_tests::ToolInterfaceTestCase"
@mainOperation "testExtractInterfaceTool_HasInterface"

package tools_tests;

require kermeta
require "../../src/kermeta/transformations/StaticTools.kmt"

using uml2
using uml2::transformations

class ToolInterfaceTestCase inherits kermeta::kunit::TestCase
{
	reference model : Model
    reference Aclass : Class
    reference Bclass : Class
	
	method setUp() is do
    	model := Model.new
    	Aclass := Class.new
    	Aclass.name := "Aclass"
    	model.ownedMember.add(Aclass)
    	
    	Bclass := Class.new
    	Bclass.name := "Bclass"
    	Bclass.isAbstract := true
    	model.ownedMember.add(Bclass)
    	
    	// test placement of attributes & operations
    	var attr : Property init Property.new
    	attr.name := "attribute"
    	Aclass.ownedAttribute.add(attr)
    	var meth : Operation init Operation.new
    	meth.name := "abstractOperation"
    	meth.isAbstract := true
    	var par : Parameter init Parameter.new
    	par.name := "par1"
    	par.type := Aclass
    	meth.returnResult.add(par)
    	Aclass.ownedOperation.add(meth)
    	meth := Operation.new
    	meth.name := "concreteOperation"
    	meth.isAbstract := false
    	par := Parameter.new
    	par.name := "par2"
    	par.type := Bclass
    	meth.ownedParameter.add(par)
    	Aclass.ownedOperation.add(meth)
    	
     	var iftool : Interface_Tool init Interface_Tool.new
     	model.ownedMember.add(iftool.extractInterface(Aclass))
     	model.ownedMember.add(iftool.extractInterface(Bclass))
	end
	
	method tearDown() is do
		// We don't need to tearDown anything in this test case.
	end
	
    operation testExtractInterfaceTool_HasInterface() is do
    	// first asserts the generalization collection is not empty (uncatched exception)
    	assertTrueWithMsg(Aclass.generalization != void,
    		"The given class must have a generalisation")
    	assertTrueWithMsg(Aclass.generalization.size != 0,
    		"The given class must inherit its new interface")
    		
    	assertTrueWithMsg(Aclass.generalization.one.general.getMetaClass == Interface,
    		"The given class must inherit its new interface")
    end
    
	
    operation testExtractInterfaceTool_InterfaceHasAttributes() is do
    	assertTrueWithMsg(Aclass.ownedAttribute.size == 0,
    		"The given class must have no attributes")
    	
    	var interf : Interface
    	interf ?= Aclass.generalization.one.general
    	assertTrueWithMsg(interf.ownedAttribute.size == 1,
    		"The interface must have the given class attributes")
    end
	
    operation testExtractInterfaceTool_InterfaceHasAllOperations() is do
    	// first asserts the generalization collection is not empty (uncatched exception)
    	assertTrueWithMsg(Aclass.generalization != void,
    		"The given class must have a generalisation")
    	assertTrueWithMsg(Aclass.generalization.size != 0,
    		"The given class must inherit its new interface")
    		
    	var interf : Interface
    	interf ?= Aclass.generalization.one.general
    	assertTrueWithMsg(interf.ownedOperation.size == 2,
    		"The new interface must have all the operations of the concrete class")
    	assertTrueWithMsg(interf.ownedAttribute.size == 1,
    		"The new interface must have all the attributes of the concrete class")
    end
	
    operation testExtractInterfaceTool_ConcreteClass_ConcreteOperations() is do
    	var bool : kermeta::standard::Boolean init true
    	Aclass.ownedOperation.each{ u | if u.isAbstract then bool := false end }
    	assertTrueWithMsg(bool,
    		"The concrete class must have only concrete operations")
    	assertTrueWithMsg(Aclass.ownedOperation.elementAt(0).name == "abstractOperation",
    		"The concrete class must have 'abstractOperation' as first operation")
    	assertTrueWithMsg(Aclass.ownedOperation.elementAt(1).name == "concreteOperation",
    		"The concrete class must have 'concreteOperation' as second operation")
    end
	
    operation testExtractInterfaceTool_InterfaceHasOperationsParameters() is do
    	var interf : Interface
    	interf ?= Aclass.generalization.one.general
    	assertTrueWithMsg(interf.ownedOperation.elementAt(0).returnResult.first.name == "par1",
    		"The new interface must have the correct name of return parameter for operation")
    	assertTrueWithMsg(interf.ownedOperation.elementAt(0).returnResult.first.type == Aclass,
    		"The new interface must have the correct return type for operation")
    	assertTrueWithMsg(interf.ownedOperation.elementAt(1).ownedParameter.first.name == "par2",
    		"The new interface must have the correct name for parameters of operation")
    	assertTrueWithMsg(interf.ownedOperation.elementAt(1).ownedParameter.first.type == Bclass,
    		"The new interface must have the correct type for parameters of operation")
    end
	
    operation testExtractInterfaceTool_ConcreteClass_NoAttributes() is do
    	assertTrueWithMsg(Aclass.ownedAttribute.size == 0,
    		"The concrete class must have no attributes")
    end
	
    // the added interface must be present in the model, to be saved
    operation testExtractInterfaceTool_Model() is do
    	var bool : kermeta::standard::Boolean init false
    	model.ownedMember.each{ u |
    		if u.name == "IA" then bool := true end
    	}
    	assertTrueWithMsg(bool,
    		"Tool failed to add the interface 'IA' to the model")
    	model.ownedMember.each{ u |
    		if u.name == "IB" then bool := true end
    	}
    	assertTrueWithMsg(bool,
    		"Tool failed to add the interface 'IB' to the model")
    end
}