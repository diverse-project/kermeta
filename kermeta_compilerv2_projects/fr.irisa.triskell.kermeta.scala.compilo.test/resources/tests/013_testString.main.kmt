@testOperation ""
@mainClass "testString::TestString"
@mainOperation "main_operation"

package testString;

require kermeta

// Testing the operations on a String : 
//
class TestString inherits kermeta::kunit::TestCase {
 
    // Main operation that calls all the below ones
    operation main_operation() : kermeta::standard::String is do
		stdio.write("toto")
		result := void
    end
    
    // throws an exception : == does not seem to work
    // error! more than failure
    operation testfailCompareTo() : Void is do
    	var comp : kermeta::standard::Integer init 0
    	comp := "String".compareTo("String")
    	stdio.writeln(comp.toString)
    	assert(comp==0)

    	comp := "String".compareTo("Strong")
    	stdio.writeln(comp.toString)
    	assert(comp!=0)

    	comp := "String".compareTo("Strin")
    	assert(comp!=0)
    	comp := "String".compareTo("string")
    	assert(comp!=0)
    end
    
    operation testCompareTo() : Void is do
    	var comp : kermeta::standard::Integer
    	comp := "String".compareTo("String")
    	stdio.write(comp.toString)
    	assert(comp == 0)
    	comp := "String".compareTo("Strongl")
    	stdio.write(comp.toString)
    	assert(comp !=0)
    end

    // Plus : "string1"+"string2"
    operation testplus() : kermeta::standard::String is do 
    	var v1 : kermeta::standard::String init "Prepend"
    	result := v1 + "_Append"
 	  	stdio.writeln(result)
    	assert(result.compareTo("Prepend_Append")==0)
    end
    
    // Plus again : 
    operation testplus_literal() : kermeta::standard::String is do
        var v1 : kermeta::standard::String init "Prependwithplus"
    	result := v1.plus("_Append")
    	stdio.writeln(result)
    	assert(result.compareTo("Prependwithplus_Append")==0)
    end

	// test the not-identity 
	operation  testfailop_equals() : kermeta::standard::Boolean is do 
        result := "toto".equals("titi")
       	assert(not(result))
    end
    
    // test the identity 
	operation  testop_equals() : kermeta::standard::Boolean is do 
        result := "titi".equals("titi")
        assert(result)
	end
	
	operation testWrite() is do
		stdio.writeln ( "kkk".append("nnn") )
	end
	
	/**
	 * Appends other at the end of self
	 */
	operation testappend() : Void is do
		var str : kermeta::standard::String init "titi"
		str.append("toto")
		stdio.write(str)
		assert(str.compareTo("tititoto")==0)
	end
	
	/**
	 * returns the character at index in the String
	 */
	operation testelementAt() : Void is do
		assert("abcd".elementAt(2)=="car".elementAt(0))
	end
	
	/**
	 * returns the index within this string of the first 
	 * occurrence of the specified substring
	 */
	operation testindexOf() : Void is do
		assert("bonjour".indexOf("jou")==3)
	end
	
	
	/**
	 * returns a new string that is a substring of this string
	 */
	operation testsubString() : Void is do
		stdio.write("bonjour".substring(1, 4))
		assert("bonjour".substring(1, 4)=="onj")
	end
	
	operation testsize() : kermeta::standard::Integer is do
		assert("BlaBleBli".size == 9)
		result := void
	end
	
	operation testIntegerToStr() : kermeta::standard::Integer is do
		assert(334.toString.size == 3)
		result := void
	end
	
	operation testIntegerToStr2() : kermeta::standard::Integer is do
		assert((334 + 2).toString == "336")
		result := void
	end
	
	operation testIntegerToStr3() : kermeta::standard::Integer is do
		stdio.writeln(3.toString)
		assert(3.toString == "3")
		result := void
	end
	
	operation testBackslashes() : Void is do
		var s1 : kermeta::standard::String init "\n"
		var s2 : kermeta::standard::String init "\\n"
		var s3 : kermeta::standard::String init "\\\n"
		var s4 : kermeta::standard::String init "\""
		
		stdio.writeln("testBackslashes s1")
		stdio.writeln(s1)
		assert( s1.size == 1)
		stdio.writeln("testBackslashes s2")
		stdio.writeln(s2)
		assert( s2.size == 2)
		assert( s2.elementAt(1) == "n".elementAt(0))
		stdio.writeln("testBackslashes s3")
		stdio.writeln(s3)
		assert( s3.size == 2)
		assert( s3.elementAt(1) == "\n".elementAt(0))
		stdio.writeln("testBackslashes s4")
		stdio.writeln(s4)
		assert( s4.size == 1)
	end


}
