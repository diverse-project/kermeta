/* $Id:$ 
 * Creation : January 10, 2013
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            ablouin
 */
package org::kermeta::language::structure;

require kermeta
require "km2llvm_structure.kmt"

using org::kermeta::language::km2llvm
using kermeta::standard
using kermeta::utils
using org::kermeta::language::llvm
 
 
aspect class ClassDefinition {
	attribute _toIgnore : Boolean
	/** Direct super classes of the class. Derived from superTypes. */
	reference superClasses : oset ClassDefinition[*]
	// The sequence of values of the table.
	reference _tableList : Sequence<Operation> 
	// Contains the position in _tableList of the beginning of the vtable of each super type.
	// This table is required during the generation of the constructor to override the vtable of the super types
	// using the vtable of the current class.
	attribute _positionsSuperTypesTable : Hashtable<String, Integer>

	operation toIgnore() : Boolean is do
		if(_toIgnore.isVoid) then
			_toIgnore := kOwnedTags.exists{t | t.name=="CompilerIgnore" }
		end
		result := _toIgnore
	end
	
	
	method getOperation(prototype:Operation) : Operation is do
		// Looking for the operation.
		// TODO: the current detection is not restrictive enough since the type of the parameters are not checked.
		result := ownedOperation.detect{op | op.name==prototype.name and op.ownedParameter.size==prototype.ownedParameter.size }
	end


	// Generates the LLVM code
	method generateLLVM(ctx:Context) is do
		//FIXME generate table of std classes when available
		if(not toIgnore) then
			generateLLVMOperationsTable(ctx)
			generateLLVMType(ctx)
			generateLLVMConstructor(ctx)
			generateLLVMGetters(ctx)
			// The cast operations are not generated here but at the end of the process to be sure to have the collection lowerTypes filled.
			// ctx.classes contains all the generated classes to, at the end, generate the cast operations.
			ctx.classes.add(self)
		end
	end


	/**
		Computes the size of the type.
	*/
	method sizeOf(ctx:Context) : Integer is do
		result := _sizeOf(ctx, 0, ctx.size.pointer) // Computing for the dynamic binding table
		var lastBlock : Integer init result.mod(ctx.size.maxBlock)

		superClasses.each{sc| // Computing for the super types
			result := result +_sizeOf(ctx, lastBlock, ctx.size.pointer)
			lastBlock :=  result.mod(ctx.size.maxBlock)
		}

		var name : String
		var size : Integer
		ownedAttribute.each{attr |//FIXME: add the other types
			if attr.type.qualifiedNameLLVM=="kermeta_standard__Integer" then size := ctx.size.int
			else if attr.type.qualifiedNameLLVM=="kermeta_standard__Boolean" then size := ctx.size.bool
			else if attr.type.qualifiedNameLLVM=="kermeta_standard__String" then size := ctx.size.pointer
			else if attr.type.isPointer(ctx) then size := ctx.size.pointer
			else 
				stdio.errorln("UNKNOWN ATTRIBUTE TYPE: " + attr.name + " " + attr.type.toString)
				size := -1000000
			end end end end
			result := result +_sizeOf(ctx, lastBlock, size)
			lastBlock :=  result.mod(ctx.size.maxBlock)
		}
	end
	
	operation _sizeOf(ctx:Context, lastBlock : Integer, sizeElt : Integer) : Integer is do
		result := sizeElt
		var diffWithPrev : Integer init ctx.size.maxBlock-lastBlock

		if sizeElt > diffWithPrev then
			result := result + diffWithPrev
		end
	end
	
	
	/**
		Generates the table of the operations of the class.
		This table will be used by call operations to support the dynamic binding.
	*/
	operation generateLLVMOperationsTable(ctx:Context) is do
		if _tableList.isVoid then
			_tableList := Sequence<Operation>.new
			// Computing the table.
			_computeOperationsTable(ctx)
			
			if _tableList.size>0 then
				var i : Integer init 0
				var typeTo : String init ctx.llvm.pointer(ctx.llvm.i8)
				// Generating the llvm type of the operations of the class.
				var params : OrderedCollection<LLVMVar> init _tableList.collect{op |
					LLVMVar.new.initialise(ctx.llvm.bitcast(op.getLLVMPrototype(ctx), typeTo, true), void)
				}

				// Generating a global array containing pointers to the operations of the class.
				ctx.tablesDynBinding.append(ctx.llvm.assign(getTableDynBindingName(ctx), ctx.llvm.global(getTableDynBindingType(ctx), "[")))
				ctx.llvm.flatListString(ctx.tablesDynBinding, params, ", ")
				ctx.tablesDynBinding.append("]\n")
	
				params.clear // Cleaning
			end
		end
	end

	
	/** Returns the vtable of the class. */
	operation getLLVMOperationsTable(ctx:Context) : Sequence<Operation> is do
		if _tableList.isVoid then generateLLVMOperationsTable(ctx) end
		result := _tableList
	end

	
	/**
		Returns the name of the LLVM variable corresponding to the table dedicated to dynamic binding.
	*/
	operation getTableDynBindingName(ctx:Context) : String is do
		result := ctx.llvm.globalVar(qualifiedNameLLVM+"##vtable")
	end
	
	
	operation getTableDynBindingType(ctx:Context) : String is do
		if _tableList.isVoid then generateLLVMOperationsTable(ctx) end
		result := ctx.llvm.arrayType(_tableList.size, ctx.llvm.pointer(ctx.llvm.i8))
	end
	
	
	/**
		Helpers for the operation generateLLVMOperationsTable
	*/
	operation _computeOperationsTable(ctx:Context) is do
		var strOp  : String
		var op2 : Operation
		var op3 : Operation

		// Adding the operations of the class
		_tableList.addAll(ownedOperation)
		
		superClasses.each{sc |
			// For each vtable of the super types
			sc.getLLVMOperationsTable(ctx).each{op |
				// The operation op can be an adapter (see the dynamic binding process).
				// In this case the adapted operation must be used.
				if op.adaptedOperation.isVoid then
					op3 := op
				else
					op3 := op.adaptedOperation
				end

				strOp := op3.getPrototype(ctx)
				// And for each operation of these vtables.
				// looking for an operation having the same prototype. 
				op2 := ownedOperation.detect{o | o.getPrototype(ctx)==strOp}
				if not op2.isVoid then
				// It means that an adapter must be generated
					op2 := ctx.generateAdapter(op3, op2, ctx)
					_tableList.add(op2)
				else
					_tableList.add(op3)
				end
			}
		} 

		_positionsSuperTypesTable := Hashtable<String, Integer>.new
		_definePositionsSuperTypesTable(_positionsSuperTypesTable)
	end
	
	
	operation _definePositionsSuperTypesTable(positions:Hashtable<String, Integer>) is do
		var gap : Integer init positions.size+ownedOperation.size

		superClasses.each{sc |
			if sc._tableList.size>0 then 
				positions.put(sc.qualifiedNameLLVM, gap)
				gap := gap + sc._tableList.size
			end
			sc._definePositionsSuperTypesTable(positions)
		}
	end
	
	
	/**
		Returns the position of the given operation into the dynamic binding table.
	*/
	operation getPositionOperationDynBindTable(op:Operation, ctx:Context) : Integer is do
		if _tableList.isVoid then generateLLVMOperationsTable(ctx) end
		result := _tableList.indexOf(op)
	end
	
	
	operation generateLLVMGetters(ctx:Context) is do
		var params : Sequence<LLVMVar> init  Sequence<LLVMVar>.new
		var fctAttr: Sequence<FunctionAttr> init  Sequence<FunctionAttr>.new
		var typeCD : String init ctx.llvm.pointer(ctx.llvm.localVar(qualifiedNameLLVM))
		var selfVar : String init ctx.llvm.localVar("self")
		var returnVar : String init ctx.llvm.localVar("r")
		var tmpVar : String init ctx.llvm.localVar("t")
		var getterName : String
		var cpt : Integer init superTypes.size + 1
		var returnType : String
		
		params.add(LLVMVar.new.initialise(typeCD, selfVar))
		fctAttr.add(FunctionAttr.~readonly)
		fctAttr.add(FunctionAttr.inlinehint)
		
		ownedAttribute.each{attr |
			ctx.reinitOperationVar
			returnType := ctx.llvm.pointer(attr.type.llvmType(ctx))
			getterName := ctx.llvm.globalVar(qualifiedNameLLVM + ".#" + attr.name)
			ctx.getters.append(ctx.llvm.define(LinkageType.internal, returnType, getterName, params, fctAttr)).append("{\n")
			ctx.getters.append(ctx.llvm.assign(tmpVar, ctx.llvm.getelementptr(typeCD, selfVar, ctx.llvm.i32, 0, ctx.llvm.i32, cpt, false))).append("\n")
			ctx.getters.append(ctx.llvm.assign(returnVar, ctx.llvm.load(ctx.llvm.pointer(returnType), tmpVar))).append("\n")
			ctx.getters.append(ctx.llvm.ret(returnType, returnVar)).append("\n")
			ctx.getters.append("}\n")
			cpt := cpt + 1
		}
		
		ownedOperation.each{op | op.generateLLVM(ctx) }
	end
	
	
	// Generates the llvm type corresponding to the class.
	operation generateLLVMType(ctx:Context) is do
		var list : Sequence<LLVMVar> init Sequence<LLVMVar>.new
		// The first element of the type is the dynamic binding table.
		list.add(LLVMVar.new.initialise(ctx.llvm.pointer(ctx.llvm.pointer(ctx.llvm.i8)), void))
		// Creating the supertype parameters
		list.addAll(superTypes.collect{st | LLVMVar.new.initialise(ctx.llvm.pointer(ctx.llvm.localVar(st.qualifiedNameLLVM)), void)})
		ownedAttribute.each{attr | list.add(LLVMVar.new.initialise(ctx.llvm.pointer(attr.type.llvmType(ctx)), void)) }
		ctx.types.append(ctx.llvm.type(ctx.llvm.localVar(qualifiedNameLLVM), list)).append("\n")
	end
	
	
	// Generates the contructor of the class.
	operation generateLLVMConstructor(ctx:Context) is do
		ctx.reinitOperationVar
	 	var typeName : String init ctx.llvm.pointer(ctx.llvm.localVar(qualifiedNameLLVM))
	 	ctx.constructors.append(ctx.llvm.define(LinkageType.internal, typeName, ctx.llvm.globalVar("create_" + qualifiedNameLLVM), void, void)).append("{\n")
		
		// Creation of the local object.
	 	var var1 : String init ctx.nextOperationVar
		ctx.constructors.append(ctx.llvm.assign(var1, ctx.llvm.alloca(ctx.llvm.pointer(ctx.llvm.localVar(qualifiedNameLLVM))))).append("\n")
		// malloc of the object
		var params : Sequence<LLVMVar> init Sequence<LLVMVar>.new
		var fctAttrs : Sequence<FunctionAttr> init Sequence<FunctionAttr>.new
		var var2 : String init ctx.nextOperationVar
		params.add(LLVMVar.new.initialise(ctx.llvm.i64, sizeOf(ctx).toString))
		fctAttrs.add(FunctionAttr.nounwind)
		ctx.constructors.append(ctx.llvm.assign(var2, ctx.llvm.call("@malloc", "noalias " + ctx.llvm.pointer(ctx.llvm.i8), params, fctAttrs))).append("\n")
		params.clear
		fctAttrs.clear
		// bitcasting the created pointer into the expected type.
	 	var var3 : String init ctx.nextOperationVar
		ctx.constructors.append(ctx.llvm.assign(var3, ctx.llvm.bitcast(ctx.llvm.pointer(ctx.llvm.i8) + " " + var2, typeName, false))).append("\n")
		// Storing the allocated pointer into the local variable.
		ctx.constructors.append(ctx.llvm.store(typeName, var3, var1)).append("\n")
		// Loading the pointer
		var var4 : String init ctx.nextOperationVar
		ctx.constructors.append(ctx.llvm.assign(var4, ctx.llvm.load(ctx.llvm.pointer(typeName), var1))).append("\n")
		
		// Creation of the super types.
		var i : Integer init 1
		var istr : String
		var ptrSuperType : String
		var varStr1 : String
		var varStr2 : String
		
		superClasses.each{st |
			varStr1 := ctx.nextOperationVar
			varStr2 := ctx.nextOperationVar
			istr := i.toString
			ptrSuperType := ctx.llvm.pointer(ctx.llvm.localVar(st.qualifiedNameLLVM))
			ctx.constructors.append(ctx.llvm.assign(varStr1, ctx.llvm.call("@\"create_"+st.qualifiedNameLLVM+"\"", ptrSuperType, void, void))).append("\n")
			ctx.constructors.append(ctx.llvm.assign(varStr2,  ctx.llvm.getelementptr(typeName, var4, ctx.llvm.i32, 0, ctx.llvm.i32, i, false))).append("\n")
			ctx.constructors.append(ctx.llvm.store(ptrSuperType, varStr1, varStr2)).append("\n")
			i := i + 1
		}

		if _tableList.size>0 and not isAbstract then
			var varStr3 : String init ctx.nextOperationVar
			var tableName : String init getTableDynBindingName(ctx)
			var tableType : String init ctx.llvm.pointer(getTableDynBindingType(ctx))
	
			// Setting the dynamic binding table to the class.
			_generateDynamicBindingTableSetting(ctx, tableType, tableName, varStr3, typeName, var4, 0)
			// For all the super types, overriding the table.
			var keys : Set<String> init _positionsSuperTypesTable.keys
			var varStrST : String

			keys.each{key |
				varStr3 := ctx.nextOperationVar
				varStrST := ctx.generateCastCall(ctx.constructors, var4, qualifiedNameLLVM, key)
				_generateDynamicBindingTableSetting(ctx, tableType, tableName, varStr3, ctx.llvm.pointer(ctx.llvm.localVar(key)), varStrST, _positionsSuperTypesTable.getValue(key))
			}
		end

		// Returning the created object.
		ctx.constructors.append(ctx.llvm.ret(typeName, var4)).append("\n}\n")
	end
	
	
	operation _generateDynamicBindingTableSetting(ctx:Context, tableType:String, tableName:String, varObjet:String, objectType:String, varContainer:String, position:Integer) is do
		ctx.constructors.append(ctx.llvm.assign(varObjet, ctx.llvm.getelementptr(objectType, varContainer, ctx.llvm.i32, 0, ctx.llvm.i32, 0, false))).append("\n")
		ctx.constructors.append(ctx.llvm.store(ctx.llvm.pointer(ctx.llvm.pointer(ctx.llvm.i8)), 
				ctx.llvm.getelementptr(tableType, tableName, ctx.llvm.i64, 0, ctx.llvm.i64, position, true), varObjet)).append("\n")
	end
	
	
	method defineLowerSuperTypes() is do
		super
		superTypes.each{st |
			if st.isInstanceOf(ClassDefinition) then
				superClasses.add(st.asType(ClassDefinition))
			end
		}
	end
}
