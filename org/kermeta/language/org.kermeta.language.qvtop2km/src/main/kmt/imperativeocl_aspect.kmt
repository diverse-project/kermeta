package imperativeocl;


using root_package
using kermeta::standard
using kermeta::io
using org::kermeta::language
/**
* iteration on a collection similar to collect or select
* TODO : what if there are more iterator ?
*/
aspect class ImperativeIterateExp {

method range2domain(context : Qvt2KermetaContext) : Void is do
	if output==void then
		
		//StdIO.writeln("ImperativeIterateExp")
	
		var iterateCall : org::kermeta::language::behavior::UnresolvedCall init org::kermeta::language::behavior::UnresolvedCall.new
		
		if (name =="xcollect") 
		then
			iterateCall.name:="collect"
		else
			if name == "xselect" then
				iterateCall.name:="select"
			else
				if name == "selectOne" then
					iterateCall.name:="detect"
				else
					iterateCall.name:=name
					StdIO.writeln("name : "+name)
				end
			end
		end
		
		var lambdaExp : org::kermeta::language::behavior::LambdaExpression init org::kermeta::language::behavior::LambdaExpression.new
		
		iterator.each{x|x.toLambdaParameter() lambdaExp.parameters.add(x.output.asType(org::kermeta::language::behavior::LambdaParameter))}
		iterateCall.parameters.add(lambdaExp)
		if body!=void then
			body.range2domain(context)
			lambdaExp.body?=body.output
		end
		
		
		//if condition is instance of TypeExp, then the condition is a type check
		if condition!=void then
			
			condition.range2domain(context)
			if condition.isInstanceOf(essentialocl::TypeExp) then 
				var typeCheckCall : org::kermeta::language::behavior::UnresolvedCall init org::kermeta::language::behavior::UnresolvedCall.new
				typeCheckCall.name:="isInstanceOf"
				typeCheckCall.parameters.add(condition.output.asType(org::kermeta::language::behavior::Expression))
				typeCheckCall.target:=org::kermeta::language::behavior::CallVariable.new.initName(lambdaExp.parameters.one.name)
				lambdaExp.body:=typeCheckCall
				
			else
				lambdaExp.body?=condition.output
			end
		end
		
		source.range2domain(context)
		iterateCall.target?=source.output
		
		if 	name != "selectOne" and
			(condition!=void)
			.andThen{x|condition.isInstanceOf(essentialocl::TypeExp)} then
			
			//a sequence<X> cannot be casted as a sequence<Y> even if X>Y
			//hence we created the deferred sequences
			condition.asType(essentialocl::TypeExp).referredType.range2domain(context)
			var asSeqType : behavior::UnresolvedCall init behavior::UnresolvedCall.new.createProxySequence(condition.asType(essentialocl::TypeExp).referredType.generateType(context))
			asSeqType.target:=iterateCall
			
			
			output:=asSeqType
			
					
		else
			if name =="selectOne" and
			(condition!=void)
			.andThen{x|condition.isInstanceOf(essentialocl::TypeExp)} then
				condition.asType(essentialocl::TypeExp).referredType.range2domain(context)
				var cast : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("asType",true)
				var typeLit : behavior::CallTypeLiteral init behavior::CallTypeLiteral.new
				typeLit.typeref:=condition.asType(essentialocl::TypeExp).referredType.generateType(context).generateTypeRef
				cast.parameters.add(typeLit)
				cast.target:=iterateCall
				output:=cast
			
			else
		
				output:=iterateCall
			end
		end 
		
	end
end

}


aspect class AssignExp {


	




	/** depends on multivaluation of left expression*/
	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then 
			//StdIO.writeln("AssignExp  -left type : "+left.getMetaClass.name)
		
			if left.isInstanceOf(essentialocl::PropertyCallExp)
					.andThen{x|left.asType(essentialocl::PropertyCallExp).referredProperty.upper != 1} 
					.orThen{x|left.isInstanceOf(essentialocl::VariableExp)
						.andThen{x| left.asType(essentialocl::VariableExp).referredVariable.representedParameter.upper  != 1} }
				//left.isInstanceOf(essentialocl::VariableExp).andThen{x| left.asType(essentialocl::VariableExp).referredVariable.representedParameter.upper  != 1} 
				then
				
				var out : behavior::UnresolvedCall init behavior::UnresolvedCall.new
				output:=out
				
				left.range2domain(context)
				out.parameters.add(left.output.asType(behavior::Expression))
				
				out.target:=behavior::UnresolvedCall.new.initialize("qvt::utils::CollectionExtension",false)
				self.~value.each{x|x.range2domain(context)}
				//TODO take care of multivalued assignement
				StdIO.writeln("value size : "+~value.size.toString)
				out.parameters.add(self.~value.one.output.asType(org::kermeta::language::behavior::Expression))
				if self.~value.one.isMulti then
					if isReset() then
						out.name:="clearAndAddAll"
					else
						out.name:="qvtAddAll"
					end
				else
					if isReset() then
						out.name:="clearAndAdd"
					else
						out.name:="add"
					end
				end
			else		
				
				
					
				var out : org::kermeta::language::behavior::Assignment init org::kermeta::language::behavior::Assignment.new
				output:=out
				
				
				//should be an add if type of left is a declared collection
				left.range2domain(context)
				out.target?=left.output
				self.~value.each{x|x.range2domain(context)}
				//TODO take care of multivalued assignement
				StdIO.writeln("value size : "+~value.size.toString)
				out.~value?=self.~value.one.output
				
			end
		end
	end


}
singleton AssignmentHelper {

	operation generateAssignement( leftPart : OclExpression , valuePart : OclExpression , context : Qvt2KermetaContext ) : behavior::Assignment is do 
		result := behavior::Assignment
		if leftPart.isInstanceOf(essentialocl::PropertyCallExp)
				.andThen{x|leftPart.asType(essentialocl::PropertyCallExp).referredProperty.upper != 1} 
				.orThen{x|leftPart.isInstanceOf(essentialocl::VariableExp)
					.andThen{x| leftPart.asType(essentialocl::VariableExp).referredVariable.representedParameter.upper  != 1} }
			//left.isInstanceOf(essentialocl::VariableExp).andThen{x| left.asType(essentialocl::VariableExp).referredVariable.representedParameter.upper  != 1} 
			then
			
			var out : behavior::UnresolvedCall init behavior::UnresolvedCall.new
			output:=out
			
			leftPart.range2domain(context)
			out.parameters.add(leftPart.output.asType(behavior::Expression))
			
			out.target:=behavior::UnresolvedCall.new.initialize("qvt::utils::CollectionExtension",false)
			valuePart.range2domain(context)
			
			out.parameters.add(self.~value.one.output.asType(org::kermeta::language::behavior::Expression))
			if ~valuePart.isMulti then
				if isReset() then
					out.name:="clearAndAddAll"
				else
					out.name:="qvtAddAll"
				end
			else
				if isReset() then
					out.name:="clearAndAdd"
				else
					out.name:="add"
				end
			end
		else		
			
			
				
			var out : org::kermeta::language::behavior::Assignment init org::kermeta::language::behavior::Assignment.new
			output:=out
			
			
			//should be an add if type of left is a declared collection
			leftPart.range2domain(context)
			out.target?=leftPart.output
			~valuePart.range2domain(context)
			out.~value?=~valuePart.output
			
		end
		
	
	end


}

aspect class VariableInitExp {

/*TODO
	check withResult

*/

	method range2domain(context : Qvt2KermetaContext) : Void  is do
		if output==void then
			var out : org::kermeta::language::behavior::VariableDecl
			out:=referredVariable.toVariableDecl(context)
			output:=out
		end
	end

}

aspect class LogExp {
	method range2domain(context : Qvt2KermetaContext) : Void  is do
		if output==void then
			//check condition if exists
			var out : org::kermeta::language::behavior::Conditional init org::kermeta::language::behavior::Conditional.new
			output:=out
			if condition==void then
				var condition : org::kermeta::language::behavior::BooleanLiteral init org::kermeta::language::behavior::BooleanLiteral.new
				condition.~value:=true
				out.condition:=condition
			else
				condition.range2domain(context)
				out.condition?=condition.output
			end
			var block : behavior::Block init behavior::Block.new
			out.thenBody:=block
			
			
			//print the message
			var writelnFeature : behavior::UnresolvedCall init behavior::UnresolvedCall.new
			writelnFeature.isCalledWithParenthesis:=true
			block.statement.add(writelnFeature)
			if argument.size>1 then
				writelnFeature.name:="write"
			else
				writelnFeature.name:="writeln"
			end
			
			var StdIOVar : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("kermeta::io::StdIO",false)
			writelnFeature.target:=StdIOVar
			
			
			argument.one.range2domain(context)
			writelnFeature.parameters.add(argument.one.output.asType(org::kermeta::language::behavior::Expression))
			
			//print the second argument
			if argument.size>1 then
				var writelnFeature2 : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("writeln",true)
				block.statement.add(writelnFeature2)
				
				
				var StdIOVar2 : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("kermeta::io::StdIO",false)
				writelnFeature2.target:=StdIOVar2
				
				
				var toStringFeature : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("toString",true)
				writelnFeature2.parameters.add(toStringFeature)
				argument.elementAt(2).range2domain(context)
				toStringFeature.target?=argument.elementAt(2).output
			end
		end
	end

}

aspect class ReturnExp {

	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then
			var out : behavior::Raise init behavior::Raise.new
			output:=out
			
			var newExp : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("new",false)
			

			var exceptionLiteral : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("root_package::ReturnException",false)
			newExp.target:=exceptionLiteral
				
				
				
			if ~value!=void then
				var returnValueAssign : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("returnWithValue",true)
				out.expression:=returnValueAssign
				returnValueAssign.target:=newExp
				~value.range2domain(context)
				returnValueAssign.parameters.add(~value.output.asType(org::kermeta::language::behavior::Expression))
			else
				out.expression:=newExp				
			end
		end
	end


}

aspect class BreakExp {

	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then
				var out : org::kermeta::language::behavior::Raise init org::kermeta::language::behavior::Raise.new
				output:=out
				
				var newExp : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("new",false)
				
				var exceptionLiteral :behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("root_package::BreakException",false)
				newExp.target:=exceptionLiteral
				
				out.expression:=newExp				
			
		end
	end


}

aspect class ContinueExp {

	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then
				var out : org::kermeta::language::behavior::Raise init org::kermeta::language::behavior::Raise.new
				output:=out
				
				var newExp : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("new",false)

				var exceptionLiteral :behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("root_package::ContinueException",false)
				newExp.target:=exceptionLiteral
				
				out.expression:=newExp				
			
		end
	end


}


/**todo : g√©rer les break ?*/
aspect class SwitchExp{

	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then
			var out : org::kermeta::language::behavior::Conditional init org::kermeta::language::behavior::Conditional.new
			output:=out
			
			var currentCondition : org::kermeta::language::behavior::Conditional init out
			from var iter : kermeta::standard::Iterator<AltExp> init alternativePart.iterator
			until iter.isOff
			loop
				var alternative : AltExp init iter.next
				alternative.body.range2domain(context)
				alternative.condition.range2domain(context)
				currentCondition.condition?=alternative.condition.output
				currentCondition.thenBody?=alternative.body.output
				
				if not iter.isOff then
					var newCondition : org::kermeta::language::behavior::Conditional init org::kermeta::language::behavior::Conditional.new
					currentCondition.elseBody:=newCondition
					currentCondition:=newCondition
				else
					elsePart.range2domain(context)
					
					currentCondition.elseBody?=elsePart.output
				end
			end
		end
	end
}


aspect class CatchExp {

	method range2domain(context : Qvt2KermetaContext) : Void is do 
		if output==void then
			var out : org::kermeta::language::behavior::Rescue init org::kermeta::language::behavior::Rescue.new
			output := out
			if body.empty then 
				out.body.add(org::kermeta::language::behavior::VoidLiteral.new)
			else
				body.each{x|x.range2domain(context)
				out.body.add(x.output.asType(org::kermeta::language::behavior::Expression))
				}
			end
			
			//todo what if many exceptions ?
			exception.one.range2domain(context)
			out.exceptionType:=exception.one.generateType(context).generateTypeRef()
		end
	end
	

}

aspect class BlockExp {

	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then
			var out : org::kermeta::language::behavior::Block init org::kermeta::language::behavior::Block.new
			output:=out
			body.each{x|x.range2domain(context)
				out.statement.add(x.output.asType(org::kermeta::language::behavior::Expression))
				}
			//in qvtop a block return a void
			out.statement.add(org::kermeta::language::behavior::VoidLiteral.new)
			
		end
	end

}


aspect class DictLiteralExp {

	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then 
			var out : org::kermeta::language::behavior::Block init org::kermeta::language::behavior::Block.new
			output:=out
			//generate declaration
			var varDec : org::kermeta::language::behavior::VariableDecl init org::kermeta::language::behavior::VariableDecl.new
			out.statement.add(varDec)
			varDec.identifier:=self.name
			
			varDec.type:=self.type.generateType(context).generateTypeRef()
			
			//add values
			
			part.each{ p|
				var callPut : behavior::UnresolvedCall init behavior::UnresolvedCall.new.initialize("put",true)
				out.statement.add(callPut)
				
				var callTable : org::kermeta::language::behavior::CallVariable init org::kermeta::language::behavior::CallVariable.new
				callPut.target:=callPut
				callTable.name:=varDec.identifier
				p.key.range2domain(context)
				callPut.parameters.add(p.key.output.asType(org::kermeta::language::behavior::Expression))
				p.~value.range2domain(context)
				callPut.parameters.add(p.~value.output.asType(org::kermeta::language::behavior::Expression))
			}
			
			
			
			
		end
	end 


}

aspect class DictionaryType {
	
	method generateType(context : Qvt2KermetaContext) : org::kermeta::language::structure::Type is do
		result?=output
	end
	
	method range2domain(context : Qvt2KermetaContext) : Void is do
		if output==void then 
			var out : org::kermeta::language::structure::Class init org::kermeta::language::structure::Class.new
			output:=out
			var cd : org::kermeta::language::structure::ClassDefinition
			cd?=context.kmtContext.getFromQualifiedName("kermeta::utils::Hashtable")
			out.typeDefinition:=cd 
			var tvarBinding1 : org::kermeta::language::structure::TypeVariableBinding init org::kermeta::language::structure::TypeVariableBinding.new
			tvarBinding1.variable :=cd.typeParameter.one
			out.typeParamBinding.add(tvarBinding1)
			
			elementType.range2domain(context)
			tvarBinding1.type:=elementType.generateType(context)
			
			if tvarBinding1.type.container==void then 
				tvarBinding1.containedType.add(tvarBinding1.type)
			end
			var tvarBinding2 : org::kermeta::language::structure::TypeVariableBinding init org::kermeta::language::structure::TypeVariableBinding.new
			tvarBinding2.variable :=cd.typeParameter.elementAt(1)
			out.typeParamBinding.add(tvarBinding2)
			
			keyType.range2domain(context)
			tvarBinding2.type:=keyType.generateType(context)
			
			if tvarBinding1.type.container==void then 
				tvarBinding1.containedType.add(tvarBinding1.type)
			end

		end
	end
	

}

/*
aspect class ComputeExp { 

	method range2domain() : Void is do
		if output==void then
			var out : org::kermeta::language::behavior::VarDecl init org::kermeta::language::behavior::VarDecl.new
			output:=out
			out.name:=returnedElement.identifier
			
			
		
		end
	end

}*/
