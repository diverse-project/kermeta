/* $Id:$ 
 * Creation : March 1, 2011
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            jfalcou
 */

package org::kermeta::language::structure;


require kermeta

require "platform:/lookup/org.kermeta.language.model/src/main/ecore/kermeta.ecore"

using kermeta::standard

aspect class Package
{
	/**
	 * A package must have a non empty name
	 */
	@Scope "LOADED"
	inv non_empty_name is do
	
		if self.name.isVoid() 
		then
			false
		else
			self.name.size > 0
		end
	end
	
	/**
	 * A Package has two sub packages with the same name.
	 * This might be due to packages having the same name but
	 * different nsUris coming from different files in the project.
	 */
	@Scope "MERGED"
	inv no_subpackages_with_same_name is do
		
		var names : Collection<String> init Sequence<String>.new
		var invResult : Boolean init true

		self.nestedPackage.each{ p | 
			if (names.contains(p.name))
			then
				invResult:=false
			else
				names.add(p.name)
			end
				
		}
		invResult
	end

}