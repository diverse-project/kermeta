package org::kermeta::language::structure;

using kermeta::standard
using kermeta::utils
using kermeta::io::StdIO => stdio
class VisualiserKermetaModel {
	reference requiredClassDefinitions : ClassDefinition[0..*]

	reference requiredModelingUnits : ModelingUnit[0..*]

	reference addedPackages : Bag<Package>

	reference addedClassDefinitions : Bag<ClassDefinition>
	
	attribute considersuperType : Boolean
	
	attribute idPass : Integer
	

	operation initialise(requiredClassDefinitions : bag ClassDefinition[0..*], requiredModelingUnits : bag ModelingUnit[0..*], considersuperType : Boolean) : VisualiserKermetaModel is do
		result := self
		self.addedPackages := Bag<Package>.new
		self.addedClassDefinitions := Bag<ClassDefinition>.new
		self.requiredClassDefinitions.clear()
		self.requiredClassDefinitions.addAll(requiredClassDefinitions)
		self.requiredModelingUnits.clear()
		self.requiredModelingUnits.addAll(requiredModelingUnits)
		self.considersuperType := considersuperType
		
		if(idPass.isVoid) then
			idPass := 0
		else
			idPass := idPass + 1
		end
	end

	operation launch() is do
		onStart()
		self.requiredClassDefinitions.each{theClassDefinition | theClassDefinition.visitToAddClasses(self) }
		self.requiredModelingUnits.each{theModelingUnit | theModelingUnit.visitToAddClasses(self) }
		self.requiredClassDefinitions.each{theClassDefinition | theClassDefinition.visitToAddRelations(self) }
		self.requiredModelingUnits.each{theModelingUnit | theModelingUnit.visitToAddRelations(self) }
		self.addedPackages.each{thePackage | onPackageAdded(thePackage) }
		self.addedClassDefinitions.each{theClassDefinition | onClassDefinitionAdded(theClassDefinition) }
		onEnd()
	end
	
	operation onStart() is do
	end

	
	operation onEnd() is do
	end

	operation onPackageAdded(pkg : Package) is do

		stdio.writeln("package: " + pkg.name)
	
	end
	operation onClassDefinitionAdded(cl : ClassDefinition) is do

		stdio.writeln("clazz: " + cl.name)
	
	end

}
