/* $Id: ActionToPerformOnEcoreModel.kmt,v 1.1 2008-03-14 10:57:56 cfaucher Exp $
 * Creation date: March 04, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors: Cyril Faucher <cfaucher@irisa.fr>
 *
 */

package ecore;
 
require kermeta
require "http://www.eclipse.org/emf/2002/Ecore"
require "platform:/lookup/fr.irisa.triskell.kermeta.ecore/src/kermeta/ecore_behavior.kmt"
require "platform:/lookup/fr.irisa.triskell.kermeta.ecore/src/kermeta/helpers/EcoreHelper.kmt"
require "platform:/lookup/fr.irisa.triskell.kermeta.ecore/src/kermeta/helpers/ContainmentBasedActionPerformer_Ecore.kmt"
require "BinaryMergerSignatureAspectGenerator.kmt"
require "BinaryMergerGenerator_ecore_common_aspect.kmt"

using ecore
using kermeta::language::structure
using kermeta::standard
using kermeta::utils
using EcoreHelpers
using org::kermeta::binarymerger::generator

aspect class EObject {
	operation binaryMergerSignatureAspectKmtPrinter(context : BinaryMergerGeneratorContext) : String is do
		result := ""
	end
	
}
aspect class EModelElement {
	operation binaryMergerSignatureAspectKmtPrinter(context : BinaryMergerGeneratorContext) : String is do
		result := ""
	end
	
}

aspect class EPackage {
	method binaryMergerSignatureAspectKmtPrinter(context : BinaryMergerGeneratorContext) : String is do
		var content : StringBuffer init StringBuffer.new
		if not self.container.isVoid() then 
			content.append(context.indentString)
			content.append("package " + self.name + " {")
			context.pushIndent
		end
		
		self.eachOwnedElement{ e |
			content.append(e.binaryMergerSignatureAspectKmtPrinter(context) + "\n")
		}
		
		if not self.container.isVoid() then
			context.popIndent
			content.append(context.indentString)
			content.append("}\n")
		end
		result := content.toString()
	end
	
}

aspect class EClass {
	method binaryMergerSignatureAspectKmtPrinter(context : BinaryMergerGeneratorContext) : String is do
		var content : StringBuffer init StringBuffer.new
		content.append(context.indentString)
		content.append("aspect class " + self.name + " {")
		
		context.pushIndent
		content.append(context.indentString)
				
		
		if self.eSuperTypes.size()>0 then
			content.append("method")
		else if (context.isMMReflexiveKermeta) then
			// special case of creating  methods on kermeta itself
			if (self.name == "Object") then
				content.append("operation")
			else
				content.append("method")
			end
			
		else
			content.append("operation")
		end end
		
		content.append(" localMatch( aspectElementObj : kermeta::standard::Object) : Boolean")
		
		if self.eSuperTypes.size()>0 then
			 content.append(" from " + context.ecoreHelper.getClassifierQualifiedName(self.eSuperTypes.first))
		end
		
		content.append(" is do")
		context.pushIndent
		content.append(context.indentString)
		content.append("var aspectElement : " + context.ecoreHelper.getClassifierQualifiedName(self))
		content.append(context.indentString)
		content.append("aspectElement ?= aspectElementObj")
		content.append(context.indentString)
		content.append("if not aspectElement.isVoid then\n")
		context.pushIndent
		content.append(context.indentString)
		if self.eSuperTypes.size()>0 then
			if(self.eSuperTypes.first().name == "Object")then
				content.append(context.indentString)
				content.append("// workaround compiler V2 bug, cannot call super on a Class named \"Object\" \n")
				content.append(context.indentString)
				content.append("// result := super( aspectElement)")
				content.append(context.indentString)
				content.append("result := true")
			else
				content.append(context.indentString)
				content.append("result := super( aspectElement)")
			end
		else 
			content.append(context.indentString)
			content.append("result := true")
		end
		
		
								
		self.eachOwnedElement{ e |
				content.append(e.binaryMergerSignatureAspectKmtPrinter(context))
		}
		if self.eSuperTypes.size()>=1 then
			// must deal with EReference that comes from the other inheritance branch
			var allFirstSuperTypes : Set<EClass> init getAllFirstSuperTypes
			self.eAllSuperTypes.each{ s |
				if (not allFirstSuperTypes.contains(s)) then
					s.eachOwnedElement{ e |
						content.append(e.binaryMergerSignatureAspectKmtPrinter(context))
					}
				else					
					if(self.eSuperTypes.first().name == "Object") then
						content.append(context.indentString)
						content.append("// workaround compiler V2 bug, embed code that should have been called via super \n")
						s.eachOwnedElement{ e |
							content.append(e.binaryMergerSignatureAspectKmtPrinter(context))
						}
					end
				end
			}
		end			
		context.popIndent
		content.append(context.indentString)
		content.append("else")
		context.pushIndent
		content.append(context.indentString)
		content.append("result := false")
		context.popIndent
		content.append(context.indentString)
		content.append("end")
		context.popIndent
		content.append(context.indentString)
		content.append("end")
		
		context.popIndent
		content.append(context.indentString)
		result := content.append("}").toString()
	end
	
	
}

aspect class EReference {
	method binaryMergerSignatureAspectKmtPrinter(context : BinaryMergerGeneratorContext) : String is do
		var content : StringBuffer init StringBuffer.new


		
		var mangledName : String init context.ecoreHelper.getMangledIdentifier(self.name) 
		if (self.containment) then
			if self.upperBound==1 then
				content.append(context.indentString)
				content.append("if (not self."+mangledName+".isVoid) and (not aspectElement."+mangledName+".isVoid) then")
				context.pushIndent
				content.append(context.indentString)
				content.append("result := result and self."+mangledName+" == aspectElement."+mangledName)					
				context.popIndent
				content.append(context.indentString)
				content.append("end")		
			end			
		end
				

		result := content.toString()
	end
}

aspect class EAttribute {
	method binaryMergerSignatureAspectKmtPrinter(context : BinaryMergerGeneratorContext) : String is do
		var content : StringBuffer init StringBuffer.new


		 
		var mangledName : String init context.ecoreHelper.getMangledIdentifier(self.name) 
		if self.upperBound==1 then
			content.append(context.indentString)
				content.append("if (not self."+mangledName+".isVoid) and (not aspectElement."+mangledName+".isVoid) then")
				context.pushIndent
				content.append(context.indentString)
				content.append("result := result and self."+mangledName+" == aspectElement."+mangledName)					
				context.popIndent
				content.append(context.indentString)
				content.append("end")			
		end
				

		result := content.toString()
	end
}
