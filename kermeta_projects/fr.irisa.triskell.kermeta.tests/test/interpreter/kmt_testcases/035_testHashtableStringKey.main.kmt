/*****************************************************************************
 * $Id: 035_testHashtableStringKey.main.kmt,v 1.1 2007-09-10 08:35:04 cfaucher Exp $
 * Project : Kermeta (First iteration)
 * Creation date :  
 * File : testHashtable.kmt
 * 		
 * License : EPL
 * Copyright : IRISA / INRIA / Universite de Rennes 1
 * Creation date : Apr 18 11:13:24 2005
 * Author : zdrey
 * Description : 
 *		test for Hashtable
 ***************************************************************************
 */

@testOperation "yes"
@mainClass "testHashtableStringKey::TestHashtable"
@mainOperation "mainTest"

package testHashtableStringKey;

require kermeta

using kermeta::kunit
alias Integer : kermeta::standard::Integer;
alias String : kermeta::standard::String;


class TestHashtable inherits kermeta::kunit::TestCase
{
	
	reference hashtable : kermeta::utils::Hashtable<String, String>
	
	/** used to manually start the test
	 */
	operation mainTest() is do
        var testRunner : TestRunner init TestRunner.new
		testRunner.run(TestHashtable)
		testRunner.printTestResult()
	end
	
	/** initialize things 
	 * automatically called by the test runner
	 */
	method setUp() : Void is do
		hashtable := kermeta::utils::Hashtable<String, String>.new
		hashtable.put("un","one")
		hashtable.put("deux","two")
		hashtable.put("trois","three")
	end
	
	/**
	 * returns an iterator on the keys of the map
	 */
	operation testKeyIterator() : Void is do
		var it : kermeta::standard::Iterator<String>
		setUp
		it := hashtable.keyIterator()
		stdio.writeln(it.next.toString)
		stdio.writeln(it.next.toString)
		stdio.writeln(it.next.toString)
	end

	
	/**
	 * returns an iterator on the keys of the map
	 */
	operation testValueIterator() : Void is do
		var it : kermeta::standard::Iterator<String>
		setUp
		it := hashtable.valueIterator()
		stdio.writeln(it.next.toString)
		stdio.writeln(it.next.toString)
		stdio.writeln(it.next.toString)
	end
	
	/**
	 * returns an iterator on the keys of the map
	 */
	operation testGet() : Void is do
  	    setUp
		assert(hashtable.getValue("un") == "one")
		assert(hashtable.getValue("deux") == "two")
		assert(hashtable.getValue("trois") == "three")
	end
	
	/**
	 * replaces an entry in the Hashtable
	 * adds an entry
	 */
	operation testPut() : Void is do
	    setUp
		hashtable.put("un", "ONE")
		assert(hashtable.getValue("un") == "ONE")
		assert(hashtable.getValue("deux") == "two")
		assert(hashtable.size == 3)
		hashtable.put("quatre", "four")
		assert(hashtable.size == 4)
	end
	
	/**
	 * Removes the entry which key equals 'key'
	 */
	operation testRemove() : Void is do
	    setUp
		hashtable.remove("deux")
		assert(hashtable.size == 2)
//		fail(hashtable.keyIterator.next == 1)
	end
	
	/**
	 * Remove all elements from the Hashtable
	 */
	operation testClear() : Void is do
	    setUp
		hashtable.clear()
		assert(hashtable.size == 0)
		assert(hashtable.getValue("un").isVoid)
	end
	
//	/** test that the values() method contains the values of the hashtable */ 
	operation testValues() : Void is do 
	    setUp
		assert(hashtable.values.size == hashtable.size) 
		assert(hashtable.values.contains(hashtable.getValue("un")))
		assert(hashtable.values.contains(hashtable.getValue("deux")))
		assert(hashtable.values.contains(hashtable.getValue("trois")))
	end
	/** test that the keys() method contains the values of the hashtable */
	operation testKeys() : Void is do
	    setUp
		assert(hashtable.keys.size == hashtable.size)
		assert(hashtable.keys.contains("un"))
		assert(hashtable.keys.contains("deux"))
		assert(hashtable.keys.contains("trois"))
	end

}
