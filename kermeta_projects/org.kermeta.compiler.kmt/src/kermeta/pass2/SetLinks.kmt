/* $Id: SetLinks.kmt,v 1.24 2008-06-13 09:20:33 cfaucher Exp $
 * Creation date: January 21, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors: Cyril Faucher <cfaucher@irisa.fr>
 *			Francois Tanguy <ftanguy@irisa.fr>
 */

package kermeta::language::structure;

 
require kermeta
require "../common/TraceStructure.kmt"
require "http://www.eclipse.org/emf/2002/Ecore"
require "../helper/KM2EcoreHelper.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/KM2EcoreContext.kmt"

using ecore
using km2ecore
using km2ecore::helper::kermeta
using km2ecore::helper::ecore

aspect class ClassDefinition {

	attribute a : kermeta::standard::Collection<String>
	operation setLinks(context : KM2EcoreContext) is do
			
		self.superType.each{ t |
			if t.isInstanceOf(Class) then
			
				var classDefinition : ClassDefinition init t.asType(Class).getClassDefinition()

				if (not self.ecoreModelElement.isVoid()) then // (classDefinition.getEMFInstanceClassName() == "") and
					/*if classDefinition.ecoreModelElement == void then
						stdio.writeln("Class Def is nil" + classDefinition.name)
					end*/
					if not classDefinition.ecoreModelElement.isVoid() then
					
						var new_eGenericType : EGenericType init EGenericType.new
						new_eGenericType.eClassifier := classDefinition.ecoreModelElement
						self.ecoreModelElement.eGenericSuperTypes.add(new_eGenericType)
						
						t.asType(Class).ecoreModelElement := new_eGenericType
						t.asType(Class).setLinks(context)
					end
				end
				
			end
		}
					
	end
}

aspect class Class {
	// Set the links to the EGenericType for the EGenericSuperTypes
	operation setLinks(context : KM2EcoreContext) is do
		self.typeParamBinding.each{ tpb |
				
			var typeArgument : EGenericType init EGenericType.new
			
			if tpb.type.isInstanceOf(Class) then
				var cd : ClassDefinition init tpb.type.asType(Class).getClassDefinition()
								
				if not cd.ecoreModelElement.isVoid() then
					typeArgument.eClassifier := cd.ecoreModelElement
				else //we assume this Class target a subtype of ValueType => EDataType
					typeArgument.eClassifier := cd.ecoreDataType
				end
								
				self.ecoreModelElement.eTypeArguments.add(typeArgument)
			else
				if tpb.type.isInstanceOf(ObjectTypeVariable) then
					var otv : ObjectTypeVariable init tpb.type.asType(ObjectTypeVariable)
					typeArgument.eTypeParameter := otv.ecoreModelElement
					self.ecoreModelElement.eTypeArguments.add(typeArgument)
				else
					if tpb.type.isInstanceOf(PrimitiveType) then
						var cd : ClassDefinition init tpb.type.getClassFromPrimitiveType().getClassDefinition()
						
						//We use the EDataType from the Ecore file
						if tpb.type.isEDataType()!="" then //cd.isValueType() and (tpb.type.getEMFInstanceClassName()!="") then
							typeArgument.eClassifier := tpb.type.asType(PrimitiveType).ecoreModelElement
						else
							//We use the final type
							if not cd.ecoreModelElement.isVoid() then
								typeArgument.eClassifier := cd.ecoreModelElement
							else
								typeArgument.eClassifier := cd.ecoreDataType
							end
						
						end
						
						self.ecoreModelElement.eTypeArguments.add(typeArgument)
					end
				end
			end
		}
	end
}

aspect class Operation {
	operation setLinks(context : KM2EcoreContext) is do
		
		var s : LinkSetterForTypedElement init LinkSetterForTypedElement.new
		s.setLinks(self, self.ecoreModelElement, context)
		if not self.superOperation.isVoid() then
			var qualifiedName : String init self.superOperation.owningClass.qualifiedName
			var ecoreAnnotation : EAnnotation init EAnnotationHelper.new.createKermetaEAnnotation(KermetaEAnnotationKey.superOperation.name.toString(), qualifiedName)
			
			//stdio.writeln("op: " + self.name + " from the ClassDefinition: " + self.container.asType(NamedElement).name + " " + self.ecoreModelElement.toString)
			
			//The super operation is not tagged as EMF_Static
			if self.getEMFStatic()=="" then
				self.ecoreModelElement.eAnnotations.add(ecoreAnnotation)
			end	
		end
		
	end
}

aspect class Parameter {
	operation setLinks(context : KM2EcoreContext) is do
	
		// DETERMINE if the parameter should be compiled
		if self.type.isInstanceOf(FunctionType) then
			stdio.writeln("setLinks FunctionType: " + self.type.toString() + " self.ecoreModelElement: " + self.ecoreModelElement.toString())
			var ft : FunctionType init self.type.asType(FunctionType)
			self.ecoreModelElement.eType := GenericTypeDefinitionHelper.new.getKermetaSpecialType(self.container.container.container.asType(Package))
			
			var ft_annotation : EAnnotation init EAnnotationHelper.new.createEmpty("kermeta.functionType")
			
			if not ft.left.isVoid() then
				EAnnotationHelper.new.addEntry(ft_annotation, "0", ft.left.getTypeName())
			end
			if not ft.left.isVoid() then
				EAnnotationHelper.new.addEntry(ft_annotation, "1", ft.right.getTypeName())
			end
			self.ecoreModelElement.eAnnotations.add(ft_annotation)
			
			//Set the traceability
			ft.ecoreModelElement := ft_annotation
			//stdio.writeln("SetLinks - inline")
			//TODO inline of the FunctionType
		else		
			var s : LinkSetterForTypedElement init LinkSetterForTypedElement.new
			s.setLinks(self, self.ecoreModelElement, context)
		end
		
	end
}

aspect class Property {
	operation setLinks(context : KM2EcoreContext) is do
		var s : LinkSetterForTypedElement init LinkSetterForTypedElement.new
		s.setLinks(self, self.ecoreModelElement, context)
		
		//Complete the structural phase
		if not self.opposite.isVoid() then //if self.opposite.isVoid() then self is an EReference
			self.ecoreModelElement.asType(EReference).eOpposite := self.opposite.ecoreModelElement.asType(EReference)
		end
	end
}


class LinkSetterForTypedElement {
	operation setLinks(element : TypedElement, ecoreModelElement : ETypedElement, context : KM2EcoreContext) is do
		//stdio.writeln(" ++++++++++++----------- " +  element.toString() + " " + element.type.toString() + " " + element.type.getTypeName())
		if not ecoreModelElement.isVoid() then

			if element.type.isInstanceOf(Class) then
				var c : Class init element.type.asType(Class)
				var classDefinition : ClassDefinition init c.getClassDefinition()
				var genericType : EGenericType init EGenericType.new
				
				if not classDefinition.ecoreModelElement.isVoid() then
					genericType.eClassifier  := classDefinition.ecoreModelElement
				else
					genericType.eClassifier  := classDefinition.ecoreDataType
				end
				ecoreModelElement.eGenericType := genericType
				c.typeParamBinding.each{ tpb |
					var typeArgument : EGenericType init EGenericType.new
					
					if tpb.type.isInstanceOf(Class) then
						var cd : ClassDefinition init tpb.type.asType(Class).getClassDefinition()
						if not cd.ecoreModelElement.isVoid() then
							typeArgument.eClassifier := cd.ecoreModelElement
						else //we assume this Class target a subtype of ValueType => EDataType
							typeArgument.eClassifier := cd.ecoreDataType
						end
						ecoreModelElement.eGenericType.eTypeArguments.add(typeArgument)
					else
						if tpb.type.isInstanceOf(ObjectTypeVariable) then
							var otv : ObjectTypeVariable init tpb.type.asType(ObjectTypeVariable)
							typeArgument.eTypeParameter := otv.ecoreModelElement
							ecoreModelElement.eGenericType.eTypeArguments.add(typeArgument)
						else
							if tpb.type.isInstanceOf(PrimitiveType) then
								var cd : ClassDefinition init tpb.type.getClassFromPrimitiveType().getClassDefinition()
								
								if tpb.type.isEDataType()!="" then //cd.isValueType() and (tpb.type.getEMFInstanceClassName()!="") then
									typeArgument.eClassifier := tpb.type.asType(PrimitiveType).ecoreModelElement
								else
								
									if not cd.ecoreModelElement.isVoid() then
										typeArgument.eClassifier := cd.ecoreModelElement
									else
										typeArgument.eClassifier := cd.ecoreDataType
									end
									
								end
								
								ecoreModelElement.eGenericType.eTypeArguments.add(typeArgument)
							end
						end
					end
				}
			else
				// Case of the type is an ObjectTypeVariable
				if element.type.isInstanceOf(PrimitiveType) then
					var cd : ClassDefinition init element.type.getClassFromPrimitiveType().getClassDefinition()
					
					if element.type.isEDataType()!="" then //cd.isValueType() and (element.type.getEMFInstanceClassName()!="") then
						ecoreModelElement.eType := element.type.asType(PrimitiveType).ecoreModelElement
					else
					
						if not cd.ecoreModelElement.isVoid() then
							ecoreModelElement.eType := cd.ecoreModelElement
						else
							ecoreModelElement.eType := cd.ecoreDataType
						end
					
					end
				else
					if element.type.isInstanceOf(Enumeration) then
						ecoreModelElement.eType := element.type.asType(Enumeration).ecoreModelElement
					else
						if element.type.isInstanceOf(ObjectTypeVariable) then
							var genericType : EGenericType init EGenericType.new
							genericType.eTypeParameter  := element.type.asType(ObjectTypeVariable).ecoreModelElement
							ecoreModelElement.eGenericType := genericType
						end
					end
				end
			end
		end
	end
}
