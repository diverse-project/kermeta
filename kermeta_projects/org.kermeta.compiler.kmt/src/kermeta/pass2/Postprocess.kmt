/* $Id: Postprocess.kmt,v 1.2 2008-08-27 16:31:26 cfaucher Exp $ 
 * Creation : August 27, 2008
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            Cyril Faucher <cfaucher@irisa.fr>
 */

package kermeta::language::structure;

require kermeta
require "http://www.eclipse.org/emf/2002/Ecore"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/helper/kermeta/KermetaHelper.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/KM2EcoreContext.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/helper/java/JavaHelper.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/helper/simk/SimkHelper.kmt"

using ecore
using km2ecore
using km2ecore::helper::java
using simk

aspect class ModelingUnit {

	reference superOperationsToCompileInHelper : Operation [0..*]
	
	operation postprocess( context : KM2EcoreContext ) : Void is do
		self.compileSuperOperation(context)
	end
	
	operation compileSuperOperation( context : KM2EcoreContext ) : Void is do 
		
		// Compile the super operation in helper in using Simk
		self.superOperationsToCompileInHelper.each{ op |
			context.staticOperationMode := true
			compileStaticOperation(op, context)
			context.staticOperationMode := false
		}
		
	end
	
	operation compileStaticOperation( op : Operation, context : KM2EcoreContext ) : Void is do
		
		var smContext : SMContext init context.simkModel.retrieveOrCreateContextForSuper(op, context)
		
		if not smContext.isVoid() then
		
			var staticMethod : StaticMethod init StaticMethod.new()
			staticMethod.name := "super_" + IdentifierHelper.new.getMangledIdentifier(op.getFinalName(), context)
			staticMethod.body := op.body.createBehaviorJava(context)
			
			staticMethod.usages := SMUsage.Super
			context.simkModel.staticMethods.add(staticMethod)
			smContext.staticMethods.add(staticMethod)
			
			// Set the return type
			var smReturn : SMReturn init SMReturn.new
			smReturn.type := op.type.createBehaviorJava(context)
			if Operation.new().isVoidType(smReturn.type) then
				smReturn.type := "void"
			end
			staticMethod.sMReturn := smReturn
			
			// By default, a self parameter is added
			var default_param : SMParameter init SMParameter.new
			default_param.name := "self"
			default_param.type := op.container.asType(ClassDefinition).qualifiedName.replace("::", ".")
			staticMethod.sMParameters.add(default_param)
			
			// Management of the Parameters
			op.ownedParameter.each{ p |
				var param : SMParameter init SMParameter.new()
				param.name := p.name
				
				if not p.type.isInstanceOf(FunctionType) then
					param.type := p.type.createBehaviorJava(context)
					if Operation.new().isVoidType(param.type) then
						param.type := "void"
					end
				else
					//This case should never happenned
				end
				
				staticMethod.sMParameters.add(param)
			}
		
		end
	end
	
}