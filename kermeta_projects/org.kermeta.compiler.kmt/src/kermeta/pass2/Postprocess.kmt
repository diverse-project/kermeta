/* $Id: Postprocess.kmt,v 1.14 2009-02-12 16:33:57 mclavreu Exp $ 
 * Creation : August 27, 2008
 * Licence  : EPL 
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors  : 
 *            Cyril Faucher <cfaucher@irisa.fr>
 */

package kermeta::language::structure;

require kermeta
require "http://www.eclipse.org/emf/2002/Ecore"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/helper/kermeta/KermetaHelper.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/KM2EcoreContext.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/helper/java/JavaHelper.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/helper/simk/SimkHelper.kmt"

using ecore
using km2ecore
using km2ecore::helper::java
using simk

aspect class ModelingUnit {

	reference superOperationsToCompileInHelper : Operation [0..*]
	
	operation postprocess( context : KM2EcoreContext ) : Void is do
		self.compileSuperOperation(context)
		self.cleanEObject()
		
		/*context.abstractOperations.each{ o |
			if o.implementationMethods.isVoid().orElse{ e | o.implementationMethods.size()==0 } then
				o.owningClass.subClassDefinitions.collect{e |
					o.implementationMethods.exists{ m | m.owningClass==e}
				}
				stdio.writeln("abstractOperations: " + o.qualifiedName)
			end
		}*/
	end
	
	/**
	 * Compilation of super operations is only mandatory when kermeta code is using super methods from classes converted into Java interfaces
	 * Super operation are declared static in Java code
	 */
	operation compileSuperOperation( context : KM2EcoreContext ) : Void is do 
		
		// Compile the super operation in helper in using Simk
		self.superOperationsToCompileInHelper.each{ op |
			var staticOperationMode_backup : Boolean init context.staticOperationMode
			context.staticOperationMode := true
			compileStaticOperation(op, context)
			context.staticOperationMode := staticOperationMode_backup
		}
		
	end
	
	operation compileStaticOperation( op : Operation, context : KM2EcoreContext ) : Void is do
		
		var smContext : SMContext init context.simkModel.retrieveOrCreateContextForSuper(op, context)
		
		if not smContext.isVoid() then
		
			var staticMethod : SMMethod init SMMethod.new()
			staticMethod.name := "super_" + op.getFinalName(context)
			staticMethod.typeParameters := "G"
			staticMethod.body := op.createBehaviorJava(context)
			
			staticMethod.usages := SMUsage.Super
			context.simkModel.sMMethods.add(staticMethod)
			smContext.sMMethods.add(staticMethod)
			
			// Set the return type
			var smReturn : SMReturn init SMReturn.new
			smReturn.type := op.type.createBehaviorJava(context)
			if Operation.new().isVoidType(smReturn.type) then
				smReturn.type := "void"
			end
			staticMethod.sMReturn := smReturn
			
			// By default, a self parameter is added
			var default_param : SMParameter init SMParameter.new
			default_param.name := "self"
			default_param.type := op.owningClass.qualifiedName.replace(context.KERMETA_SEPARATOR, context.JAVA_SEPARATOR)
			staticMethod.sMParameters.add(default_param)
			
			// Management of the Parameters
			op.ownedParameter.each{ p |
				var param : SMParameter init SMParameter.new()
				param.name := p.name
				
				if not p.type.isInstanceOf(FunctionType) then
					param.type := p.type.createBehaviorJava(context)
					if Operation.new().isVoidType(param.type) then
						param.type := "void"
					end
				else
					//This case should never happened
				end
				
				staticMethod.sMParameters.add(param)
			}
		
		end
	end
	
	operation cleanEObject() : Void is do
		if not getEClassifierByQualifiedName("ecore::EObject").isVoid() then
			getEClassifierByQualifiedName("ecore::EObject").asType(EClass).eOperations.clear()
		end
	end

}