/* $Id: containment_traversable.kmt,v 1.8 2008-01-29 16:15:49 ftanguy Exp $
 * Creation date: January 21, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors: Cyril Faucher <cfaucher@irisa.fr>
 *			Fran√ßois Tanguy <ftanguy@irisa.fr>
 */

/**
 *
 * func : <Object -> Object> because we need to visit the tags.
 *
 */

package kermeta::language::structure;
 
require kermeta
  
using kermeta::language::structure
using kermeta::standard

aspect class Object {
	operation eachOwnedElement(func : <Object -> Object>) : Void is do
    	self.ownedTag.each{ o |
			func(o.asType(Tag))
		}
	end
}

aspect class ModelingUnit {
	method eachOwnedElement(func : <Object -> Object>) : Void is do
		self.packages.each{p|
			func(p.asType(Package))
		}
	end
}
 
aspect class Package {
	method eachOwnedElement(func : <Object -> Object>) : Void is do
        self.ownedTypeDefinition.each{ o |
			func(o.asType(TypeDefinition))
		}
		self.nestedPackage.each{ p |
			func(p.asType(Package))
		} 
	end
}

aspect class Enumeration {
	method eachOwnedElement(func : <Object -> Object>) : Void is do
        self.ownedLiteral.each{ l |
        	func(l.asType(EnumerationLiteral))
		}
	end	
}

aspect class ClassDefinition {
	method eachOwnedElement(func : <Object -> Object>) : Void is do
        self.ownedAttribute.each{ o |
			func(o.asType(MultiplicityElement))
		}
		
		self.ownedOperation.each{ o |
			func(o.asType(MultiplicityElement))
		}
		
		self.~inv.each{ i |
			func(i.asType(Constraint))
		}
		
		self.typeParameter.each{ t | 
		stdio.writeln("class def")
			func(t.asType(TypeVariable))
		}
	end	
}

aspect class Operation {
	method eachOwnedElement(func : <Object -> Object>) : Void is do
        self.ownedParameter.each{ o |
			func(o.asType(Parameter))
		}
		
		self.~pre.each{ o |
			func(o.asType(Constraint))
		}
		
		self.~post.each{ o |
			func(o.asType(Constraint))
		}
		
		self.typeParameter.each{ t |
		stdio.writeln("operation")
			func(t.asType(TypeVariable))
		}
	end	
}
