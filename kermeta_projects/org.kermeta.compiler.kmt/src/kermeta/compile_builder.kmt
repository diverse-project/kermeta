/* $Id: compile_builder.kmt,v 1.4 2008-01-28 17:13:17 ftanguy Exp $
 * Creation date: January 25, 2008
 * License:
 * Copyright:
 * Authors:
 */

package kermeta::language::structure;


require kermeta
require "http://www.eclipse.org/emf/2002/Ecore"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/helpers/CompilerHelper.kmt"

using kermeta::language::structure
using ecore
using compiler::helper::ecore
using compiler::helper::kermeta

aspect class ModelingUnit {
	operation build() : EAnnotation is do
		var ecoreAnnotation : EAnnotation init EAnnotation.new
		ecoreAnnotation.source := "ModelingUnit"
		// Creation of the require entry
		var requireEntry : EStringToStringMapEntry init EStringToStringMapEntry.new
		requireEntry.key := "require"
		var v : String init String.new
		self.requires.each{r|
			v.append( r.uri + "|" )
		}
		requireEntry.~value := v
		// Creation of the using entry
		var usingEntry : EStringToStringMapEntry init EStringToStringMapEntry.new
		usingEntry.key := "using"
		v := String.new
		self.usings.each{u|
			v.append( u.qualifiedName + "|" )
		}
		usingEntry.~value := v
		// Setting the details
		ecoreAnnotation.details.add(requireEntry)
		ecoreAnnotation.details.add(usingEntry)
		result := ecoreAnnotation
	end
}

aspect class Package {
	operation build() : EPackage is do
		var ecorePackage : EPackage init EPackage.new
		ecorePackage.name := self.name
		ecorePackage.nsURI := self.uri
		ecorePackage.nsPrefix := self.uri
		result := ecorePackage	
	end
}

aspect class PrimitiveType {
	operation build() : EDataType is do
		var ecoreDataType : EDataType init EDataType.new
		ecoreDataType.name := self.name
		//ecoreDataType.serializable
		//ecoreDataType.instanceClass
		//ecoreDataType.instanceTypeName
		//ecoreDataType.defaultValue
		ecoreDataType.instanceClassName := "java.lang.Object"
		if ( self.instanceType.isInstanceOf(Class) ) then
			var qualifiedName : String init self.instanceType.asType(Class).typeDefinition.qualifiedName
			ecoreDataType.eAnnotations.add(EAnnotationHelper.new.create("kermeta", "alias", qualifiedName))
		end
		result := ecoreDataType
	end
}

aspect class Enumeration {
	operation build() : EEnum is do
		var ecoreEnumeration : EEnum init EEnum.new
		ecoreEnumeration.name := self.name
		//ecoreEnumeration.serializable
		//ecoreEnumeration.instanceClass
		//ecoreEnumeration.instanceTypeName
		//ecoreEnumeration.instanceClassName
		//ecoreDataType.defaultValue
		result := ecoreEnumeration
	end
}

aspect class EnumerationLiteral {
	operation build() : EEnumLiteral is do
		var ecoreEnumerationLiteral : EEnumLiteral init EEnumLiteral.new
		ecoreEnumerationLiteral.name := self.name
		ecoreEnumerationLiteral.literal := self.name
		ecoreEnumerationLiteral.~value := self.~enumeration.ecoreProxy.eLiterals.size
		//ecoreEnumerationLiteral.instance
		result := ecoreEnumerationLiteral
	end
}

aspect class ClassDefinition {
	operation build() : EClass is do
		var ecoreClass : EClass init EClass.new
		ecoreClass.interface := false
		ecoreClass.name := self.name
		ecoreClass.~abstract := self.isAbstract
		result := ecoreClass
	end
}

aspect class Operation {
	operation build() : EOperation is do
		var ecoreOperation : EOperation init EOperation.new
		ecoreOperation.name := self.name
		ecoreOperation.ordered := self.isOrdered
		ecoreOperation.unique := self.isUnique
		ecoreOperation.lowerBound := self.lower
		ecoreOperation.upperBound := self.upper
		//ecoreOperation.many := 
		//ecoreOperation.required :=
		if ( self.isAbstract ) then
			var ecoreAnnotation : EAnnotation init EAnnotationHelper.new.createAbstract()
			ecoreOperation.eAnnotations.add( ecoreAnnotation )
		end 
		result := ecoreOperation 
	end
}

/**
 *
 */
aspect class Property {
	operation build() : EStructuralFeature is do
		var ecoreStructuralFeature : EStructuralFeature
		if ( self.isComposite and TypeHelper.new.isPrimitiveType(self.type) ) then
			ecoreStructuralFeature := EAttribute.new
			//ecoreStructuralFeature.iD := 
		else
			ecoreStructuralFeature := EReference.new
			ecoreStructuralFeature.asType(EReference).containment := self.isComposite
		end
		ecoreStructuralFeature.name := self.name
		ecoreStructuralFeature.ordered := self.isOrdered
		ecoreStructuralFeature.unique := self.isUnique
		ecoreStructuralFeature.lowerBound := self.lower
		ecoreStructuralFeature.upperBound := self.upper
		//ecoreStructuralFeature.many := 
		//ecoreStructuralFeature.required := 
		//ecoreStructuralFeature.changeable := 
		//ecoreStructuralFeature.volatile := 
		//ecoreStructuralFeature.transient :=
		//ecoreStructuralFeature.defaultValueLiteral := 
		//ecoreStructuralFeature.defaultValue := 
		//ecoreStructuralFeature.unsettable := 
		ecoreStructuralFeature.derived := self.isDerived
		// gestion du read only
		// gestion du default		
		result := ecoreStructuralFeature
	end
}