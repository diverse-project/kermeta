/* $Id: CreateEcoreModelElement.kmt,v 1.12 2008-04-14 07:50:36 cfaucher Exp $
 * Creation date: February 1, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors: Francois Tanguy <ftanguy@irisa.fr>
 *			Cyril Faucher <cfaucher@irisa.fr>
 */

package kermeta::language::structure;


require kermeta
require "http://www.eclipse.org/emf/2002/Ecore"
require "../common/containment_traversable.kmt"
//require "../common/ContainmentBasedActionPerformer_Kermeta.kmt"
require "../helper/KM2EcoreHelper.kmt"
require "../common/exception.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/KM2EcoreContext.kmt"

using ecore
using kermeta::utils
using km2ecore
using km2ecore::helper::ecore
using km2ecore::helper::kermeta
using km2ecore::common::exception

/**
 *
 */
aspect class ModelingUnit {
	operation createEcoreModelElement(context : KM2EcoreContext) : EAnnotation is do
		var ecoreAnnotation : EAnnotation init EAnnotation.new
		ecoreAnnotation.source := "ModelingUnit"
		// Creation of the require entry
		var requireEntry : EStringToStringMapEntry init EStringToStringMapEntry.new
		requireEntry.key := "require"
		var v : StringBuffer init StringBuffer.new
		self.requires.each{r|
			v.append( r.uri + "|" )
		}
		requireEntry.~value := v.toString()
		// Creation of the using entry
		var usingEntry : EStringToStringMapEntry init EStringToStringMapEntry.new
		usingEntry.key := "using"
		v := StringBuffer.new
		self.usings.each{u|
			v.append( u.qualifiedName + "|" )
		}
		usingEntry.~value := v.toString()
		// Setting the details
		ecoreAnnotation.details.add(requireEntry)
		ecoreAnnotation.details.add(usingEntry)
		result := ecoreAnnotation
	end
}

/**
 * FIXME: This should be used to thread the require tag
 */
aspect class Require {
}

/**
 * FIXME: This should be used to thread the require tag
 */
aspect class Using {
}

/**
 *
 */
aspect class Package {
	operation createEcoreModelElement(context : KM2EcoreContext) : EPackage is do
	
		stdio.writeln(" + Package " + self.name + " in progress")
		
		var ecorePackage : EPackage init EPackage.new
		ecorePackage.name := self.name
		
		if not self.uri.isVoid() and self.uri != "" then
			ecorePackage.nsURI := self.uri
		else
			raise UndefinedUriEPackageException.new
		end
		
		// In order complete the EMF checking, we have to set the nsPrefix to "", void is not enough
		ecorePackage.nsPrefix := ""
		result := ecorePackage	
	end
}

/**
 *
 */
aspect class PrimitiveType {
	operation createEcoreModelElement(context : KM2EcoreContext) : EClass is do
		
		var eModelElement : EClass init EClass.new
		
		/*if context.primitiveType_Type == PrimitiveType_Type.edatatype then
			eModelElement := EDataType.new
		else
			if context.primitiveType_Type == PrimitiveType_Type.eclass  then
				eModelElement := EClass.new
			end
		end*/
		
		eModelElement.name := self.name
		//eModelElement.serializable
		//eModelElement.instanceTypeName
		//eModelElement.defaultValue
		
		
		// by default
		eModelElement.instanceClassName := "java.lang.Object"
		if ( self.instanceType.isInstanceOf(Class) ) then

			if TypeHelper.new.isValueType(self.instanceType) then
				var instanceClassName : String init self.instanceType.asType(Class).typeDefinition.getEMFInstanceClassName()
				if instanceClassName != "" then
					eModelElement.instanceClassName := instanceClassName
				end				
			end	

			var qualifiedName : String init self.instanceType.asType(Class).typeDefinition.qualifiedName
			eModelElement.eAnnotations.add(EAnnotationHelper.new.createKermetaEAnnotation(KermetaEAnnotationKey.~alias.name.toString(), qualifiedName))
			eModelElement.instanceClassName := qualifiedName.replace("::", ".")
		end
		
		result := eModelElement
	end
}

/**
 *
 */
aspect class Enumeration {
	operation createEcoreModelElement(context : KM2EcoreContext) : EEnum is do
		var ecoreEnumeration : EEnum init EEnum.new
		ecoreEnumeration.name := self.name
		//ecoreEnumeration.serializable
		//ecoreEnumeration.instanceClass
		//ecoreEnumeration.instanceTypeName
		//ecoreEnumeration.instanceClassName
		//ecoreDataType.defaultValue
		result := ecoreEnumeration
	end
}

/**
 *
 */
aspect class EnumerationLiteral {
	operation createEcoreModelElement(context : KM2EcoreContext) : EEnumLiteral is do
		var ecoreEnumerationLiteral : EEnumLiteral init EEnumLiteral.new
		ecoreEnumerationLiteral.name := self.name
		ecoreEnumerationLiteral.literal := self.name
		ecoreEnumerationLiteral.~value := self.~enumeration.asType(Enumeration).ecoreModelElement.eLiterals.size
		//ecoreEnumerationLiteral.instance
		result := ecoreEnumerationLiteral
	end 
}

/**
 *
 */
aspect class ClassDefinition {
	operation createEcoreModelElement(context : KM2EcoreContext) : EClass is do
		var ecoreClass : EClass init EClass.new
		ecoreClass.interface := false
		ecoreClass.name := self.name
		ecoreClass.~abstract := self.isAbstract
		result := ecoreClass
	end
}

/**
 *
 */
aspect class Operation {
	operation createEcoreModelElement(context : KM2EcoreContext) : EOperation is do
		var ecoreOperation : EOperation init EOperation.new
		ecoreOperation.name := self.name
		ecoreOperation.ordered := self.isOrdered
		ecoreOperation.unique := self.isUnique
		ecoreOperation.lowerBound := self.lower
		ecoreOperation.upperBound := self.upper
		//ecoreOperation.many := 
		//ecoreOperation.required :=
		if ( self.isAbstract ) then
			var ecoreAnnotation : EAnnotation init EAnnotationHelper.new.createAbstract()
			ecoreOperation.eAnnotations.add( ecoreAnnotation )
		end 
		result := ecoreOperation 
	end
}

/**
 *
 */
aspect class Parameter {
	operation createEcoreModelElement(context : KM2EcoreContext) : EParameter is do
		var ecoreParameter : EParameter init EParameter.new
		ecoreParameter.name := self.name
		//ecoreParameter.ordered
		//ecoreParameter.unique
		//ecoreParameter.lowerBound
		//ecoreParameter.upperBound
		//ecoreParameter.many
		//ecoreParameter.required
		result := ecoreParameter
	end
}

/**
 *
 */
aspect class Constraint {
	operation createEcoreModelElement(context : KM2EcoreContext) : EAnnotation is do
		// TODO : Fill the body using the java pretty printer
		var body : String init ""
				
		if ( self.container.isInstanceOf(ClassDefinition) ) then
			result := EAnnotationHelper.new.create("kermeta.inv", self.name, body)		
		else
			if ( ConstraintHelper.new.isPre(self) ) then
				result := EAnnotationHelper.new.create("kermeta.pre", self.name, body)	
			else
				result := EAnnotationHelper.new.create("kermeta.post", self.name, body)
			end
		end
	end
}

/**
 *
 */
aspect class Property {
	operation createEcoreModelElement(context : KM2EcoreContext) : EStructuralFeature is do
		var ecoreStructuralFeature : EStructuralFeature
		var current_type : Type init self.type.asType(Type)
		if TypeHelper.new.isValueType(current_type) or TypeHelper.new.isEnumeration(current_type) then
			ecoreStructuralFeature := EAttribute.new
			//ecoreStructuralFeature.iD := 
		else
			ecoreStructuralFeature := EReference.new
			ecoreStructuralFeature.asType(EReference).containment := self.isComposite
		end
		
		EcoreModelElementHelper.new.setEStructuralFeatureProperties(self, ecoreStructuralFeature)
		
		if ( self.isDerived ) then
			var annotation : EAnnotation init EAnnotationHelper.new.createKermetaEAnnotation(KermetaEAnnotationKey.isReadOnly.name.toString(), self.isReadOnly.toString())
			ecoreStructuralFeature.eAnnotations.add(annotation)
		end
		// management of read only
		// management of default		
		result := ecoreStructuralFeature
	end
}

/**
 *
 */
aspect class ObjectTypeVariable {
	operation createEcoreModelElement(context : KM2EcoreContext) : ETypeParameter is do
		var ecoreTypeParameter : ETypeParameter init ETypeParameter.new
		ecoreTypeParameter.name := self.name
		result := ecoreTypeParameter
	end
}

/**
 *
 */
aspect class Tag {
	operation createEcoreModelElement(context : KM2EcoreContext) : EAnnotation is do
		var ecoreAnnotation : EAnnotation init EAnnotationHelper.new.getKermetaEAnnotation(self)
		
		if ( self.name.isVoid() ) then
		
			EAnnotationHelper.new.addEntry(ecoreAnnotation, KermetaEAnnotationKey.documentation.name.toString(), self.~value)
			//ecoreAnnotation := EAnnotationHelper.new.createKermetaEAnnotation(KermetaEAnnotationKey.documentation.name.toString(), self.~value)
		else
			EAnnotationHelper.new.addEntry(ecoreAnnotation, self.name, self.~value)
			//ecoreAnnotation := EAnnotationHelper.new.createKermetaEAnnotation(self.name, self.~value)
		end
		result := ecoreAnnotation
	end
}
