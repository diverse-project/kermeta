/* $Id: TypeHelper.kmt,v 1.9 2008-04-29 14:52:27 cfaucher Exp $
 * Creation date: January 28, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 */

package kermeta::language::structure;

require kermeta
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/KM2EcoreContext.kmt"

using kermeta::language::structure
using kermeta::standard
using kermeta::utils
using km2ecore

aspect class Type {

	operation isPrimitiveType() : Boolean is do 
		result := self.isInstanceOf(PrimitiveType)
	end
	
	operation isEnumeration() : Boolean is do
		result := self.isInstanceOf(Enumeration)
	end
	
	
	
	operation getClassFromPrimitiveType() : Class is do
		if self.isInstanceOf(PrimitiveType) then
			var pt : PrimitiveType init self.asType(PrimitiveType)
			if ( pt.instanceType.isInstanceOf(Class) ) then
				result := pt.instanceType.asType(Class)
			else
				//deal the case where the alias refers another alias
				result := pt.instanceType.getClassFromPrimitiveType()
			end
		else
			result := void
		end
	end
	
	// Check if the type is a subType of ValueType => we have to consider as a PrimitiveType, i.e.: an EAttribute
	operation isValueType() : Boolean is do
		result := false
		if self.isInstanceOf(ParameterizedType) then
			var pt : ParameterizedType init self.asType(ParameterizedType)
			
			if pt.typeDefinition.isInstanceOf(ClassDefinition) then
				var cd : ClassDefinition init pt.typeDefinition.asType(ClassDefinition)
				if not cd.superType.detect{ sp | sp.isInstanceOf(ParameterizedType) and sp.asType(ParameterizedType).typeDefinition.qualifiedName == "kermeta::standard::ValueType" }.isVoid() then
					result := true
				end
			end
		end
		/*if self.isInstanceOf(ParameterizedType) and (not self.asType(ParameterizedType).typeDefinition.asType(ClassDefinition).superType.detect{ sp | sp.isInstanceOf(ParameterizedType) and sp.asType(ParameterizedType).typeDefinition.qualifiedName == "kermeta::standard::ValueType" }.isVoid()) then
			result := true
		else
			result := false
		end*/
		
		// old code: Check if the type is in the types map
		/*if not result and self.isInstanceOf(Class) then
			var c : Class init self.asType(Class)
			var qualifiedName : String init c.typeDefinition.qualifiedName()
			result := context.javaTypeMapping.types_mapping.containsKey(qualifiedName)
		end*/
	end
	
	
	
	
	
	/** To move to an aspect on the Class Definition Type */
	operation getTypeName() : String is do
		result := void
		if self.isInstanceOf(Class) then
			var cd : ClassDefinition init self.asType(Class).getClassDefinition()
			result := cd.name
			
		else
			if self.isInstanceOf(ObjectTypeVariable) then
				var otv : ObjectTypeVariable init self.asType(ObjectTypeVariable)
				result := otv.name
			else
				if self.isInstanceOf(PrimitiveType) then
					var cd : ClassDefinition init self.getClassFromPrimitiveType().getClassDefinition()
					result := cd.name
				end
			end
		end
	end
}

aspect class Class {
	operation getClassDefinition() : ClassDefinition is do
		result := self.typeDefinition.asType(ClassDefinition)
	end
}

/*enumeration PrimitiveType_Type {
	edatatype;
	eclass;
}*/



