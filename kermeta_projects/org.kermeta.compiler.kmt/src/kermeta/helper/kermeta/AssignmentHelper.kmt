/* $Id: AssignmentHelper.kmt,v 1.7 2009-02-09 17:09:23 cfaucher Exp $
 * Creation date: Nov 06, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors:
 * 			Cyril Faucher <cfaucher@irisa.fr>
 */

package kermeta::language::behavior;

require kermeta
require "KermetaHelper.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/KM2EcoreContext.kmt"

using kermeta::language::structure
using kermeta::standard
using km2ecore
using km2ecore::helper::kermeta

aspect class Assignment {

	attribute valueTypeCastPrefix : String[0..1]
	attribute valueTypeCastSuffix : String[0..1]
	
	attribute cacheCastPrefix : String[0..1]
	attribute cacheCastSuffix : String[0..1]
	
	operation ppCast(context : KM2EcoreContext) : String is do
		result := ""
		if self.isCast() then
			result := "(" + self.getCastTypeQName(context) + ") "
		end
	end
	
	operation ppCastPrefix(context : KM2EcoreContext) : String is do
		result := ""
		if self.isCast() then
			result := self.ppCast(context) + context.OBJECT_UTIL_CLASS + ".asTypeOrVoid("
		end
	end
	
	operation ppCastSuffix(context : KM2EcoreContext) : String is do
		result := ""
		if self.isCast() then
			result := ", \"" + self.getCastTypeQName(context) + "\")"
		end
	end
	
	operation getCastTypeQName(context : KM2EcoreContext) : String is do
		result := ""
		if self.isCast() then
			//FIXME CF 08-08-01 When the bug#6018 will be fixed, self.target.staticType could be self.staticType
			result := self.target.staticType.createBehaviorJava(context)
		end
	end
	
	operation ppValueTypeCastPrefix(context : KM2EcoreContext) : String is do
		result := ""
		if self.requireConversionValueTypeToObject(context) then
			result := context.OBJECT_UTIL_CLASS + ".convertAsObject("
		end
	end
	
	operation ppValueTypeCastSuffix(context : KM2EcoreContext) : String is do
		result := ""
		if self.requireConversionValueTypeToObject(context) then
			result := ")"
		end
	end
	
	//Not used yet
	operation requireConversionObjectToValueType(context : KM2EcoreContext) : Boolean is do
		result := false
		var right_type : String init self.~value.staticType.getTypeQName()

		if right_type == "kermeta::language::structure::Object" then
			if self.target.staticType.isValueType() then
				stdio.writeln("Not implemented yet, a conversion is required, see at: Assignment::requireConversionObjectToValueType")
				result := true
			end
		end
	end
	
	operation requireConversionValueTypeToObject(context : KM2EcoreContext) : Boolean is do
		result := false
		
		var left_type : String init self.target.staticType.getTypeQName()

		if left_type == "kermeta::language::structure::Object" then
			if self.~value.staticType.isValueType() then
				result := true
			end
		end
	end
}
