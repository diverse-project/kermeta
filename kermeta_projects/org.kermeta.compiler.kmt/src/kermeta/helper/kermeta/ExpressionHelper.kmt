/* $Id: ExpressionHelper.kmt,v 1.13 2008-09-08 19:58:26 cfaucher Exp $
 * Creation date: Jul 25, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors:
 * 			Cyril Faucher <cfaucher@irisa.fr>
 */

package kermeta::language::behavior;

require kermeta
require "KermetaHelper.kmt"
require "../java/JavaHelper.kmt"
require "platform:/resource/org.kermeta.compiler.kmt/src/kermeta/KM2EcoreContext.kmt"

using kermeta::language::structure
using kermeta::standard
using kermeta::utils
using km2ecore
using km2ecore::helper::kermeta
using km2ecore::helper::java

aspect class Expression {
	
	attribute beforeLambdaExpressionResult : String [0..1]
	
	method mustBeAssigned() : Boolean is do
		result := false
		
		if self.isInstanceOf(CallVariable)
			or self.isInstanceOf(Literal)
			or self.isInstanceOf(SelfExpression)
			or self.isInstanceOf(CallResult)
			or self.isInstanceOf(VoidType) then
			result := true
		end
		
		if self.isFunctionTypeVariable() then
			result := false
		end
		
	end
	
	operation isFunctionTypeVariable() : Boolean is do
		result := false
		
		if self.isInstanceOf(CallVariable) then
			if self.asType(CallVariable).isFunctionTypeVariable() then
				result := true
			end
		end
	end
	
	/**
	 * Check if the current element is at the end of an Expression, if so a ";" is added
	 */
	operation endOfExpressionFixer() : String is do
		result := ""
		
		if not self.callsFunctionType() then
		
		if self.container.isInstanceOf(Conditional) or self.container.isInstanceOf(Loop) or self.container.isInstanceOf(Block) or self.container.isInstanceOf(Rescue) or self.container.isInstanceOf(Assignment) or self.container.isInstanceOf(VariableDecl) then
			if self.container.isInstanceOf(Assignment) then
				if self.container.asType(Assignment).target == self then
					result := ""
				else
					if self.container.asType(Assignment).target.isCallFeatureAsStaticProperty() then
						result := ""
					else
						result := ";"
					end
				end
			else
				result := ";"
			end
			
		end
		
		end
	end
	
}


aspect class CallVariable {
	
	operation isParameter() : Boolean is do
		result := false
		if self.container.isInstanceOf(CallFeature) then
			var exp : Expression init self.container.asType(CallFeature).parameters.detect{ p |
				p==self
			}
			if not exp.isVoid() then
				result := true
			end
		end
	end
	
	operation isParameterSingle() : Boolean is do
		result := false
		
		if self.isParameter() then
			var cf_ : CallFeature init self.container.asType(CallFeature)
			var i : Integer init -1
			var exp : Expression init cf_.parameters.detect{ p |
				i := i+1
				p==self
			}
			if cf_.staticOperation.ownedParameter.elementAt(i).asType(Parameter).upper==1 then
				result := true
			end
		end
	end
	
	operation isParameterMany() : Boolean is do
		result := false
		
		if self.isParameter() then
			var cf_ : CallFeature init self.container.asType(CallFeature)
			var i : Integer init -1
			var exp : Expression init cf_.parameters.detect{ p |
				i := i+1
				p==self
			}
			if cf_.staticOperation.ownedParameter.elementAt(i).asType(Parameter).upper!=1 then
				result := true
			end
		end
	end
	
	operation getCallFeatureCorrespondingParameter() : Parameter is do
		result := void

		if self.isParameter() then
			var cf_ : CallFeature init self.container.asType(CallFeature)
			var i : Integer init -1
			var exp : Expression init cf_.parameters.detect{ p |
				i := i+1
				p==self
			}
			result := cf_.staticOperation.ownedParameter.elementAt(i).asType(Parameter)
		end
	end
	
	operation isParameterForOwningOperation() : Boolean is do
		result := false
		// The container could not be an Operation, but a derived property
		if self.container.isInstanceOf(CallFeature) and (not self.getContainerAsType(Operation).isVoid()) then
			result := not self.getContainerAsType(Operation).asType(Operation).ownedParameter.detect{ p |
				p.name==self.name
			}.isVoid()
		end
	end
	
	operation isParameterForOwningOperationSingle() : Boolean is do
		result := false
		if not self.retrieveCorrespondingParameter().isVoid() then
			if self.retrieveCorrespondingParameter().upper==1 then
				result := true
			end
		end
	end
	
	operation isParameterForOwningOperationMany() : Boolean is do
		result := false
		if not self.retrieveCorrespondingParameter().isVoid() then
			if self.retrieveCorrespondingParameter().upper!=1 then
				result := true
			end
		end
	end
	
	operation isParameterManyAndForOwningOperation() : Boolean is do
		result := false
		
		if self.isParameterMany() and self.isParameterForOwningOperation() then
			result := true
		end
	end
	
	method isFunctionTypeVariable() : Boolean is do
		result := false
		var ftParam : Parameter init retrieveCorrespondingParameter()
		if not ftParam.isVoid() then
			if ftParam.isFunctionType() then
				result := true
			end
		end
	end
	
	operation getFunctionTypeParameter() : Parameter is do
		var ftParam : Parameter init retrieveCorrespondingParameter()
		if not ftParam.isVoid() then
			if ftParam.isFunctionType() then
				result := ftParam
			end
		end
	end
	
	operation retrieveCorrespondingParameter() : Parameter is do
		result := void
		// The container could not be an Operation, but a derived property
		if not self.getContainerAsType(Operation).isVoid() then
			result := self.getContainerAsType(Operation).asType(Operation).ownedParameter.detect{ p | p.name == self.name }
		end
	end
}

aspect class CallFeature {

	/*operation retrieveTopCallFeatureFunctionType() : CallFeature is do
		
		var return : Boolean init false
		
		var test : CallFeature init self
		
		from return
        until return
        loop
        	return := true
        	if test.target.isInstanceOf(CallFeature) then
        		
            	test := test.target.asType(CallFeature)
            	
            	if test.callsFunctionType() then
            		result := test
            		return := false
            	end
            	
            end
        end
        
	end*/
	
	
	operation isParameter() : Boolean is do
		result := false
		if self.container.isInstanceOf(CallFeature) then
			var exp : Expression init self.container.asType(CallFeature).parameters.detect{ p |
				p==self
			}
			if not exp.isVoid() then
				result := true
			end
		end
	end
	
	// Determine if the callFeature is called by a static method: package util
	operation fromUtil(context : KM2EcoreContext) : Boolean is do
	
		var fName : String  init IdentifierHelper.new.getMangledIdentifier(self.name, context)
		
		//stdio.writeln(" -- " + self.toString + " - " + self.staticOperation.toString + " - " + self.staticOperation.container().toString)
		
		if self.staticOperation.container().asType(ClassDefinition).isValueType()
		
			or (self.staticOperation.container().asType(ClassDefinition).qualifiedName=="kermeta::language::structure::Object"
				and (
					context.frameworkOperations.contains(fName)
						or ( (not target.isInstanceOf(CallVariable)) 
								and (not target.isInstanceOf(CallResult))
								and (not target.isInstanceOf(CallFeature))
								and (not target.isInstanceOf(SelfExpression))
							)
					)
				)
			
			or (self.staticOperation.container().asType(ClassDefinition).qualifiedName=="kermeta::language::structure::Type"
				and (
					context.frameworkOperations.contains(fName)
						or ( (not target.isInstanceOf(CallVariable)) 
								and (not target.isInstanceOf(CallResult))
								and (not target.isInstanceOf(CallFeature))
								and (not target.isInstanceOf(SelfExpression))
							)
					)
				)
		then
			result := true
		else
			result := false
		end
						
	end
	
	// Determine if the callFeature refers a method owning by the class definition: "Class"
	operation isFromClass() : Boolean is do
		result := false
		if not self.staticOperation.isVoid() then
			if self.staticOperation.container.asType(ClassDefinition).qualifiedName=="kermeta::language::structure::Class" then
				result := true
			end
		end
	end

	operation createStringFromFunctionType(context : KM2EcoreContext) : String is do
		stdio.writeln("toto ---------- createStringFromFunctionType: " + self.name)
		var tgt : Expression init self.target
		
		//context.tasks_stack.push("FunctionType_Compilation")
		
		//Retrieve the corresponding CallFeature for setting the name of the searched call variable
		/*self.staticOperation.ownedParameter.each{ p |
			if p.type.isInstanceOf(FunctionType) then
				context.current_param_ft.push(p.name)
				stdio.writeln("context.current_param_ft.peek(): " + context.current_param_ft.peek())
			end
		}*/
		
		var map_type : Hashtable<Type, Type> init Hashtable<Type, Type>.new()
		var inc_tp : Integer init 0
		self.staticTypeVariableBindings.each{ type_ |
			map_type.put(self.staticOperation.typeParameter.elementAt(inc_tp), type_)
			inc_tp := inc_tp + 1			
		}
		context.ftMapTypeStack.push(map_type)
		
		//Lambda expression to inline
		var ftRightType : Type init Type.new()
		if not self.parameters.detect{param | param.isInstanceOf(LambdaExpression)}.isVoid() then
			
			var theLambdaExp : LambdaExpression init self.parameters.detect{param | param.isInstanceOf(LambdaExpression)}.asType(LambdaExpression)
		
			context.ftStack.push(theLambdaExp)
			
			self.staticOperation.lambdaExpression := theLambdaExp
			
			if theLambdaExp.staticType.isInstanceOf(FunctionType) then
			
				var current_functionType : FunctionType init theLambdaExp.staticType.asType(FunctionType)
				
				if current_functionType.left.isInstanceOf(ProductType) then
					context.ftProductTypeStack.push(current_functionType.left.asType(ProductType))
				end
				
				ftRightType := current_functionType.right
			end
		end
		
		var ft_str : String init ""
		
		context.pushFtSuffix()
		
		//Replacing the self
		if tgt.callsFunctionType() then
			//context.ftSuffixActivation := true
			context.ftContextualSelf := context.SELF_EXPRESSION_TO_REPLACE// + context.printFtSuffix()
			//context.ftSuffixActivation := false
		else
		//if context.ftContextualSelf=="" then
			context.ftContextualSelf := tgt.createBehaviorJava(context)
		end

		context.ftSuffixActivation := true

		var callElement : String init ""
		if self.container.isInstanceOf(VariableDecl) then
			callElement := IdentifierHelper.new.getMangledIdentifier(self.container.asType(VariableDecl).identifier, context)
		else
			if self.container.isInstanceOf(CallVariable) then
				callElement := IdentifierHelper.new.getMangledIdentifier(self.container.asType(CallVariable).name, context)
			end
		end
		
		if (callElement != "") or self.container.isInstanceOf(Assignment) then
			ft_str := ft_str + "null;\n"
		end
		
		ft_str := ft_str + context.SEPARATOR_STATEMENT_FT + "//Beginning of the Inlining of the function type: " + self.staticOperation.name + "\n"
		
		context.ftSuffixActivation := false
		var type_ftReturnType : String init self.staticType.createBehaviorJava(context)
		context.ftSuffixActivation := true
		
		context.ftReturnType := type_ftReturnType
		
		ft_str := ft_str + self.staticOperation.createBehaviorJava(context)
		
		if callElement != "" then
			ft_str := ft_str + "\n//callElement\n"
			ft_str := ft_str + callElement + " = result" + context.printFtSuffix() + ";\n"
		end
		
		if (callElement == "") and (not Operation.new().isVoidType(self.staticType.getTypeName())) then
			//ft_str := ft_str + self.staticType.createBehaviorJava(context) + " res_res = result" + context.printFtSuffix() + ";\n"
			//context.ftContextualSelf := "result" + context.printFtSuffix()
			
			context.resultOfLastFt := "result" + context.printFtSuffix()
		end
		
		ft_str := ft_str + "//End of the Inlining of the function type: " + self.staticOperation.name + "\n"
		
		//Pop all the stacks related to the function type compilation
		context.ftSuffixActivation := false
		context.ftMapTypeStack.pop()
		context.ftProductTypeStack.pop()
		context.ftSuffix.pop()
		//context.current_param_ft.pop()
		
		result := ft_str
	end


	/**
	 * Unused for the moment
	 */
	operation getParamBindings() : String is do
		result := ""
		var i : Integer init 0
		self.parameters.each{ param |
			if param.isInstanceOf(LambdaExpression) then
				if param.asType(LambdaExpression).staticType.isInstanceOf(ProductType) then
					param.asType(LambdaExpression).staticType.asType(ProductType).type.each{ t |
						if i==1 then
							result := result + ", "
						else
							i := 1
						end
						result := result + t.getTypeQName()
					}
				end
			end
		}
		if result != "" then
			result := "<" + result + ">"
		end
	end

}