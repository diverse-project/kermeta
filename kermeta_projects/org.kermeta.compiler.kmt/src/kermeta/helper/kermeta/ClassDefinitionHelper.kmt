/* $Id: ClassDefinitionHelper.kmt,v 1.5 2008-06-26 12:49:56 cfaucher Exp $
 * Creation date: May 16, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors: Cyril Faucher <cfaucher@irisa.fr>
 */

package kermeta::language::structure;

require kermeta

require "TagHelper.kmt"

using kermeta::standard
using kermeta::utils

aspect class ClassDefinition {

	//Get all the super types for a ClassDefinition
	operation allSuperTypes() : Set<Type> is do
	 
		var allSuperClasses : Set<Type> init Set<Type>.new
        var superClasses : Set<Type> init Set<Type>.new
        superClasses.addAll(self.superType)
        
        from  not superClasses.isEmpty
        until superClasses.isEmpty
        loop
            allSuperClasses.addAll(superClasses)
            var temp : Set<Type> init Set<Type>.new
             superClasses.each{ s |
             	if s.isInstanceOf(ParameterizedType) and s.asType(ParameterizedType).typeDefinition.isInstanceOf(ClassDefinition) then
                	temp.addAll(s.asType(ParameterizedType).typeDefinition.asType(ClassDefinition).superType)
                end
             }
             superClasses.clear()
             superClasses.addAll(temp)
        end
        
        result := allSuperClasses
	end
	
	//Check if the ClassDefinition is a subType of ValueType => we have to consider as a EDataType, i.e.: an EAttribute
	operation isValueType() : Boolean is do
		result := false

		var res : Type init self.allSuperTypes.detect{ st |
			st.isInstanceOf(ParameterizedType) and st.asType(ParameterizedType).typeDefinition.qualifiedName == "kermeta::standard::ValueType"
		}
		
		if not res.isVoid() then
			result := true
		end

	end
	
	operation getCollectionInfos() : Hashtable<String, String> is do
		
		var set_infos : OrderedSet<String> init OrderedSet<String>.new()
		var map_infos : Hashtable<String, String> init Hashtable<String, String>.new()
		
		set_infos := self.getEMFNeedReference().split(" ")
		
		set_infos.each{ i |
			var tmp_set_infos : OrderedSet<String> init i.split("=")
			if tmp_set_infos.size()>=2 then
				if not tmp_set_infos.elementAt(0).isVoid() then
					if (not tmp_set_infos.elementAt(1).isVoid()) and (tmp_set_infos.elementAt(1)!="") then
						stdio.writeln("tmp_set_infos.elementAt(0) " + tmp_set_infos.elementAt(0) + " " + tmp_set_infos.elementAt(1))
						map_infos.put(tmp_set_infos.elementAt(0), tmp_set_infos.elementAt(1))
					end
				end
			end
		}
		
		result := map_infos
	end
}