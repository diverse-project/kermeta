/* $Id: EAnnotationHelper.kmt,v 1.5 2008-04-02 08:39:30 cfaucher Exp $
 * Creation date: January 29, 2008
 * License: EPL
 * Copyright: IRISA / INRIA / Universite Rennes 1
 * Authors: Francois Tanguy <ftanguy@irisa.fr>
 */

package km2ecore::helper::ecore;


require kermeta
require "http://www.eclipse.org/emf/2002/Ecore"

using kermeta::language::structure
using ecore

class EAnnotationHelper {
	
	operation createKermetaEAnnotation(key : String, ~value : String) : EAnnotation is do
		result := create(KermetaEAnnotationSource.kermeta.name.toString(), key, ~value)
	end

	operation createAbstract() : EAnnotation is do
		result := createKermetaEAnnotation(KermetaEAnnotationKey.isAbstract.name.toString(), "true")
	end

	operation create(source : String, key : String, ~value : String) : EAnnotation is do 
		var annotation : EAnnotation init EAnnotation.new
		annotation.source := source
		var entry : EStringToStringMapEntry init EStringToStringMapEntry.new
		entry.key := key
		entry.~value := ~value 
		annotation.details.add(entry)
		result := annotation
	end
	
	operation addAnnotationGenModelImpl( eModelElement : EModelElement , body : String) : Void is do
		eModelElement.eAnnotations.add(create("http://www.eclipse.org/emf/2002/GenModel", "body", body))
	end
}

/** Enumeration that lists the different keys among the Kermeta annotation in the Ecore side */
enumeration KermetaEAnnotationSource {
	kermeta;
	/*kermeta.inv;
	kermeta.~pre;
	kermeta.~post;*/
}

enumeration KermetaEAnnotationKey {
	isAbstract;
	~alias;
	superOperation;
	isReadOnly;
	documentation;
}
