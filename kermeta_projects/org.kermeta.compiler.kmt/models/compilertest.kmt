/* $Id: compilertest.kmt,v 1.2 2008-09-02 13:07:16 cfaucher Exp $ 
 * Creation : August 18, 2008
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            cfaucher <cfaucher@irisa.fr>
 */
@mainClass "simpletest::Main"
@mainOperation "main"

@uri "http://www.kermeta.org/simpletest"
package simpletest;

require kermeta
require "http://www.kermeta.org/fsm"

using kermeta::persistence
using fsm
using kermeta::standard

class Main {

	operation main() : Void is do
		
		stdio.writeln("Beginning of the test")
		
		var theFSM : FSM init load("platform:/resource/org.kermeta.compiler.kmt/fsmmodels/sample1.fsm")
		
		stdio.writeln("\n" + theFSM.toString())
		
		theFSM.ownedState.each{ s |
			stdio.writeln(s.name)
		}
		
		var newState : State init State.new()
		newState.name := "newState"
		theFSM.ownedState.add(newState)
		
		stdio.writeln("\nAdding the NewState")
		theFSM.ownedState.each{ s |
			stdio.writeln(s.name)
		}
		
		var strings : Sequence<String> init theFSM.ownedState.collect{ s |
			s.name
		}
		
		strings.each{ s |
			stdio.writeln("from the sequence: " + s)
		}
		
		save(theFSM, "platform:/resource/org.kermeta.compiler.kmt/fsmmodels/sample1.new.fsm")
		
		//Detection and remove
		var detectedState : State init theFSM.ownedState.detect{ s |
			s.name == "newState"
		}
		if not detectedState.isVoid() then
			stdio.writeln("\nnewState has been detected")
			theFSM.ownedState.remove(detectedState)
			stdio.writeln("newState has been removed")
		end
		
		theFSM.ownedState.each{ s |
			stdio.writeln(s.name)
		}
		
		save(theFSM, "platform:/resource/org.kermeta.compiler.kmt/fsmmodels/sample1.remove.fsm")
		
		stdio.writeln("\nTest of times:")
		5000.times{ i |
			stdio.writeln("times: " + i.toString() + ", " + (i*2).toString() + ", " + (i+1).toString() + ", " + (i*5-1).toString() )
		}
		
		stdio.writeln("\nEnd of the test")
	end
	
	operation load(modelInputPath : String) : FSM is do
		var modelResource : Resource init EMFRepository.new.createResource(modelInputPath, "http://www.kermeta.org/fsm")		
	    modelResource.load()
	    result := modelResource.instances.one.asType(FSM)
	end
	
	operation save(root : FSM, modelInputPath : String) : Void is do
		var modelResource : Resource init EMFRepository.new.createResource(modelInputPath, "http://www.kermeta.org/fsm")
	    modelResource.add(root)
	    modelResource.save()
	end
	
}