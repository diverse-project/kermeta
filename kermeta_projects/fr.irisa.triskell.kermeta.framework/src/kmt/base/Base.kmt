package kermeta::standard;

/***************************************************
 * Franck FLEUREY - 11/02/2005
 * Triskell Research Group
 * IRISA / University of Rennes 1 (France)
 * Distributed under the terms of the GPL licence
 *
 * Implementation of Metacore base types.
 *************************************************** */

/**
 * The abstract class ValueType does not contain any feature
 * but any instance of a sub-class of ValueType will be passed by value 
 * instead of by reference for any other objects.
 */
abstract class  ValueType {}

/**
 * The abstract class Comparable defines a set of operation
 * to compare instances of a class.
 * Sub-classes only has to implement abstract operation compareTo.
 * All other methods are defined w.r.t. the compareTo operation
 */
abstract class Comparable {
	
	/**
	 * This method should be implemented by any class that inherits
	 * from Comparable.
	 * returns 0 if self = other
	 * returns > 0 if  self > other
	 * returns < 0 if  self < other
	 */
	operation compareTo(other : Self) : Integer is abstract
	
	/**
	 * return true if self > other
	 */
	operation isGreater(other : Self) : Boolean is do
		self.compareTo(other) > 0
	end
	
	/**
	 * return true if self < other
	 */
	operation isLower(other : Self) : Boolean is do
		self.compareTo(other) < 0
	end
	
	/**
	 * return true if self <= other
	 */
	operation isLowerOrEqual(other : Self) : Boolean is do
		self.compareTo(other) <= 0
	end
	
	/**
	 * return true if self >= other
	 */
	operation isGreaterOrEqual(other : Self) : Boolean is do
		self.compareTo(other) >= 0
	end
} // Comparable



/**
 * The Boolean type
 */
class Boolean
{
	/**
	 * return self and other
	 */ 
	operation ~and(other : Boolean) : Boolean is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Boolean.~and(other)
	end
	
	/**
	 * return not self
	 */ 
	operation ~not() : Boolean is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Boolean.~not()
	end
	
	/**
	 * return self or other
	 */ 
	operation ~or(other : Boolean) : Boolean is do
		// using the 'and' and 'not' operators
		result := not (self.~not() and other.~not()) 
	end
} // Boolean

/**
 * The abstract class Numeric is the root class for all
 * numeric types
 */
abstract class Numeric inherits Comparable, ValueType
{ 
	/**
	 * Comparaison between two numerics
	 */
	method compareTo(other : Self) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Numeric.compareTo(other)
	end
}

/**
 * The class Integer
 */
class Integer inherits Numeric
{
	/**
	 * returns self + other
	 */
	operation plus(other : Integer) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Integer.plus(other)
	end
	
	/**
	 * returns self - other
	 */
	operation minus(other : Integer) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Integer.minus(other)
	end
	
	/**
	 * returns self * other
	 */
	operation times(other : Integer) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Integer.times(other)
	end
	
	/**
	 * returns self / other
	 */
	operation div(other : Integer) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Integer.div(other)
	end
	
	/**
	 * returns self modulo other
	 */
	operation mod(other : Integer) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Integer.mod(other)
	end
	
	/**
	 * returns -self
	 */
	operation uminus() : Integer is do
		result := 0 - self
	end
	
	/**
	 * Convert self as a Real
	 */
	operation toReal() : Real is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Integer.toReal()
	end
	
	/**
	 * returns the Integer value of String str
	 */
	operation parse(str : String) : Integer is do
		extern fr::irisa::triskell::kermeta::runtime::statics::Integer.parse()
	end
}

/**
 * class Real
 */
class Real inherits Numeric
{
	/**
	 * returns self + other
	 */
	operation plus(other : Numeric) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Real.plus(other)
	end
	
	/**
	 * returns self - other
	 */
	operation minus(other : Numeric) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Real.minus(other)
	end
	
	/**
	 * returns self * other
	 */
	operation times(other : Numeric) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Real.times(other)
	end
	
	/**
	 * returns self / other
	 */
	operation div(other : Numeric) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Real.div(other)
	end
	
	/**
	 * Convert self as a Real
	 */
	operation toInteger() : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Real.toInteger()
	end
	
	/**
	 * returns -self
	 */
	operation uminus() : Numeric is do
		result := 0 - self
	end
}

/**
 * The class Character
 */
class Character inherits ValueType, Comparable
{
	/**
	 * Comparaison
	 */
	method compareTo(other : Self) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::Character.compareTo(other)
	end
}

/**
 * The class String
 */
class String inherits ValueType, Comparable 
{

	/**
	 * Comparaison
	 */
	method compareTo(other : Self) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::String.compareTo(other)
	end
	
	/**
	 * returns a new string self + other
	 */
	operation plus(other : String) : String is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::String.plus(other)
	end
	
	/**
	 * Appends other at the end of self
	 */
	operation append(other : String) : Void is do
		extern fr::irisa::triskell::kermeta::runtime::statics::String.append(other)
	end
	
	/**
	 * returns the character at index in the String
	 */
	operation elementAt(index : Integer) : Character is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::String.elementAt(index)
	end
	
	/**
	 * returns the index within this string of the first 
	 * occurrence of the specified substring
	 */
	operation indexOf(str : String) : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::String.indexOf(str)
	end
	
	/**
	 * returns a new string that is a substring of this string
	 */
	operation subString(index : Integer, length : Integer) : String is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::String.subString(index, length)
	end
	
	operation size() : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::statics::String.size()
	end
}

/**
 * Unlimited natural is used for reflection.
 * It is mapped to integer.equals().
 * Any negative number is interpreted as '*'
 */
alias UnlimitedNatural : Integer ;

/***************************************************
 *                  END OF FILE
 *************************************************** */ 
