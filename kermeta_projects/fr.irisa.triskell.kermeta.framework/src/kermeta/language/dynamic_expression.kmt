/**
 * Contains a set of classes whose function is to evaluate a kermeta 
 * expression written as a string.
 * <img src="platform:/plugin/fr.irisa.triskell.kermeta.documentation/src/figures/interpreter_package.png"/>
 */
   @uri "http://www.kermeta.org/kermeta/interpreter"
package kermeta::interpreter;

require "../Standard.kmt"

using kermeta::standard
using kermeta::utils
using kermeta::language::structure

/**
 * DynamicExpression is a class used to make dynamic calls, i.e dynamic evaluation
 * of Kermeta statements represented in a string.
 * 
 * An example :
 *   var de : DynamicExpression init DynamicExpression.new
 *   de.initializeDefaults
 *   de.formalParameters.put("a", String)
 *   de.parse("stdio.writeln(a)") // note: to execute a list of statements, you have to surround them with do..end
 *   var params : Hashtable<String, Object> init Hashtable<String, Object>.new
 *   params.put("a", "hello world!")
 *   de.execute(void, params)
 * <img src="platform:/plugin/fr.irisa.triskell.kermeta.documentation/src/figures/dynamic_expression_view.png"/>
 */
class DynamicExpression
{
	/**
	 * Parse errors and type errors
	 */
	reference errors : KermetaError[*]

	/**
	 * the expression
	 */
	reference expression : kermeta::language::behavior::Expression

	/**
	 * The type of the self object
	 * or void if the expression is not to be executed
	 * in the context of an object
	 */
	reference selfClass : ClassDefinition

	/**
	 * Formal parameters of the expression (bindings between parameters' names and types)
	 */
	reference formalParameters : Hashtable<String, Type>

	/**
	 * Initializes the formalParameters with an empty set of parameters
	 * and the self type with void
	 */
	operation initializeDefaults() is do
		formalParameters := Hashtable<String, kermeta::language::structure::Type>.new
		selfClass ?= Object.typeDefinition
	end

	/**
	 * Parse the expression as a string
	 * and updates the expression field and errors
	 * Returns true if the expression was parsed sucessfuly
	 */
    operation parse(expression : String) : Boolean is do
    	errors.clear
    	if formalParameters == void or selfClass == void then
    		var e : kermeta::exceptions::DynamicExpressionException init kermeta::exceptions::DynamicExpressionException.new
    		e.message := "parse precondition - formalParameters or selfType is void"
    		raise e
    	end
    	@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.DynamicExpressionUtil.parse(this, expression)"
        result ?= extern fr::irisa::triskell::kermeta::runtime::language::DynamicExpression.parse(self, expression)
        
    	if errors.size != 0 then
    		var e : kermeta::exceptions::DynamicExpressionException init kermeta::exceptions::DynamicExpressionException.new
    		e.message := "parse result - expression contains errors"
    		errors.each{ error |
    			e.message.append("\n"+error.message)
    		}
    		raise e
    	end
    end
    
    /**
     * Execute the expression in the given context
     */
    operation execute(selfObj : Object, actualParams : Hashtable<String, Object>) : Object is do
    	if expression == void then
    		var e : kermeta::exceptions::DynamicExpressionException init kermeta::exceptions::DynamicExpressionException.new
    		e.message := "execute precondition - expression is void"
    		raise e
    	end
    	if errors.size != 0 then
    		var e : kermeta::exceptions::DynamicExpressionException init kermeta::exceptions::DynamicExpressionException.new
    		e.message := "execute precondition - expression contains errors"
    		raise e
    	end
    	@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.DynamicExpressionUtil.execute(this, selfObj, actualParams)"
    	result := extern fr::irisa::triskell::kermeta::runtime::language::DynamicExpression.execute(self, selfObj, actualParams)
    end
    
    /**
     * Execute the expression given as a string.
     * the self type and formal parameters are supposed to be set
     * before calling this operation
     */
    operation evalString(body : String, selfObj : Object, actualParams : Hashtable<String, Object>) : Object is do
    	if not parse(body) then
    		var e : kermeta::exceptions::DynamicExpressionException init kermeta::exceptions::DynamicExpressionException.new
    		e.message := "evalString - expression contains errors"
    		raise e
    	end
    	result := execute(selfObj, actualParams)
    end
}


// MOVED TO PACKAGE kermeta::exceptions
//class DynamicExpressionException inherits kermeta::exceptions::Exception {}


/**
 * Kermeta language error in a code written in Kermeta
 */
abstract class KermetaError {
	/**
	 * Error message
	 */
	attribute message : String
}


/**
 * Kermeta error for parsing problems
 */
class ParseError inherits KermetaError {}


/**
 * Type errors in a code written in Kermeta
 */
class TypeError inherits KermetaError {}
