/* $Id: structure.kmt,v 1.39 2007-05-30 11:34:42 jsteel Exp $
 * Project : framework
 * File : structure.kmt
 * License : EPL
 * Copyright : IRISA / INRIA / Universite de Rennes 1
 * ----------------------------------------------------------------------------
 * Creation date : Jul 04, 2005
 * Authors :
 *  Franck Fleurey  ffleurey@irisa.fr 
 *  Zoe Drey 		zdrey@irisa.fr
 *  Jean-Philippe Thibault 		jpthibault@irisa.fr
 */
 
/**
 * Contains the concrete implementation (including the operation bodies) 
 * of the classes of the <code>kermeta::reflection</code> package. If you need
 * documentation for elements marked as "Undocumented", please take a look
 * at the <code>kermeta::reflection</code> package which is more complete.
 */
package kermeta::language::structure; 


require "../Standard.kmt"
require "behavior.kmt" 

alias String : kermeta::standard::String;
class Object inherits kermeta::reflection::Object, KMStructureVisitable
{
	method getMetaClass() : kermeta::reflection::Class from kermeta::reflection::Object is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.getMetaClass(self)
	end
	
	method container() : kermeta::reflection::Object from kermeta::reflection::Object is do
		result := extern fr::irisa::triskell::kermeta::runtime::language::Object.container(self)
	end
	
	method checkInvariants() : Void from kermeta::reflection::Object is do
		extern fr::irisa::triskell::kermeta::runtime::language::Object.checkInvariants(self)
	end
	
	method checkAllInvariants() : Void from kermeta::reflection::Object is do
		if(self!=void) then
			// Call the invariant verification
			self.checkInvariants
			// Invariant checking on each value of self properties			
			self.getMetaClass.ownedAttribute.each { subObj |
				if ( subObj.isComposite) then
					if ( self.get(subObj).getMetaClass.typeDefinition.name.equals("ReflectiveSequence") 
						or self.get(subObj).getMetaClass.typeDefinition.name.equals("ReflectiveCollection") ) then

						var subObjInstances : kermeta::standard::Collection<kermeta::reflection::Object>
						subObjInstances ?= self.get(subObj)
						subObjInstances.each { aSubObjInstances | aSubObjInstances.checkAllInvariants }
					else
						self.get(subObj).checkAllInvariants
					end
				end
			}
		end
	end
	
	method equals(element : kermeta::reflection::Object) : Boolean from kermeta::reflection::Object is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.equals(self, element)
	end
	
	method isNotEqual(element : kermeta::reflection::Object) : Boolean from kermeta::reflection::Object is do
		result := not equals(element)
	end		
	
	method isKindOf(cl : kermeta::reflection::Class) : Boolean is do
		result := (self.getMetaClass.equals(cl))
	end
	
	method get(~property : kermeta::reflection::Property) : kermeta::reflection::Object from kermeta::reflection::Object is do
		result := extern fr::irisa::triskell::kermeta::runtime::language::Object.get(self, ~property)
	end
	
	method ~set(~property : kermeta::reflection::Property, element : kermeta::reflection::Object) : Void from kermeta::reflection::Object is do
		if element.isInstanceOf(~property.type) then
			extern fr::irisa::triskell::kermeta::runtime::language::Object.~set(self, ~property, element)
		else
			var e : kermeta::exceptions::IncompatibleTypeError init kermeta::exceptions::IncompatibleTypeError.new
			e.message := "IncompatibleTypeError : Cannot set property " + ~property.name + " of object " + self.toString + " to " + element.toString + "."
			raise e
		end
	end
	
	method isSet(~property : kermeta::reflection::Property) : Boolean from kermeta::reflection::Object is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.isSet(self, ~property)
	end
	
	method unset(~property : kermeta::reflection::Property) : Void from kermeta::reflection::Object is do
		extern fr::irisa::triskell::kermeta::runtime::language::Object.unSet(self, ~property)
	end
	
	method oid() : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.oid(self)
	end
	
	method toString() : String is do
		result := "[" + self.getMetaClass.typeDefinition.qualifiedName + ":" + oid.toString + "]"
	end
	
	method isFrozen() : Boolean is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.isFrozen(self)
	end
	
	method isVoid() : Boolean is do
		result := false
	end
	
	method hashcode() : Integer is do		
		// usually this method is not called when using hashtable because it directly tries the method hashcode in the RuntimeObject.java
		// which fall back here only if the method is not redefined
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.hashcode(self)
	end
	
	method freeze() : Void is do
		extern fr::irisa::triskell::kermeta::runtime::language::Object.freeze(self)
	end
	
	method asType(type : kermeta::reflection::Class) : kermeta::reflection::Object is do
		if isInstanceOf(type) then result := self
		else raise kermeta::exceptions::TypeCastError.new
		end
	end
	
	method isInstanceOf(type : kermeta::reflection::Type) : Boolean is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.isInstanceOf(self, type)
	end
	
}
alias Boolean : kermeta::standard::Boolean;
alias Integer : kermeta::standard::Integer;


abstract class ParameterizedType inherits Type, kermeta::reflection::ParameterizedType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitParameterizedType(self, context)
		end

}
class ModelType inherits Type, TypeDefinition, kermeta::reflection::ModelType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitModelType(self, context)
		end

	method new() : kermeta::reflection::Model is
		do
			result ?= extern fr::irisa::triskell::kermeta::runtime::language::ModelType.newObject(self)
		end
	method isModelTypeOf(m : kermeta::reflection::Model) : Boolean is
		do
			result ?= extern fr::irisa::triskell::kermeta::runtime::language::ModelType.isModelTypeOf(self, m)
		end
}

class Class inherits ParameterizedType, kermeta::reflection::Class, KMStructureVisitable
{
	method new() : kermeta::reflection::Object from kermeta::reflection::Class is 
	do
		result := extern fr::irisa::triskell::kermeta::runtime::language::Class.newObject(self)
	end

	method clone(objectToClone : Object) : Object is
	do
		result := extern fr::irisa::triskell::kermeta::runtime::language::Class.cloneObject(self, objectToClone)
	end
	
	method deepClone(objectToClone : Object) : Object is
	do
		result := extern fr::irisa::triskell::kermeta::runtime::language::Class.deepCloneObject(self, objectToClone)
	end
	/*
	method isInstance(object : kermeta::reflection::Object) : Boolean is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Class.isInstance(self, object)
	end 
	*/
	method isSubType(object : kermeta::reflection::Type) : Boolean is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Class.isSubType(self, object)
	end
	
	method equals(other : kermeta::reflection::Object) : Boolean from Object is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Class.equals(self, other)
	end
	
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
	do
		result := visitor.visitClass(self, context)
	end
}


class MultiplicityElement inherits TypedElement, kermeta::reflection::MultiplicityElement, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitMultiplicityElement(self, context)
		end
}
class Constraint inherits kermeta::reflection::Constraint,NamedElement,kermeta::reflection::Operation, KMStructureVisitable
{
	attribute language : ConstraintLanguage
	attribute stereotype : ConstraintType
	
	attribute body : kermeta::language::behavior::Expression
	
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitConstraint(self, context)
		end
}
enumeration ConstraintLanguage {
	kermeta;
	ocl;
}
enumeration ConstraintType {
	~pre;
    ~post;
    ~inv;
}
class Operation inherits MultiplicityElement, kermeta::reflection::Operation, KMStructureVisitable
{
	attribute body : kermeta::language::behavior::Expression

	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitOperation(self, context)
		end
}
class Tag inherits Object, kermeta::reflection::Tag, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitTag(self, context)
		end
}
class Property inherits MultiplicityElement, kermeta::reflection::Property, KMStructureVisitable
{

	attribute getterBody : kermeta::language::behavior::Expression
	attribute setterBody : kermeta::language::behavior::Expression
	
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitProperty(self, context)
		end
}
abstract class TypeContainer inherits Object, kermeta::reflection::TypeContainer
{
}
abstract class VirtualTypeContainer inherits Object, kermeta::reflection::VirtualTypeContainer
{
}
class GenericTypeDefinition inherits TypeDefinition, kermeta::reflection::GenericTypeDefinition, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitGenericTypeDefinition(self, context)
		end
}
class ClassDefinition inherits TypeContainer, GenericTypeDefinition, kermeta::reflection::ClassDefinition, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitClassDefinition(self, context)
		end
}
class TypedElement inherits TypeContainer, NamedElement, kermeta::reflection::TypedElement, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitTypedElement(self, context)
		end
}
abstract class TypeVariable inherits Type, NamedElement, kermeta::reflection::TypeVariable, KMStructureVisitable
{
}
class ObjectTypeVariable inherits TypeContainer, TypeVariable, kermeta::reflection::ObjectTypeVariable, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitObjectTypeVariable(self, context)
		end
}
class VirtualType inherits ObjectTypeVariable, kermeta::reflection::VirtualType, KMStructureVisitable {
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitVirtualType(self, context)
		end
}
class ModelTypeVariable inherits TypeVariable, TypeContainer, VirtualTypeContainer, kermeta::reflection::ModelTypeVariable {
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitModelTypeVariable(self, context)
		end
}
class Model inherits Object, kermeta::reflection::Model {
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitModel(self, context)
		end
	method filter(typeName : kermeta::reflection::Type) : set kermeta::reflection::Object[0..*] is
		do
			result := contents.select { o | typeName.isInstance(o) }.asSet
		end
	method add(obj : kermeta::reflection::Object) : Void is
		do
			// This is only implemented as an extern so that at some point in the future, a runtime
			// typecheck might be added in the event that un-modeltype-safe adds start occurring
			
			// Cute hack. The java code for add is hard if contents hasn't already been accessed. So...
			contents
			extern fr::irisa::triskell::kermeta::runtime::language::Model.add(self, obj)
		end
	method remove(obj : kermeta::reflection::Object) : Void is
		do
			// This is not really a hack.
			extern fr::irisa::triskell::kermeta::runtime::language::ReflectiveCollection.remove(contents, obj)
		end
	method addAllCompatible(objectsToAdd : kermeta::standard::Collection<Object>) : kermeta::standard::Collection<Object> is do
		// version writen in java: don't know if this is faster enough to worth it ...
		// Cute hack. The java code for add is hard if contents hasn't already been accessed. So...
		//contents
		//extern fr::irisa::triskell::kermeta::runtime::language::Model.addAllCompatible(self, objectsToAdd)
		
		// Let's do it in kermeta
		result := kermeta::standard::Set<Object>.new
		objectsToAdd.each { obj | 
			var addedObject : kermeta::reflection::Object init addCompatible(obj) 
			if(not addedObject.isVoid) then
				result.add(obj)
			end
		} 
	end
	
	method addCompatible(objectToAdd : kermeta::reflection::Object) : kermeta::reflection::Object is do
		// Cute hack. The java code for addCompatible is hard if contents hasn't already been accessed. So...
		contents
		extern fr::irisa::triskell::kermeta::runtime::language::Model.addCompatible(self, objectToAdd)
	end
}
class ProductType inherits TypeContainer, Type, kermeta::reflection::ProductType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitProductType(self, context)
		end
}

class FunctionType inherits TypeContainer, Type, kermeta::reflection::FunctionType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitFunctionType(self, context)
		end
}

class Type inherits Object, kermeta::reflection::Type, KMStructureVisitable
{
	
	operation clone (objectToClone : Object) : Object is abstract


	operation deepClone (objectToClone : Object) : Object is abstract


	method isInstance(element : kermeta::reflection::Object) : Boolean from kermeta::reflection::Type is do
		result := element.isInstanceOf(self)
	end


	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitType(self, context)
		end
}


class TypeVariableBinding inherits TypeContainer, Object, kermeta::reflection::TypeVariableBinding, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitTypeVariableBinding(self, context)
		end
}
class PrimitiveType inherits TypeContainer, DataType, kermeta::reflection::PrimitiveType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
	do
		result := visitor.visitPrimitiveType(self, context)
	end
			
	method clone(objectToClone : Object) : Object is abstract		

}

class NamedElement inherits Object, kermeta::reflection::NamedElement, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitNamedElement(self, context)
		end
}

/**
class SelfType inherits Type, kermeta::reflection::SelfType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitSelfType(self, context)
		end
} */
class TypeDefinitionContainer inherits NamedElement, kermeta::reflection::TypeDefinitionContainer, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitTypeDefinitionContainer(self, context)
		end
}

class Package inherits TypeDefinitionContainer, kermeta::reflection::Package, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitPackage(self, context)
		end
}
class TypeDefinition inherits NamedElement, kermeta::reflection::TypeDefinition, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitTypeDefinition(self, context)
		end
}
class VoidType inherits Type, kermeta::reflection::VoidType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitVoidType(self, context)
		end
}
class DataType inherits TypeDefinition, Type, kermeta::reflection::DataType, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitDataType(self, context)
		end
	
	method clone(objectToClone : Object) : Object is abstract
	
}
class Enumeration inherits DataType, kermeta::reflection::Enumeration, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitEnumeration(self, context)
		end

	method clone(objectToClone : Object) : Object is
	do
		raise "NOT IMPLEMENTED"
	end
}

class EnumerationLiteral inherits NamedElement, kermeta::reflection::EnumerationLiteral, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitEnumerationLiteral(self, context)
		end
}

class Parameter inherits MultiplicityElement, kermeta::reflection::Parameter, KMStructureVisitable
{
	method acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType from kermeta::language::structure::KMStructureVisitable is
		do
			result := visitor.visitParameter(self, context)
		end
}
/** Alias to kermeta::standard::UnlimitedNatural */
alias UnlimitedNatural : kermeta::standard::UnlimitedNatural;

/** Implementation of the Visitor pattern for the model elements contained in structure package */
abstract class KMStructureVisitor<ContextType, ResultType>
{
	operation visitClass(node : Class, context : ContextType) : ResultType is do end
	operation visitMultiplicityElement(node : MultiplicityElement, context : ContextType) : ResultType is do end
	operation visitOperation(node : Operation, context : ContextType) : ResultType is do end
	operation visitTag(node : Tag, context : ContextType) : ResultType is do end
	operation visitProperty(node : Property, context : ContextType) : ResultType is do end
	operation visitClassDefinition(node : ClassDefinition, context : ContextType) : ResultType is do end
	operation visitTypedElement(node : TypedElement, context : ContextType) : ResultType is do end
	operation visitObjectTypeVariable(node : ObjectTypeVariable, context : ContextType) : ResultType is do end
	operation visitVirtualType(node : VirtualType, context : ContextType) : ResultType is do end
	operation visitModelTypeVariable(node : ModelTypeVariable, context : ContextType) : ResultType is do end
	operation visitModel(node : Model, context : ContextType) : ResultType is do end
	operation visitProductType(node : ProductType, context : ContextType) : ResultType is do end
	operation visitFunctionType(node : FunctionType, context : ContextType) : ResultType is do end
	operation visitType(node : Type, context : ContextType) : ResultType is do end
	operation visitTypeVariableBinding(node : TypeVariableBinding, context : ContextType) : ResultType is do end
	operation visitPrimitiveType(node : PrimitiveType, context : ContextType) : ResultType is do end
	operation visitNamedElement(node : NamedElement, context : ContextType) : ResultType is do end
	//operation visitSelfType(node : SelfType, context : ContextType) : ResultType is do end
	operation visitPackage(node : Package, context : ContextType) : ResultType is do end
	operation visitTypeDefinition(node : TypeDefinition, context : ContextType) : ResultType is do end
	operation visitVoidType(node : VoidType, context : ContextType) : ResultType is do end
	operation visitDataType(node : DataType, context : ContextType) : ResultType is do end
	operation visitEnumeration(node : Enumeration, context : ContextType) : ResultType is do end
	operation visitEnumerationLiteral(node : EnumerationLiteral, context : ContextType) : ResultType is do end
	operation visitParameter(node : Parameter, context : ContextType) : ResultType is do end
	operation visitGenericTypeDefinition(node : GenericTypeDefinition, context : ContextType) : ResultType is do end
	operation visitParameterizedType(node : ParameterizedType, context : ContextType) : ResultType is do end
	operation visitModelType(node : ModelType, context : ContextType) : ResultType is do end
	operation visitTypeDefinitionContainer(node : TypeDefinitionContainer, context : ContextType) : ResultType is do end
	operation visitConstraint(node : Constraint, context : ContextType) : ResultType is do end
	
}
/** All the classes defined in this <code>kermeta::language::structure</code> package 
 * inherit this class. Part of Visitor pattern implementation. */
abstract class KMStructureVisitable
{
	operation acceptKMStructureVisitor<ContextType, ResultType>(visitor : KMStructureVisitor<ContextType, ResultType>, context : ContextType) : ResultType is abstract
}
