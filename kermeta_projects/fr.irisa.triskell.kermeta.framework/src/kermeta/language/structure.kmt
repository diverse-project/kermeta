/* $Id: structure.kmt,v 1.79 2009-02-16 15:55:50 cfaucher Exp $
 * Project : framework
 * File : structure.kmt
 * License : EPL
 * Copyright : IRISA / INRIA / Universite de Rennes 1
 * ----------------------------------------------------------------------------
 * Creation date : Jul 04, 2005
 * Authors :
 *  Franck Fleurey  ffleurey@irisa.fr 
 *  Zoe Drey 		zdrey@irisa.fr
 *  Jean-Philippe Thibault 		jpthibault@irisa.fr
 */
 
/**
 * Contains the concrete implementation (including the operation bodies) 
 * of the classes of the <code>kermeta::reflection</code> package.
 * If you need more documentation, please take a look at the
 * <code>kermeta::reflection</code> package which is more complete.
 * <img src="platform:/plugin/fr.irisa.triskell.kermeta.documentation/src/figures/language_structure_package.png"/>
 * <img src="platform:/plugin/fr.irisa.triskell.kermeta.documentation/src/figures/language_structure_visitor_view.png"/>
 */
@uri "http://www.kermeta.org/kermeta/1_2_0//kermeta/language/structure"
package kermeta::language::structure; 


require "../Standard.kmt"
require "platform:/resource/fr.irisa.triskell.kermeta.model/build/ecore/kermeta_java.ecore"

using kermeta::standard
using kermeta::exceptions

aspect class Object
{
	/**
	 * Returns the Class object that is the metaclass of current Object
	 */
	 @CompilerIgnore "true"
	operation getMetaClass() : Class from Object is do
		//@compiledJavaExtern "org.kermeta.compil.runtime.ExecutionContext.getInstance().getMetaClass( this.getClass().getName() )"
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.getMetaClass( this )"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.getMetaClass(self)
	end

	/**
	 * Returns the Object that contains current Object, void if the Object has
	 * no container
	 */
	@CompilerIgnore "true"
	operation container() : Object from Object is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.container(this)"
		result := extern fr::irisa::triskell::kermeta::runtime::language::Object.container(self)
	end

	/**
	 * Runs checking of invariants defined for the metaclass of the Object
	 * Also check the multiplicity of attributes (except derived and transient attributes)
	 */
	@CompilerIgnore "true"
	operation checkInvariants() : Void from Object is do
		var cd : ClassDefinition init self.getMetaClass.classDefinition
		if ( not cd.isVoid ) then
			cd.allAttribute.each{ p |
			    if not p.tag.exists{t | t.name == "ecore.isTransient"} and
			       not p.isDerived 
			    then
					var v : Object init self.get(p)
					if ( (not v.isVoid).andThen{ f| v.isInstanceOf(Collection<Object>)} ) then
						var c : Collection<Object> init v.asType(Collection<Object>)
						// A collection that does not respect the defined bounds raises an exception.
						if ( p.upper != -1 and c.size() > p.upper ) then
							var e : ConstraintViolatedInv init ConstraintViolatedInv.new
							e.constraintAppliedTo := self
							e.message := "Inv upper bound of " + cd.name + "." + p.name + " violated on " + self.toString
							raise e
						end
						if ( c.size() < p.lower ) then
							var e : ConstraintViolatedInv init ConstraintViolatedInv.new
							e.constraintAppliedTo := self
							e.message := "Inv lower bound of " + cd.name + "." + p.name + " violated on " + self.toString
							raise e
						end
					else 
						// A required property that is void raises an exception.
						if ( p.lower == 1 and v.isVoid ) then
							var e : ConstraintViolatedInv init ConstraintViolatedInv.new
							e.constraintAppliedTo := self
							e.message := "Inv lower bound of " + cd.name + "." + p.name + " violated on " + self.toString
							raise e
						end
					end
			    end
			}
		end
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.checkInvariants(this)"
		extern fr::irisa::triskell::kermeta::runtime::language::Object.checkInvariants(self)
	end
	
	/**
	 * Recursively runs checking of invariants defined for the metaclass of the Object
	 * and its supertypes
	 * Ignores derived attributes. 
	 */	
	@CompilerIgnore "true"
	operation checkAllInvariants() : Void from Object is do
		if(self!=void) then
			// Call the invariant verification
			self.checkInvariants
			// Invariant checking on each value of self properties			
			var cd : ClassDefinition
			cd ?= self.getMetaClass.typeDefinition
			cd.allAttribute.each { prop |
				if ( prop.isComposite and not prop.isDerived) then
					if not self.get(prop).isVoid() then
						if self.get(prop).getMetaClass.typeDefinition.asType(ClassDefinition).allSuperTypes.exists{ t |
							if t.isInstanceOf(ParameterizedType) then
								t.asType(ParameterizedType).typeDefinition.qualifiedName=="kermeta::standard::Collection"
							else
								false
							end } then
						
						/*
						if ( self.get(prop).getMetaClass.typeDefinition.name.equals("ReflectiveSequence") 
							or self.get(prop).getMetaClass.typeDefinition.name.equals("ReflectiveCollection") ) then
						*/
						
							var subObjInstances : kermeta::standard::Collection<Object>
							subObjInstances ?= self.get(prop)
							subObjInstances.each { aSubObjInstances | aSubObjInstances.checkAllInvariants }
						else
							self.get(prop).checkAllInvariants
						end
					end
				end
			}
		end
	end

	/**
	 * semantic equality,
	 * if you wish to test for object identity you need to use the method oid
	 * ex: x.oid == y.oid
	 * by default, (ie. if not overloaded), the equals method for class Object implements 
	 * the most discriminating possible equivalence relation on objects; that is, for any non-null 
	 * reference values x and y, this method returns true if and only if x and y refer to the same 
	 * object (x.oid == y.oid has the value true). 
	 *
	 * note1: the operator == is mapped to this 
	 * note2: overloading this operation have some impact on the behavior on collection and hashtable that rely on it
	 */
	@CompilerIgnore "true"
	operation equals(element : Object) : Boolean from Object is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.equals(this, element)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.equals(self, element)
	end
	
	/**
	 * See kermeta::language::structure::Object.equals(Object)
	 */
	@CompilerIgnore "true"
	operation isNotEqual(element : Object) : Boolean from Object is do
		result := not equals(element)
	end		
	
	/**
	 * Returns a Boolean stating whether the current Object is an instance of the given Class
	 */
	@CompilerIgnore "true"
	operation isKindOf(cl : Class) : Boolean is do
		result := (self.getMetaClass.equals(cl))
	end
	
	/** 
	 * Returns the instances of the given property for this Object.
	 *
	 * Example : 
	 * <pre>
	 * class A { reference attr : String }
	 * </pre>
	 * Using A :
	 * <pre>
	 * operation getAProp() is do
	 *    var a : A
	 *    var s : String
	 *    var the_attr : Property init self.getMetaClass.ownedAttribute.one
	 *    s ?= a.get(the_attr)
	 * end
	 * </pre>
	 * The user has to cast
	 * the result of this method according to the type and the upper multiplicity
	 * of this property. If upper multiplicity > 1, than the effective type of the 
	 * result is a Sequence<ThePropertyName>. Otherwise, the type corresponds to 
	 * the name of the given Property (i.e the type of the property instance).
	 */
	@CompilerIgnore "true"
	operation get(~property : Property) : Object from Object is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.get(this, property)"
		result := extern fr::irisa::triskell::kermeta::runtime::language::Object.get(self, ~property)
	end
	
	/**
	 * Sets the <code>element</code> to the <code>~property</code> of the object 
	 */
	@CompilerIgnore "true"
	operation ~set(~property : Property, element : Object) : Void from Object is do
		if element.isInstanceOf(~property.type) then
			@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.set(this, property, element)"
			extern fr::irisa::triskell::kermeta::runtime::language::Object.~set(self, ~property, element)
		else
			var v : kermeta::language::structure::EnumerationLiteral
			v ?= element
			if (v != void).andThen{ e | ~property.type == v.~enumeration } then
				@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.set(this, property, v)"
				extern fr::irisa::triskell::kermeta::runtime::language::Object.~set(self, ~property, v)
			else
				var e : kermeta::exceptions::IncompatibleTypeError init kermeta::exceptions::IncompatibleTypeError.new
				e.message := "IncompatibleTypeError : Cannot set property " + ~property.name + " of object " + self.toString + " to " + element.toString + "."
				raise e
			end
		end
	end
	
	/**
	 * True if the <code>~property</code> of the object has been set
	 */
	@CompilerIgnore "true"
	operation isSet(~property : Property) : Boolean from Object is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.isSet(this, property)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.isSet(self, ~property)
	end
	
	/**
	 * Remove the element set as the <code>~property</code> of the object.
	 * The <code>isSet(~property)</code> method will then return False
	 */
	@CompilerIgnore "true"
	operation unset(~property : Property) : Void from Object is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.unset(this, property)"
		extern fr::irisa::triskell::kermeta::runtime::language::Object.unSet(self, ~property)
	end
	
	/**
	 * Returns the unique Oid of the Object
	 */
	@CompilerIgnore "true"
	operation oid() : Integer is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.getOID(this)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.oid(self)
	end
	
	/**
 	 * Implements Object.toString()
 	 * Returns a String of form [qn:oid] where 'qn' is the qualified name of
 	 * the object type, and oid the unique ID of the object
 	 */
	@CompilerIgnore "true"
	operation toString() : String is do
		result := "[" + self.getMetaClass.typeDefinition.qualifiedName + ":" + oid.toString + "]"
	end

	/**
	 * Returns a Boolean stating whether the Object is currently in a
	 * frozen state
	 */
	@CompilerIgnore "true"
	operation isFrozen() : Boolean is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.isFrozen(this)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.isFrozen(self)
	end

	/**
	 * Returns a Boolean stating whether the Object is Void
	 */
	@CompilerIgnore "true"
	operation isVoid() : Boolean is do
		result := false
	end

	/**
	 * code used in hashtable in order to identify an object in the hashtable keys
	 * This system is similar to the hashcode used in java. Please refer to java documentation
	 * for more information about hashcode
	 */
	@CompilerIgnore "true"
	operation hashcode() : Integer is do		
		// usually this method is not called when using hashtable because it directly tries the method hashcode in the RuntimeObject.java
		// which fall back here only if the method is not redefined
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.hashcode(this)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.hashcode(self)
	end

	/**
	 * Freeze the Object
	 */
	@CompilerIgnore "true"
	operation freeze() : Void is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.freeze(this)"
		extern fr::irisa::triskell::kermeta::runtime::language::Object.freeze(self)
	end

	/**
	 * Implementation of OCL like cast. It returns self if the object object conforms to the type given as parameter.
	 */
	@CompilerIgnore "true"
	operation asType(type : Class) : Object is do
		if isInstanceOf(type) then result := self
		else raise kermeta::exceptions::TypeCastError.new
		end
	end

	/**
	 * Returns a Boolean stating whether the current Object conforms to given Type
	 * This means: is this object an instance of this type, or is it an instance of its subtype 
	 */
	@CompilerIgnore "true"
	operation isInstanceOf(type : Type) : Boolean is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.isInstanceOf(this, type.getClass().getName())"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.isInstanceOf(self, type)
	end
	
	/**
	 * Returns the Resource currently containing (directly or indirectly) the Object 
	 * or void if the object belongs to no Resource
	 */
	@CompilerIgnore "true"
	operation containingResource() : kermeta::persistence::Resource is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ObjectUtil.getContainingResource(this)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Object.getContainingResource(self)
	end
}

aspect class ModelType
{

	/**
	 * To be written ##########################################
	 */
	@CompilerIgnore "true"
	operation new() : Model is
		do
			@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ModelTypeUtil.newObject(this)"
			result ?= extern fr::irisa::triskell::kermeta::runtime::language::ModelType.newObject(self)
		end

	/**
	 * To be written ##########################################
	 */
	@CompilerIgnore "true"
	operation isModelTypeOf(m : Model) : Boolean is
		do
			@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ModelTypeUtil.isModelTypeOf(this, m)"
			result ?= extern fr::irisa::triskell::kermeta::runtime::language::ModelType.isModelTypeOf(self, m)
		end
		
}

aspect class Class
{

	property readonly ownedAttribute : set Property[0..*]
		getter is do
			var cDef : ClassDefinition
			cDef ?= typeDefinition
			result := cDef.ownedAttribute
		end
		
	property readonly ownedOperation : set Operation[0..*]
		getter is do
			var cDef : ClassDefinition
			cDef ?= typeDefinition
			result := cDef.ownedOperation
		end
		
	property readonly superClass : set Class[0..*]
		getter is do
			var cDef : ClassDefinition
			cDef ?= typeDefinition
			var supertypes : OrderedSet<Class> init OrderedSet<Class>.new
			cDef.superType.each{ elem | 
				if ( Class.isInstance(elem) ) then
					var c : Class
					c ?= elem
					supertypes.add(c)
				end
			}
			result := supertypes
		end
		
	property readonly isAbstract : Boolean
		getter is do
			var cDef : ClassDefinition
			cDef ?= typeDefinition
			result := cDef.isAbstract
		end
		
	/**
	 * Returns the ClassDefinition for this class
	 */
	@CompilerIgnore "true"
	property readonly classDefinition : ClassDefinition
		getter is do
			result ?= typeDefinition
		end
	
	property readonly name : String
		getter is do
			result := typeDefinition.name
		end

	/**
	 * Instantiates a new occurence for this Class
	 */
	@CompilerIgnore "true"
	operation new() : Object from Class is 
	do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ClassUtil.newObject(this.getTypeDefinition().qualifiedName().replace(\"::\", \".\"))"
		result := extern fr::irisa::triskell::kermeta::runtime::language::Class.newObject(self)
	end

	@CompilerIgnore "true"
	method clone(objectToClone : Object) : Object is
	do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ClassUtil.cloneObject(this, objectToClone)"
		result := extern fr::irisa::triskell::kermeta::runtime::language::Class.cloneObject(self, objectToClone)
	end

	@CompilerIgnore "true"
	method deepClone(objectToClone : Object) : Object is
	do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ClassUtil.deepCloneObject(this, objectToClone)"
		result := extern fr::irisa::triskell::kermeta::runtime::language::Class.deepCloneObject(self, objectToClone)
	end

	@CompilerIgnore "true"
	method equals(other : Object) : Boolean from kermeta::language::structure::Object is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ClassUtil.equals(this, other)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Class.equals(self, other)
	end
	
	/**
	 * Returns the String representation of this class. 
	 *  Note : throws an exception if this class has no classDefinition
	 *  or if the classDefinition is not well constructed.
	 */
	@CompilerIgnore "true"
	method toString() : String is do
		result := self.typeDefinition.name
	end
}

aspect class ClassDefinition
{
	/**
	 * Returns all the types directly and indirectly inherited by this ClasDefinition 
	 */
	operation allSuperTypes() : Set<Type> is do
	 
		var allSuperClasses : Set<Type> init Set<Type>.new
        var superClasses : Set<Type> init Set<Type>.new
        superClasses.addAll(self.superType)
        
        from  not superClasses.isEmpty
        until superClasses.isEmpty
        loop
            allSuperClasses.addAll(superClasses)
            var temp : Set<Type> init Set<Type>.new
             superClasses.each{ s |
             	if s.isInstanceOf(ParameterizedType) then
					if s.asType(ParameterizedType).typeDefinition.isInstanceOf(ClassDefinition) then
                		temp.addAll(s.asType(ParameterizedType).typeDefinition.asType(ClassDefinition).superType)
                	end
                end
             }
             superClasses.clear()
             superClasses.addAll(temp)
        end
        
        result := allSuperClasses
	end
		
	/**
	 * Returns all the Attributes, References, derived Properties of this 
	 * ClassDefinition including the inherited ones
	 */
	property readonly allAttribute : set Property[0..*]
		getter is do
 		    result := kermeta::standard::Set<Property>.new
 		    result.addAll(self.ownedAttribute)
		    self.superType.each{ t | 
		    	if Class.isInstance(t) then
		    		var c : Class
		    		c ?= t
		    		var cDef : ClassDefinition
			    	cDef ?= c.typeDefinition
		    		cDef.allAttribute.each{ p | 
						result.add(p)
					}
		    	end
			}
		end
		
	/**
	 * Returns all the Operations of this ClassDefinition
	 * including the inherited ones
	 */
	property readonly allOperation : set Operation[0..*]
		getter is do
 		    result := kermeta::standard::Set<Operation>.new
 		    result.addAll(self.ownedOperation)
		    self.superType.each{ t | 
		    	if Class.isInstance(t) then
		    		var c : Class
		    		c ?= t
		    		var cDef : ClassDefinition
			    	cDef ?= c.typeDefinition
		    		cDef.allOperation.each{ p | 
						result.add(p)
					}
		    	end
			}
	end
}

aspect class Model {

 	/**
 	 * Returns a set of all the elements of the model that are instance of the given Type
 	 */
	operation filter(typeName : Type) : Set<Object> is
		do
			result := contents.select { o | typeName.isInstance(o) }.asSet
		end

 	/**
 	 * Add an object to the model. According to the ModelType, the typechecker will statically verify if the object can be added or not
 	 */ 
	@CompilerIgnore "true"
	operation add(obj : Object) : Void is
		do
			// This is only implemented as an extern so that at some point in the future, a runtime
			// typecheck might be added in the event that un-modeltype-safe adds start occurring
			
			// Cute hack. The java code for add is hard if contents hasn't already been accessed. So...
			contents
			@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ModelUtil.add(this, obj)"
			extern fr::irisa::triskell::kermeta::runtime::language::Model.add(self, obj)
		end

 	/**
 	 * Remove an object from the model
 	 */
	@CompilerIgnore "true"
	operation remove(obj : Object) : Void is
		do
			// This is not really a hack.
			@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ReflectiveCollectionUtil.remove(contents, obj)"
			extern fr::irisa::triskell::kermeta::runtime::language::ReflectiveCollection.remove(contents, obj)
		end

 	/**
 	 * Add all the Object of the collection that are copmpatible with the ModelDefinition. Other objects are ignored.
 	 * Returns the list of Object that have been added (One can check that some element have been ignored or not)
 	 */
	@CompilerIgnore "true"
	operation addAllCompatible(objectsToAdd : kermeta::standard::Collection<Object>) : kermeta::standard::Collection<Object> is do
		// version writen in java: don't know if this is faster enough to worth it ...
		// Cute hack. The java code for add is hard if contents hasn't already been accessed. So...
		//contents
		//extern fr::irisa::triskell::kermeta::runtime::language::Model.addAllCompatible(self, objectsToAdd)
		
		// Let's do it in kermeta
		result := kermeta::standard::Set<Object>.new
		objectsToAdd.each { obj | 
			var addedObject : Object init addCompatible(obj) 
			if(not addedObject.isVoid) then
				result.add(obj)
			end
		} 
	end

 	/**
 	 * Add the Object if it is copmpatible with the ModelDefinition. Other objects are ignored.
 	 * Returns the object if it has been added (One can check that some element have been ignored or not)
 	 * return Void if not added
 	 */
	@CompilerIgnore "true"
	operation addCompatible(objectToAdd : Object) : Object is do
		// Cute hack. The java code for addCompatible is hard if contents hasn't already been accessed. So...
		contents
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.ModelUtil.addCompatible(this, objectToAdd)"
		result := extern fr::irisa::triskell::kermeta::runtime::language::Model.addCompatible(self, objectToAdd)
	end
}

aspect class Type
{
	/**
	 * Returns a copy of passed Object built by recursively copying attribute
	 * properties, and referencing original reference properties values
	 */
	operation clone (objectToClone : Object) : Object is abstract

	/**
	 * Returns a copy of passed Object built by recursively copying values of
	 * all properties, no matter the property kind
	 */
	operation deepClone (objectToClone : Object) : Object is abstract

	/**
	 * Implements Type.isInstance(Object)
	 */
	@CompilerIgnore "true"
	operation isInstance(element : Object) : Boolean from Type is do
		result := element.isInstanceOf(self)
	end
	
	/**
 	 * Implements Type.isSuperTypeOf(Type)
 	 * For technical reason, the current implementation works only with Type that comes from a kermeta declaration (ie. in a require)
 	 * it won't work with Type created programmaticaly by the user. If you need that, please ask to the kermeta developpers
 	 * to extend it.
 	 */
	@CompilerIgnore "true"
	operation isSuperTypeOf(object : Type) : Boolean is do
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.language.TypeUtil.hasSubType(this, object)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::language::Type.hasSubType(self, object)
	end
	
	/**
 	 * Implements Type.isSubTypeOf(Type)
 	 * For technical reason, the current implementation works only with Type that comes from a kermeta declaration (ie. in a require)
 	 * it won't work with Type created programmaticaly by the user. If you need that, please ask to the kermeta developpers
 	 * to extend it.
 	 */
	@CompilerIgnore "true"
	operation isSubTypeOf(object : Type) : Boolean is do
		result := object.isSuperTypeOf(self)
	end
}

aspect abstract class NamedElement
{
	/**
	 * Returns the qualified name of this named element. Qualified name is 
	 * the list of the names of the packages hierarchically ordered, delimited by
	 * a "::", followed by the name of this named element.
	 */
	@CompilerIgnore "true"
	operation qualifiedName() : String is do
		var elem : NamedElement init self
		result := self.name
		
		//We must check if the container is a NamedElement, because the root is a ModelingUnit that is not a NamedElement
		if elem.container.isInstanceOf(NamedElement) then
		
			from elem ?= elem.container
			until elem == void 
			loop
				result := elem.name + "::" + result
				
				if elem.container.isInstanceOf(NamedElement) then
					elem ?= elem.container
				else
					elem := void
				end
				
			end
			
		end
	end	
}

aspect class Enumeration
{

	/**
	 * Implements kermeta::language::structure::Type.clone(Object)
	 */
	@CompilerIgnore "true"
	method clone(objectToClone : Object) : Object is
	do
		raise "NOT IMPLEMENTED"
	end
}

@CompilerIgnore "true"
aspect class FunctionType {
	/**
	 * Right part of the FunctionType cannot be a ProductType
	 */
	inv resultType_must_not_be_a_ProductType is not right.isKindOf(ProductType)
}

