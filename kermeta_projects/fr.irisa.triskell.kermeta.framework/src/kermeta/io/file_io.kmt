/**
 * Contains file input/output related classes. 
 * <img src="platform:/plugin/fr.irisa.triskell.kermeta.documentation/src/figures/io_package.png"/>
 */
@uri "http://www.kermeta.org/kermeta/1_2_0//kermeta/io"
package kermeta::io;

require "../Standard.kmt"

using kermeta::standard
using kermeta::language::structure
using kermeta::exceptions

/* *************************************************
 * Franck FLEUREY - 01/03/2007
 * Triskell Research Group
 * IRISA / University of Rennes 1 (France)
 * Distributed under the terms of the EPL licence
 *
 * Implementation of Kermeta File Helper.
 *************************************************** */
 
/**
 * Implementation of simple helpers to read ans write text files
 * <img src="platform:/plugin/fr.irisa.triskell.kermeta.documentation/src/figures/io_package.png"/>
 */
@CompilerIgnore "true"
class FileIO
{
	/**
	 * Writes a String in a text file
	 */ 
	operation writeTextFile(filename : String, text : String) : Void is do
		if filename == void then raise FileNotFoundException.new end
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.io.SimpleFileIOUtil.writeTextFile(filename, text)"
		extern fr::irisa::triskell::kermeta::runtime::io::SimpleFileIO.writeTextFile(filename, text)
	end
	
	/**
	 * Writes a String in a text file by using a specific encoding
	 */ 
	operation writeTextFileWithEncoding(filename : String, text : String, encoding : String) : Void is do
		if filename == void then raise FileNotFoundException.new end
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.io.SimpleFileIOUtil.writeTextFileWithEncoding(filename, text, encoding)"
		extern fr::irisa::triskell::kermeta::runtime::io::SimpleFileIO.writeTextFileWithEncoding(filename, text, encoding)
	end
	
	/**
	 * Reads a text file
	 */ 
	operation readTextFile(filename : String) : String is do
		if filename == void then raise FileNotFoundException.new end
		var exists : Boolean
		var isdirectory : Boolean
		
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.io.SimpleFileIOUtil.fileExists(filename)"
		exists ?= extern fr::irisa::triskell::kermeta::runtime::io::SimpleFileIO.fileExists(filename)
		
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.io.SimpleFileIOUtil.fileIsDirectory(filename)"
		isdirectory ?= extern fr::irisa::triskell::kermeta::runtime::io::SimpleFileIO.fileIsDirectory(filename)
		if not exists then 
			var e : FileNotFoundException init FileNotFoundException.new
			e.message := filename
			raise e 
		end
		if isdirectory then 
			var e : FileNotFoundException init FileNotFoundException.new
			e.message := filename + " is not a file"
			raise e 
		end
		
		@compiledJavaExtern "org.kermeta.compil.runtime.helper.io.SimpleFileIOUtil.readTextFile(filename)"
		result ?= extern fr::irisa::triskell::kermeta::runtime::io::SimpleFileIO.readTextFile(filename)
	end

}
// FileIO
