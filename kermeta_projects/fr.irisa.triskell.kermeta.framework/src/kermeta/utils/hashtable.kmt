/* *************************************************
 * Franck FLEUREY - 14/02/2005
 * Triskell Research Group
 * IRISA / University of Rennes 1 (France)
 * Distributed under the terms of the EPL licence
 *
 * Implementation of kermeta hashtable types.
 *************************************************** */
 
/** 
 * Contains special classes (Hashtable, Stack) that are not contained in Kermeta
 * metamodel
 * 
 * <img src="./figures/utils_package.png"/>
 */
 @uri "http://www.kermeta.org/kermeta/utils"
package kermeta::utils;

require "../Standard.kmt"

using kermeta::standard

/**
 * An implementation of hashtable
 * This implementation is for speed optimisation it cannot be serialized. 
 * If your wish to serialize its content you need to copy its content into a classical ecore structure (attribute or references)
 * <img src="platform:/plugin/fr.irisa.triskell.kermeta.documentation/src/figures/utils_package.png"/>
 */
class Hashtable<K, V>
{
	/**
	 * Returns the size of this Hashtable
	 */
	operation size() : Integer is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::basetypes::Map.size(self)
	end

	/**
	 * Returns an iterator over the Keys of this Hashtable
	 */
	operation keyIterator() : Iterator<K> is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::basetypes::Map.keysIterator(self)
	end
	
	/**
	 * Returns an iterator over the values of this Hashtable
	 */
	operation valueIterator() : Iterator<V> is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::basetypes::Map.valueIterator(self)
	end
	
	/**
	 * Returns the value for key 'key' if it exists, otherwise returns Void
	 */
	operation getValue(key : K) : V is do
		result ?= extern fr::irisa::triskell::kermeta::runtime::basetypes::Map.get(self,key)
	end
	
	/**
	 * Adds a entry into the Hashtable
	 */
	operation put(key : K, ~value : V) : Void is do
		extern fr::irisa::triskell::kermeta::runtime::basetypes::Map.put(self,key, ~value)
	end
	
	/**
	 * Removes the entry with key equals to 'key'
	 */
	operation remove(key : K) : Void is do
		extern fr::irisa::triskell::kermeta::runtime::basetypes::Map.remove(self,key)
	end
	
	/**
	 * Removes all elements from the Hashtable
	 */
	operation clear() : Void is do
		extern fr::irisa::triskell::kermeta::runtime::basetypes::Map.clear(self)
	end
	
	/**
	 * Returns a set containing the hash keys of this Hashtable
	 */
	operation keys() : Set<K> is do
		result := Set<K>.new
		from var it : Iterator<K> init keyIterator until it.isOff
		loop
			result.add(it.next)
		end
	end
	
	/**
	 * Returns a collection containing the elements of this Hashtable
	 */
	operation values() : Collection<V> is do
		result := Bag<V>.new
		keys.each { k | result.add(self.getValue(k)) }
	end
	
	/**	
	 * Returns a Boolean stating whether a key is present or not in this Hashtable
	 */
	operation containsKey(key : K) : Boolean is do
		result := getValue(key) != void
	end
	
	// TODO: complete this class
	// The rest might be writable in metacore directly
}