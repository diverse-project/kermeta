/* $Id: genericSmartAdapters.kmt,v 1.5 2007-12-07 07:15:14 bmorin Exp $
 * Creation date: July 17, 2007
 * License:
 * Copyright:
 * Authors: bmorin
 */

package org;

require "platform:/resource/SmartAdapters/metamodel/generic/genericSmartAdaptersBase.kmt"
require "platform:/resource/SmartAdapters/program/weaving/converter.kmt"

using kermeta::standard
using kermeta::utils
using kermeta::persistence
using kermeta::exceptions
using patternframework
using org::smartadapters::core
using org::smartadapters::extension
using ecore
using org::smartadapters::utils

package smartadapters
{
	package core
	{
		@aspect "true"
		class Adapter
		{	
			/**
			key : aspect element
			value : associated clone
			*/
			attribute clone : Hashtable<Object, Object>
			attribute cloned : Set<Object>
			attribute binding : Hashtable<Object, Object>
			attribute convert : Hashtable<kermeta::reflection::Class,kermeta::reflection::Class>
			attribute enum : Hashtable<kermeta::reflection::Enumeration,kermeta::reflection::Enumeration>
			attribute created : Hashtable<Object, Object>
			attribute converter : Converter
			
			operation initConvert() : Void is abstract
			
			operation apply(b:Hashtable<Object, Object>) is do
				binding:=b
				if(clone==void) then
					initClone 
				end
				adapt.each{a |
					stdio.writeln("#>"+a.getMetaClass.name+"...") 
					a.execute()
					stdio.writeln("#>done!")
				}
				updateClones
			end
					
				
			/**
			initClone must be called before the first binding
			*/
			operation initClone() is do
				//stdio.writeln("initClone...")
				initConvert
				clone:=Hashtable<Object,Object>.new
				cloned:=Set<Object>.new
				created:=Hashtable<Object,Object>.new
				if(aspect.structure!=void) then
				aspect.structure.content.each{elt |
							//stdio.writeln("elt: "+elt.toString)
							var newCloned:Object
							newCloned:=converter.getConverted(elt)
							if(newCloned!=void) then
								clone.put(elt, newCloned)
							end
						}
				end
				//stdio.writeln("initClone: END!")
			end
			
			/**
			updateClone must be called after every binding
			*/
			operation updateClones() is do
				//stdio.writeln("updateClones...")
				if(aspect.structure!=void) then
				aspect.structure.content.each{o | 
					if(not(aspect.persistent.contains(o))) then
						//stdio.writeln("updateClone: "+o.toString)
						updateClone(o,void)
					end}
				end
				//stdio.writeln("updateClones: END!")
			end
			
			operation updateClone(pObj : Object, template : Object) is do
				//stdio.writeln("  updateClone..."+pObj.toString+", "+template.toString)
				clone.remove(pObj)
				converter.deleteConverted(pObj)
				var newCloned:Object
				if(template!=void) then
					cloned.remove(template)
					newCloned:=Object.clone(template)
				else
					newCloned:=converter.getConverted(pObj)
				end
				if(newCloned!=void) then
					clone.put(pObj, newCloned)
					if(template!=void) then
						cloned.add(template)
					end
				end
				//stdio.writeln("  updateClone: END!")
			end

			operation cloneAgain(o : Object, template : Object, type : uniqueType)	: Void is do
				var toClone : Boolean init type==uniqueType.non_unique
				if(not(toClone) and type==uniqueType.unique) then
					//stdio.writeln("?>unique")
					if(template!=void) then
						toClone := not(cloned.contains(getRealObject(template,false)))
					else
						toClone := clone.getValue(o)==void
					end
				end
				if(not(toClone) and type==uniqueType.onDemand) then
					var input : String init stdio.read("$>clone "+o.toString+" (true/false) :")
			        toClone := input.toBoolean
				end
				if(toClone) then
					//stdio.writeln("?>updating clones...")
					if(template!=void) then
						updateClone(o,getRealObject(template,false))
					else
						updateClone(o,void)
					end
				end
			end
			
			operation setMultipleProperty(prty : Object, o : Object) : Void is do
				//stdio.writeln("  setProperty: "+prty.toString+", "+o.toString)
				if(o != void) then
					var coll : Collection<kermeta::reflection::Object>
					var collToSet : Collection<kermeta::reflection::Object>
					coll ?= o
					collToSet ?= prty
					if(coll != void and collToSet != void and coll.size > 0) then
						var realColl : Collection<kermeta::reflection::Object>
						realColl ?= getRealObject(o,true)
						collToSet.addAll(realColl)
					end
				end
			end
			
			operation setSingleProperty(o : Object, old : Object) : Object is do
				if(o != void) then
					result := getRealObject(o,false)
				else
					result := old
				end
			end

			operation unsetSingleProperty(o : Object, old : Object) : Object is do
				if(o != void) then
					stdio.writeln("?>unset: 1")
					result := void
				else
					stdio.writeln("?>unset: 2 "+getRealObject(o,false).toString)
					result := old
				end
			end

			operation unsetMultipleProperty(prty : Object, o : Object) : Void is do
				//stdio.writeln("unsetProperty: "+prty.toString+", "+o.toString)
				if(prty != void and o != void) then
					var coll : Collection<kermeta::reflection::Object>
					var collToSet : Collection<kermeta::reflection::Object>
					coll ?= o
					collToSet ?= prty
					if(coll != void and collToSet != void) then
						//stdio.writeln("  unsetProperty: Collection")
						var realColl : Collection<kermeta::reflection::Object>
						realColl ?= getRealObject(o,true)
						realColl.each{o |
							collToSet.remove(o)
						}
					end
				end
			end			
						
			operation getRealObject(obj : Object, isCollect:Boolean) : Object is do
				if(isCollect) then
					var col:Collection<kermeta::reflection::Object>
					col?=obj
					var res:Set<Object> init Set<Object>.new
					col.each{o | res.add(getRealObject(o,false))}
					result:=res
				else
					var realObj:Object
					realObj:=clone.getValue(obj)
					if(realObj!=void) then
						result:=realObj
					else
						realObj:=binding.getValue(obj)
						if(realObj!=void) then
							result:=realObj
						else
							realObj:=created.getValue(obj)
							if(realObj!=void) then
								result:=realObj
							else
								var pObj:PObject
								pObj?=obj
								if(pObj!=void) then
									result:=converter.getConverted(obj)
								else
									result:=obj
								end
							end
						end
					end	
				end		
			end
		}
	}
	
	package extension
	{
		@aspect "true"
		class DerivableAdapter inherits Adapter
		{
			operation resolve(derivation:Derivation):Adapter is do
				var adapter:DerivableAdapter init DerivableAdapter.new
				var isChecked:Boolean init true
				constraints.each{c | isChecked:=isChecked and c.check(derivation)}
				if(isChecked) then
					adapt.each{a | adapter.addAdaptation(derivation,a)}
					alternatives.each{a | adapter.addAlternative(derivation,a)}
					if(adapter.isConcretizable) then
						var concreteAdapter:Adapter init Adapter.new
						converter.addEntry(adapter.getMetaClass,concreteAdapter.getMetaClass)
						adapter.adapt.each{a | converter.addEntry(a.getMetaClass, a.getMetaClass)}
						if(converter==void) then
							converter:=Converter.new
						end
						concreteAdapter?=converter.getConverted(adapter)
						result:=concreteAdapter
					else
						result:=adapter
					end					
				else
					var e:NonApplicableDerivation init NonApplicableDerivation.new
					e.display
					raise e
				end
			end
			
			operation addAdaptation(d:Derivation, a:Adaptation) is do
				if(not(a.isOptional) or d.selectedOptions.contains(a)) then
					adapt.add(a)
				end
			end
			
			operation addConjunction(d:Derivation, c:EntityConjunction) is do
				if(not(c.isOptional) or d.selectedOptions.contains(c)) then
					c.~and.each{e |
						var a:Adaptation
						a?=e
						if(a!=void) then
							addAdaptation(d,a)
						else
							var alt:EntityAlternative
							alt?=e
							if(alt!=void) then
								addAlternative(d,alt)
							else
								var conj:EntityConjunction
								conj?=e
								if(conj!=void) then
									addConjunction(d,conj)
								end
							end
						end
					}
				end
			end
			
			operation addAlternative(d:Derivation, alt:EntityAlternative) is do
				if(d.elements.exists{e | e.alternative==alt}) then
					var v:AdapterElement 
					var elt:DerivationElement
					elt:=d.elements.select{e | e.alternative==alt}.one
					v:=elt.selectedVariant
					var a:Adaptation
					a?=v
					if(a!=void) then
						addAdaptation(d,a)
					else
					   var altern:EntityAlternative
					   altern?=v
					   if(altern!=void) then
					   		addAlternative(d,altern)
					   else
					   		var c:EntityConjunction
					   		c?=v
					   		if(c!=void) then
					   			addConjunction(d,c)
					   		end
					   end
					end

				else
					if(not(alt.isOptional) or d.selectedOptions.contains(alt)) then
						self.alternatives.add(alt)
					end
				end
			end

			
			operation isConcretizable() : ecore::EBoolean is do
				result:=(alternatives.size==0)
			end			
		}
		
		class NonApplicableDerivation inherits kermeta::exceptions::Exception {
			operation display() is do
				message:="Derivation is not applicable to DerivableAdapter. Maybe the derivation is not well formed.\n
				Please check that the derivation respects all the explicit and implicit constraints..."			
			end
		}		
	}
}

