/* $Id: genericSmartAdapters.kmt,v 1.12 2008-02-20 17:52:57 bmorin Exp $
 * Creation date: July 17, 2007
 * License:
 * Copyright:
 * Authors: bmorin
 */

package org;

require "platform:/resource/SmartAdapters/metamodel/generic/genericSmartAdaptersBase.kmt"
require "platform:/resource/SmartAdapters/program/weaving/converter.kmt"
 

using kermeta::standard
using kermeta::utils
using kermeta::persistence
using kermeta::exceptions
using patternframework
using org::smartadapters::core
using org::smartadapters::extension
using ecore
using org::smartadapters::utils

package smartadapters
{
	package core
	{
		aspect class Adapter
		{	
			/**
			key : aspect element
			value : associated clone
			*/
			attribute clone : Hashtable<Object, Object>
			attribute binding : Hashtable<Object, Object>
			attribute cloned : Set<Object>
			attribute convert : Hashtable<kermeta::language::structure::Class,kermeta::language::structure::Class>
			attribute enum : Hashtable<kermeta::language::structure::Enumeration,kermeta::language::structure::Enumeration>
			attribute converter : Converter
			
			operation initConvert() : Void is abstract
			
			/*
			This operation weaves the the aspect for the binding b
			In other words, it executes all the adaptations of the composition protocol  
			*/
			operation apply(b:Hashtable<Object, Object>) is do
				var i : Integer init 0
				binding := b
				adapt.each{a |
					if not reverse.contains(a) then
						stdio.writeln("Adaptation "+i.toString)
						stdio.writeln("------------")
						stdio.writeln("#>weaving: "+a.getMetaClass.name+"...")
						a.getMetaClass().ownedAttribute.each{p |
							stdio.writeln("#>  "+p.name+": "+a.get(p).toString)
						}
						a.execute(true)
						stdio.writeln("#>weaving: done!")
						stdio.writeln("")
						i := i+1
					end
				}
				//updateClones
			end
			
			/*
			This operation unweaves the previously woven aspect, for the current binding
			In other words, it performs a roll back.
			*/
			operation unweave() is do	
				var i : Integer init 0
				reverse.each{a | 
					stdio.writeln("Adaptation "+i.toString)
					stdio.writeln("------------")
					stdio.writeln("#>unweaving: "+a.getMetaClass.name+"...")
					a.getMetaClass().ownedAttribute.each{p |
						stdio.writeln("#>  "+p.name+": "+a.get(p).toString)
					} 
					a.execute(false)
					stdio.writeln("#>unweaving: done!")
					stdio.writeln("")
					i := i+1
					adapt.remove(a)
				}
				reverse.clear
			end
			
			operation initAdapter() : Void is 
			do
				if (clone == void) then
					clone := Hashtable<Object, Object>.new
				end
				if (convert == void) then
					initConvert
				end
				if (cloned == void) then
					cloned := Set<Object>.new
				end
			end		
				
			/**
			initClone must be called before the first binding
			*/
			operation initClone() is do
				initAdapter
				if (~aspect.structure!=void) then
					~aspect.structure.content.each{elt |
							var newCloned:Object
							newCloned:=converter.getConverted(elt)
							if (newCloned!=void) then
								clone.put(elt, newCloned)
							end
					}
				end
			end
			
			/**
			updateClone must be called after every binding
			*/
			operation updateClones() is do
				if (~aspect.structure!=void) then
					~aspect.structure.content.each{o | 
						if (not(~aspect.persistent.contains(o))) then
							updateClone(o,void)
						end
					}
				end
			end
			
			operation updateClone(handler : Object, template : Object) is do
				clone.remove(handler)
				converter.deleteConverted(handler)
			
				var realHandler : Object
				var realTemplate : Object
			
				if (template != void) then
					realTemplate := getRealObject(template,false)
					realHandler:=Object.clone(realTemplate)
					cloned.remove(realTemplate)
				else
					realHandler:=converter.getConverted(handler)
				end
				if (realHandler != void) then
					clone.put(handler, realHandler)
					if (template != void) then
						cloned.add(realTemplate)
					end
				end
			end

			operation cloneAgain(handler : Object, template : Object, type : uniqueType)	: Void is do
				var toClone : Boolean init type == uniqueType.non_unique
				if (not(toClone) and type == uniqueType.unique) then
					if (template != void) then
						toClone := not(cloned.contains(getRealObject(template,false)))
						clone.remove(handler)
						clone.put(handler, getRealObject(template,false))
					else
						toClone := not(clone.containsKey(handler)) 
					end
				end
				if (not(toClone) and type == uniqueType.onDemand) then
					var input : String init stdio.read("$>clone "+handler.toString+" (true/false) :")
			        toClone := input.toBoolean
				end
				if (toClone) then
					if(template != void) then
						updateClone(handler,template)
					else
						updateClone(handler,void)
					end
				end
			end
			
			operation setMultipleProperty(prty : Object, o : Object) : Void is do
				if(o != void) then
					var coll : Collection<kermeta::language::structure::Object>
					var collToSet : Collection<kermeta::language::structure::Object>
					coll ?= o
					collToSet ?= prty
					if(coll != void and collToSet != void and coll.size > 0) then
						var realColl : Collection<kermeta::language::structure::Object>
						realColl ?= getRealObject(o,true)
						collToSet.addAll(realColl)
					end
				end
			end
			
			operation setSingleProperty(o : Object, old : Object) : Object is do
				if(o != void) then
					result := getRealObject(o,false)
				else
					result := old
				end
			end

			operation unsetSingleProperty(o : Object, old : Object) : Object is do
				if(o != void) then
					result := void
				else
					result := old
				end
			end

			operation unsetMultipleProperty(prty : Object, o : Object) : Void is do
				if(prty != void and o != void) then
					var coll : Collection<kermeta::language::structure::Object>
					var collToSet : Collection<kermeta::language::structure::Object>
					coll ?= o
					collToSet ?= prty
					if(coll != void and collToSet != void) then
						var realColl : Collection<kermeta::language::structure::Object>
						realColl ?= getRealObject(o,true)
						realColl.each{o |
							collToSet.remove(o)
						}
					end
				end
			end			
						
			operation getRealObject(obj : Object, isCollect:Boolean) : Object is do
				if (isCollect) then
					var col : Collection<kermeta::language::structure::Object>
					col ?= obj
					var res : Set<Object> init Set<Object>.new
					col.each{o | res.add(getRealObject(o,false))}
					result := res
				else
					var realObj : Object
					realObj := clone.getValue(obj)
					if (realObj != void) then
						result := realObj
					else
						realObj := binding.getValue(obj)
						if (realObj != void) then
							result := realObj
						else
							var pObj : PObject
							pObj ?= obj
							if (pObj != void) then
								result := converter.getConverted(obj)
							else
								result := obj
							end
						end
					end
				end			
			end
		}
	}
	
	package extension
	{
		aspect class DerivableAdapter inherits Adapter
		{
			operation resolve(derivation:Derivation) : Adapter is do
				var adapter : DerivableAdapter init DerivableAdapter.new
				var isChecked : Boolean init true
				constraints.each{c | isChecked:=isChecked and c.check(derivation)}
				if(isChecked) then
					adapt.each{a | adapter.addAdaptation(derivation,a)}
					alternatives.each{a | adapter.addAlternative(derivation,a)}
					if(adapter.isConcretizable) then
						var concreteAdapter:Adapter init Adapter.new
						if converter == void then
							converter := Converter.new
							converter.initConverter(Hashtable<kermeta::language::structure::Class,kermeta::language::structure::Class>.new)
						end
						converter.addEntry(adapter.getMetaClass,concreteAdapter.getMetaClass)
						adapter.adapt.each{a | converter.addEntry(a.getMetaClass, a.getMetaClass)}
						concreteAdapter ?= converter.getConverted(adapter)
						result := concreteAdapter
					else
						result := adapter
					end					
				else
					var e:NonApplicableDerivation init NonApplicableDerivation.new
					e.message := "Derivation "+derivation.toString+" is not applicable to DerivableAdapter "+self.toString+". \n
					Maybe the derivation is not well formed. \n
					Please check that the derivation respects all the explicit and implicit constraints..."	
					raise e
				end
			end
			
			operation addAdaptation(d:Derivation, a:Adaptation) is do
				if(not(a.isOptional) or d.selectedOptions.contains(a)) then
					adapt.add(a)
				end
			end
			
			operation addConjunction(d:Derivation, c:EntityConjunction) is do
				if(not(c.isOptional) or d.selectedOptions.contains(c)) then
					c.~and.each{e |
						var a:Adaptation
						a?=e
						if(a!=void) then
							addAdaptation(d,a)
						else
							var alt:EntityAlternative
							alt?=e
							if(alt!=void) then
								addAlternative(d,alt)
							else
								var conj:EntityConjunction
								conj?=e
								if(conj!=void) then
									addConjunction(d,conj)
								end
							end
						end
					}
				end
			end
			
			operation addAlternative(d:Derivation, alt:EntityAlternative) is do
				if(d.elements.exists{e | e.alternative==alt}) then
					var v:AdapterElement 
					var elt:DerivationElement
					elt:=d.elements.select{e | e.alternative==alt}.one
					v:=elt.selectedVariant
					var a:Adaptation
					a?=v
					if(a!=void) then
						addAdaptation(d,a)
					else
					   var altern:EntityAlternative
					   altern?=v
					   if(altern!=void) then
					   		addAlternative(d,altern)
					   else
					   		var c:EntityConjunction
					   		c?=v
					   		if(c!=void) then
					   			addConjunction(d,c)
					   		end
					   end
					end

				else
					if(not(alt.isOptional) or d.selectedOptions.contains(alt)) then
						self.alternatives.add(alt)
					end
				end
			end

			
			operation isConcretizable() : ecore::EBoolean is do
				result:=(alternatives.size==0)
			end			
		}
		
		class NonApplicableDerivation inherits kermeta::exceptions::Exception {
			
		}		
	}
}

