/* $Id:$ 
 * Creation : February 4, 2010
 * Licence  : EPL 
 * Copyright:
 * Authors  : 
 *            ffouquet
 */

package org::kermeta::komparator;


require kermeta
require "platform:/resource/org.kermeta.art.model/metamodel/art.ecore"
require "platform:/resource/org.kermeta.ArtDeployer/src/kermeta/AbstractArtDeployCommand.kmt"
require "DerivePropertyArtAspect.kmt"

using kermeta::persistence
using kermeta::standard
using kermeta::io
using art2
using org::kermeta::art::deployer

class Komparator
{
	operation kompare(runtimeModel : art2::ContainerRoot, updateModel : art2::ContainerRoot , deployNodeName : String , factory : org::kermeta::art::deployer::AbtractCommandFactory) : OrderedSet<org::kermeta::art::deployer::Command>  is do 
	
		result := OrderedSet<org::kermeta::art::deployer::Command>.new

		
		if(runtimeModel.isVoid)
		then 
			//VOID MODEL USE CASE
			if(updateModel.isVoid)
				then stdio.writeln("KOMPARATOR INFO : Both input model are void , nothing to do")
			else
			updateModel.nodes.each{node|
				if(node.name.equals(deployNodeName)) 
				then 
					result.addAll(initNode(node,factory))
				end
			}
			end
		else
			//STEP 0 - FOUND LOCAL NODE
			var actualRuntimeLocalNode : ContainerNode init runtimeModel.nodes.detect{c| c.name.equals(deployNodeName) } 
			var actualUpdateLocalNode : ContainerNode init updateModel.nodes.detect{c| c.name.equals(deployNodeName) }
			
			if( (actualRuntimeLocalNode == void) and (actualUpdateLocalNode != void))
			then 
				//STOP ALL PREVIOUS THINGS RUNNING ON NODE
				//TODO
				//INIT NODE
				result.addAll(initNode(actualUpdateLocalNode,factory))
			end
			if( (actualRuntimeLocalNode!=void) and (actualUpdateLocalNode!= void))
			then
				//PERFORM DIF
				result.addAll(updateNode(actualRuntimeLocalNode,actualUpdateLocalNode,factory))
			end
		end
	end
	
	operation initNode(node : ContainerNode,factory : AbtractCommandFactory) : OrderedSet<org::kermeta::art::deployer::Command> is do
		result := OrderedSet<org::kermeta::art::deployer::Command>.new
		stdio.writeln("DEBUG : INIT NODE : "+node.name)
		
		var alreadyDeployComponentType : Set<ComponentType> init Set<ComponentType>.new
		
		node.components.each{c|
			var ct : ComponentType init c.componentType
			if(not(alreadyDeployComponentType.exists{ e | ct.name.equals(e.name) } ))
			then 
				stdio.writeln("DEBUG : PROCESS "+ct.name)
				alreadyDeployComponentType.add(ct)
				result.add(factory.createCommandAddComponentType(ct))
			end	
		}
		
		node.components.each{c|
			result.add(factory.createCommandAddComponentInstance(c))
		}
	end
	
	operation updateNode(runtimeNode : ContainerNode, updateNode : ContainerNode, factory : AbtractCommandFactory) : OrderedSet<org::kermeta::art::deployer::Command> is do
		result := OrderedSet<org::kermeta::art::deployer::Command>.new
		stdio.writeln("DEBUG : UPDATE NODE : "+updateNode.name)
		
		var runtimeComponentType : Set<ComponentType> init Set<ComponentType>.new
		var alreadyTestedUpdateComponentType : Set<ComponentType> init Set<ComponentType>.new
		
		//COMPONENT TYPE STEP
		runtimeNode.componentTypes.each{ rct |
			//SEARCH SIMILAR COMPONENT TYPE IN UPDATE MODEL
			var similarComponentType : ComponentType init updateNode.componentTypes.detect{uct| uct.isEquals(rct) }
			if(similarComponentType == void)
			then result.add(factory.createCommandRemoveComponentType(rct))
			end
		}
		
		updateNode.componentTypes.each{ uct |
			var similarComponentType : ComponentType init runtimeNode.componentTypes.detect{rct| rct.isEquals(uct) }
			if(similarComponentType == void)
			then result.add(factory.createCommandAddComponentType(uct))
			end
		}
		
		//COMPONENT INSTANCE STEP
		runtimeNode.components.each{ rc |
			var similarComponent : ComponentInstance init updateNode.components.detect{uc| uc.isEquals(rc) }
			if(similarComponent == void)
			then result.add(factory.createCommandRemoveComponentInstance(rc))
			else 
				if(rc.isUpdated(similarComponent))
				then result.add(factory.createCommandUpdateComponentInstance(similarComponent))
				end
			end
		}
		
		updateNode.components.each{ uc |
			var similarComponent : ComponentInstance init runtimeNode.components.detect{rc| rc.isEquals(uc) }
			if(similarComponent == void)
			then result.add(factory.createCommandAddComponentInstance(uc))
			end
		}		
	end
	
}






