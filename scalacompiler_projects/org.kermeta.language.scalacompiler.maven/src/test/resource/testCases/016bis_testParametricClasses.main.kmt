@mainClass "testPackageTestParametricClass::TestParametricClass"
@mainOperation "main"

package testPackageTestParametricClass;

require kermeta

using kermeta::standard
using kermeta::kunit
 
 class ParamClass<T,U>
 {
 	attribute x : T
 	attribute y : U
 	
 	operation set_x(elt : T) is do
 		x := elt
 	end

 	operation set_y(elt : U) is do
 		y := elt
 	end

 	operation get_x() : T is do
 		result := x
 	end

 	operation get_y() : U is do
 		result := y
 	end
 	
 	/** Create an instance of a type variable */
	operation callNewOnTypeParameter() : T is do
		result := T.new 
	end
	/** Clone an instance of a type variable */
	operation callCloneOnTypeParameter(param : T) : T is do
		result := T.clone(param)
	end
 }

class TestParametricClass inherits kermeta::kunit::TestCase
{    

	property names : String[*]
		getter is do
			result := OrderedSet<String>.new
		end

	operation test1() is do
		var l : Collection<String> init Bag<String>.new
		// check the metaclass
		assert(l.isKindOf(Bag<String>))
	end
	
	operation test2() is do
		var l : Collection<String> init Bag<String>.new
		// check sub-typing poliy
		assert(l.isInstanceOf(Collection<String>))
	end

	operation test3() is do
		assert(names.isInstanceOf(Collection<String>))
	end

	operation main() is do
	   	var tr : TestRunner init TestRunner.new
		tr.run(TestParametricClass)
		tr.printTestResult
	end

    operation test_main_operation() is do

     	var P1 : ParamClass<Integer,String> 
     	var P2 : ParamClass<String,Integer>
     	
     	P1 := ParamClass<Integer,String>.new
     	P2 := ParamClass<String,Integer>.new
     	P1.set_x(3)
     	P1.set_y("three")
     	P2.set_x("five")
     	P2.set_y(5)
     	assert(P1.get_x()==3)
     	assert(P1.get_y()=="three")
     	assert(P2.get_x()=="five")
     	assert(P2.get_y()==5)
     	P2.set_y(P1.get_x())
     	P2.set_x(P1.get_y())
     	assert(P2.get_x()=="three")
     	assert(P2.get_y()==3)
     end
     
    operation test_callNewOnTypeParameter() is do
     	var P1 : ParamClass<Integer,String>
     	P1 := ParamClass<Integer,String>.new
     	P1.callNewOnTypeParameter()
     end
     
     /** Clone an instance of a type variable */
	operation test_callCloneOnTypeParameter() : Void is do
		var P1 : ParamClass<String,String>
     	P1 := ParamClass<String,String>.new
		var s2 : String
		s2 := P1.callCloneOnTypeParameter("bla")
		assert("bla" == s2)
	end
     
     
}    
