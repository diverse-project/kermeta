package fsm;

require kermeta

using kermeta::standard

// Some data types mapped to the kermeta standard library
alias String : kermeta::standard::String;
alias Character : kermeta::standard::Character;


class FSM
{
    attribute ownedState : set State[0..*]#owningFSM
    reference initialState : State[1..1]
    reference currentState : State

    operation run(input : String) : String raises FSMException is do
        // reset if there is no current state
        if currentState == void then reset end
        // initialise result
        result := ""
        from var i : Integer init 0
        until input.size == i
        loop
            result := result + currentState.step(input.elementAt(i)).toString()
            i := i + 1
        end
    end
    
    operation reset() : Void is do
        currentState := initialState
    end
}

class State {
    attribute name : String
    reference owningFSM : FSM#ownedState
    attribute outgoingTransition : set Transition[0..*]#source
    reference incomingTransition : set Transition[0..*]#target
    
    operation step(c : Character) : Character 
        raises FSMException 
    is do
        // Get the valid transitions
        var validTransitions : Collection<Transition> 
        validTransitions :=    outgoingTransition.select { t |
t.input.equals(c) }
        // Check if there is one and only one valid transition
        if validTransitions.empty then raise NoTransition.new end
        if validTransitions.size > 1 then raise NonDeterminism.new end
        // fire the transition
        result := validTransitions.one.fire
    end
}

class Transition {
    reference source : State[1..1]#outgoingTransition
    reference target : State[1..1]#incomingTransition
    attribute output : Character
    attribute input : Character

    operation fire() : Character is do
        // update FSM current state
        source.owningFSM.currentState := target
        result := output
    end
}

abstract class FSMException {}
class NonDeterminism inherits FSMException {}
class NoTransition inherits FSMException {}